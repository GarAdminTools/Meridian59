% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
LadderMaintenance is UtilityFunctions

% A challenge ladder is a separated pool of players, ranked by wisdom, that can only interact with each other.
% They have their own economy - out of ladder items can't be picked up by them and they can't give items to normal players.
% No spells, buffs, AEs, etc can cross over. Separate wall element limits in a room.
% Players in ladder / non-ladder can see each other, mainly for cool purposes like watching fights, but also because mules can be used for spotting anyway.
% Monster killing with ladder + non-ladder is possible, but only because monster XP is split based on damage dealt anyway.
%     You can't really gain an advantage by building with out of ladder players.
% Players in a ladder join building groups separate from non-ladder players.
%
%
% Ladders also have special events, rules, and rewards.
% The first challenge ladder had specially crafted Marauders - aka PKs and hunters - active at unpredictable times.
%    Players may find themselves under attack by a variety of PKs while out in the world.
%    These PKs may also be pursued by a variety of hunters, who will appear shortly after to assist law-abiding citizens.
%    Be aware, players who are outlaws or murderers themselves will not be treated so kindly by these hunters!
%    Not all the PKs are red. Some have soldier shields, or ao3s/swords of the hunt, to challenge players participating in those systems.
%    Defeated Marauders generally drop the specialized gear they're using, plus something extra and unexpected...
%
%
%
% The second challenge ladder is Dangerous Treasure, with trapped loot boxes.
%
%
% The challenge ladder also has special drops available in certain places.
% These unique items offer powerful school-based abilities.
% For example, the Shadowstep Tabi boots grant Qor 6 players a new level 6 Qor spell called Sanguine Fervor,
%    which makes all of your attacks cause bleed for 10 seconds, but can only be cast once per minute.
%    Shadowstep Tabi boots also grant the passive abilities 'You are hidden while not in combat.' and 'You are immune to poison.'
% These unique items are obviously only available to ladder players.
%
% The character with the highest wisdom at the end of the challenge ladder gets a title and a Magic Spirit Helm.
% The top ten characters get Magic Bows.
% The top one hundred characters receive either Boots or a Staff.
% 

constants:

   include blakston.khd
   include protocol.khd

resources:

   no_ladder_players = \
      "There are no adventurers currently in the ladder competition."
   ranking_header = \
      "The top adventurers currently in the ladder competition have:"
   ranking_name_and_wisdom = \
      "Rank %i: %i wisdom"
      
   your_ranking_ladder = \
      "You are rank %d in the ladder competition."
   
   final_report_msg = \
      "Rank %i: %q, with %i wisdom."

classvars:

properties:

   plLadderPlayers = $
   plTopLadderPlayers = $
   
   plTopOneHundredLadderPlayers = $
   
   pbLadderEnabled = TRUE
   
   piLadderNum = 1
   
   plMarauderClasses = $
   plMarauderList = $
   
   piMarauderOnKillSpawnChanceOutOf = 200 %200
   piMarauderOnZoneSpawnChanceOutOf = 300 %300

   piLeaderboardCountdownHours = 288
   
   plIslandMarauderClasses = $
   plMainlandMarauderClasses = $
   plMarionToSeafarersMarauderClasses = $
   plObscureAreasMarauderClasses = $
   plDesertMarauderClasses = $
   plAnywhereMarauderClasses = $
   
   pbSpawnIslandMaraudersOnPlayers = FALSE

messages:

   Constructor()
   {
      Send(self,@Recreate);

      return;
   }

   Recreate()
   {
      Send(self,@BuildMarauderList);
      Send(self,@RecalculateLeaderboard);

      return;
   }

   NewDay()
   {
      return;
   }

   NewGameHour()
   {
      if NOT pbLadderEnabled
      {
         return;
      }

      piLeaderboardCountdownHours--;
      
      if piLeaderboardCountdownHours <= 0
      {
         Send(self,@RecalculateLeaderboard);
      }
      return;
   }
   
   RecalculateLeaderboard(who=$)
   {
      local lUsers, lLadderUsers, oPlayer, iHighestWisdom, oHighestPlayer, iCount;

      piLeaderboardCountdownHours = 288;
      
      lUsers = Send(SYS,@GetUsers);
      
      lLadderUsers = $;
      foreach oPlayer in lUsers
      {
         if Send(oPlayer,@GetLadderID) = 1
         {
            lLadderUsers = Cons(oPlayer,lLadderUsers);
         }
      }
      
      if lLadderUsers = $
      {
         return;
      }
      
      plTopLadderPlayers = $;
      iCount = 10;
      
      while iCount > 0
      {
         oHighestPlayer = $;
         iHighestWisdom = 0;
         foreach oPlayer in lLadderUsers
         {
            if Send(oPlayer,@GetAccumulatedWisdom) > iHighestWisdom
            {
               oHighestPlayer = oPlayer;
               iHighestWisdom = Send(oPlayer,@GetAccumulatedWisdom);
            }
         }
         plTopLadderPlayers = Cons([oHighestPlayer,iHighestWisdom],plTopLadderPlayers);
         lLadderUsers = DelListElem(lLadderUsers,oHighestPlayer);
         if lLadderUsers = $
         {
            return;
         }
         iCount--;
      }

      return;
   }

   GetLadderPlayers()
   {
      return plLadderPlayers;
   }
   
   AddLadderPlayer(oPlayer=$)
   {
      if plLadderPlayers = $
         OR NOT FindListElem(plLadderPlayers,oPlayer)
      {
         plLadderPlayers = Cons(oPlayer,plLadderPlayers);
         return TRUE;
      }
      return FALSE;
   }
   
   RemoveLadderPlayer(oPlayer=$)
   {
      if plLadderPlayers <> $
         AND FindListElem(plLadderPlayers,oPlayer)
      {
         plLadderPlayers = DelListElem(plLadderPlayers,oPlayer);
         return TRUE;
      }
      return FALSE;
   }
   
   IsOnLadder(oPlayer=$)
   {
      if plLadderPlayers <> $
         AND FindListElem(plLadderPlayers,oPlayer)
      {
         return TRUE;
      }
      return FALSE;
   }
   
   GetLadderEnabled()
   {
      return pbLadderEnabled;
   }

   GetLadderNum()
   {
      return piLadderNum;
   }
   
   DoLadderReport(who=$)
   {
      local lTopPlayer;

      if plTopLadderPlayers = $
      {
         Send(who,@MsgSendUser,#message_rsc=no_ladder_players);
         return;
      }
      
      Send(who,@MsgSendUser,#message_rsc=ranking_header);

      if Length(plTopLadderPlayers) >= 10
      {
         Send(who,@MsgSendUser,#message_rsc=ranking_name_and_wisdom,#parm1=1,#parm2=Nth(Nth(plTopLadderPlayers,10),2));
         Send(who,@MsgSendUser,#message_rsc=ranking_name_and_wisdom,#parm1=2,#parm2=Nth(Nth(plTopLadderPlayers,9),2));
         Send(who,@MsgSendUser,#message_rsc=ranking_name_and_wisdom,#parm1=3,#parm2=Nth(Nth(plTopLadderPlayers,8),2));
         Send(who,@MsgSendUser,#message_rsc=ranking_name_and_wisdom,#parm1=4,#parm2=Nth(Nth(plTopLadderPlayers,7),2));
         Send(who,@MsgSendUser,#message_rsc=ranking_name_and_wisdom,#parm1=5,#parm2=Nth(Nth(plTopLadderPlayers,6),2));
         Send(who,@MsgSendUser,#message_rsc=ranking_name_and_wisdom,#parm1=6,#parm2=Nth(Nth(plTopLadderPlayers,5),2));
         Send(who,@MsgSendUser,#message_rsc=ranking_name_and_wisdom,#parm1=7,#parm2=Nth(Nth(plTopLadderPlayers,4),2));
         Send(who,@MsgSendUser,#message_rsc=ranking_name_and_wisdom,#parm1=8,#parm2=Nth(Nth(plTopLadderPlayers,3),2));
         Send(who,@MsgSendUser,#message_rsc=ranking_name_and_wisdom,#parm1=9,#parm2=Nth(Nth(plTopLadderPlayers,2),2));
         Send(who,@MsgSendUser,#message_rsc=ranking_name_and_wisdom,#parm1=10,#parm2=Nth(Nth(plTopLadderPlayers,1),2));
      }

%      if FindListElem(plTopLadderPlayers,who)
%      {
%         Send(who,@MsgSendUser,#message_rsc=your_ranking_ladder,#parm1=FindListElem(plTopLadderPlayers,who));
%      }

      return;
   }
   
   BuildMarauderList()
   {
      plMarauderClasses = $;
      plMainlandMarauderClasses = $;
      plMarionToSeafarersMarauderClasses = $;
      plObscureAreasMarauderClasses = $;
      plDesertMarauderClasses = $;
      
      plIslandMarauderClasses = $;
      plIslandMarauderClasses = Cons([&MarauderArabella,60,3],plIslandMarauderClasses);
      plIslandMarauderClasses = Cons([&MarauderVallok,60,3],plIslandMarauderClasses);
      plIslandMarauderClasses = Cons([&MarauderKal,60,3],plIslandMarauderClasses);
      plIslandMarauderClasses = Cons([&MarauderAbu,60,3],plIslandMarauderClasses);
      plIslandMarauderClasses = Cons([&MarauderZett,60,3],plIslandMarauderClasses);
      plIslandMarauderClasses = Cons([&MarauderNog,60,3],plIslandMarauderClasses);

      
      plMainlandMarauderClasses = $;
      plMainlandMarauderClasses = Cons([&MarauderGeiLong,50,2], plMainlandMarauderClasses);
      plMainlandMarauderClasses = cons([&MarauderSeava,50,2],plMainlandMarauderClasses);
      plMainlandMarauderClasses = cons([&MarauderEvila,50,3],plMainlandMarauderClasses);
      plMainlandMarauderClasses = cons([&MarauderGon,50,2],plMainlandMarauderClasses);
      plMainlandMarauderClasses = cons([&MarauderSethBot,50,2],plMainlandMarauderClasses);
      plMainlandMarauderClasses = cons([&MarauderJamesBot,50,2],plMainlandMarauderClasses);
      plMainlandMarauderClasses = cons([&MarauderPsy,50,2],plMainlandMarauderClasses);
      plMainlandMarauderClasses = cons([&MarauderAlo,100,3],plMainlandMarauderClasses);
      plMainlandMarauderClasses = cons([&MarauderCynn,100,3],plMainlandMarauderClasses);
      plMainlandMarauderClasses = cons([&MarauderMutant,50,3],plMainlandMarauderClasses);
      
      plMarionToSeafarersMarauderClasses = $;
      plMarionToSeafarersMarauderClasses = Cons([&MarauderBobo,0,0], plMarionToSeafarersMarauderClasses);
      
      plObscureAreasMarauderClasses = $;
      plObscureAreasMarauderClasses = cons([&MarauderKat,100,5],plObscureAreasMarauderClasses);
      plObscureAreasMarauderClasses = cons([&MarauderRusco,100,5],plObscureAreasMarauderClasses);
      plObscureAreasMarauderClasses = cons([&MarauderRocket,100,5],plObscureAreasMarauderClasses);
      plObscureAreasMarauderClasses = cons([&MarauderSammy,100,5],plObscureAreasMarauderClasses);
      plObscureAreasMarauderClasses = Cons([&MarauderToxus,100,5], plObscureAreasMarauderClasses);
      plObscureAreasMarauderClasses = cons([&MarauderWorm,100,5],plObscureAreasMarauderClasses);
      plObscureAreasMarauderClasses = cons([&MarauderDannyDiablo,100,5],plObscureAreasMarauderClasses);
      
      plDesertMarauderClasses = $;
      plDesertMarauderClasses = cons([&MarauderJensen,125,12],plDesertMarauderClasses);
      plDesertMarauderClasses = cons([&MarauderSalem,125,12],plDesertMarauderClasses);
      plDesertMarauderClasses = cons([&MarauderDyvora,125,12],plDesertMarauderClasses);
      
      plAnywhereMarauderClasses = $;
      plAnywhereMarauderClasses = cons([&MarauderEvilChrisChan,100,3],plAnywhereMarauderClasses);
      plAnywhereMarauderClasses = cons([&MarauderEvilSunnyCatt,100,3],plAnywhereMarauderClasses);
      plAnywhereMarauderClasses = cons([&MarauderEvilJJ,125,5],plAnywhereMarauderClasses);
      plAnywhereMarauderClasses = cons([&MarauderEvilGar,125,12],plAnywhereMarauderClasses);
      plAnywhereMarauderClasses = Cons([&MarauderShindra,125,7], plAnywhereMarauderClasses);
      plAnywhereMarauderClasses = Cons([&MarauderJinora,125,7], plAnywhereMarauderClasses);
      plAnywhereMarauderClasses = Cons([&MarauderRonu,100,3], plAnywhereMarauderClasses);
      plAnywhereMarauderClasses = Cons([&MarauderDavinus,100,7], plAnywhereMarauderClasses);
      plAnywhereMarauderClasses = Cons([&MarauderLacie,100,7], plAnywhereMarauderClasses);
      plAnywhereMarauderClasses = cons([&MarauderShivron,100,7],plAnywhereMarauderClasses);
      plAnywhereMarauderClasses = Cons([&MarauderMoxie,50,5], plAnywhereMarauderClasses);
      plAnywhereMarauderClasses = Cons([&MarauderMoosico,50,5], plAnywhereMarauderClasses);
      
      plMarauderClasses = Cons(&MarauderGeiLong, plMarauderClasses);
      plMarauderClasses = Cons(&MarauderDavinus, plMarauderClasses);
      plMarauderClasses = Cons(&MarauderAbu, plMarauderClasses);
      plMarauderClasses = Cons(&MarauderZett, plMarauderClasses);
      plMarauderClasses = Cons(&MarauderVallok, plMarauderClasses);
      plMarauderClasses = Cons(&MarauderRonu, plMarauderClasses);
      plMarauderClasses = Cons(&MarauderMoxie, plMarauderClasses);
      plMarauderClasses = Cons(&MarauderMoosico, plMarauderClasses);
      plMarauderClasses = Cons(&MarauderLuganeon, plMarauderClasses);
      plMarauderClasses = Cons(&MarauderLacie, plMarauderClasses);
      plMarauderClasses = Cons(&MarauderKal, plMarauderClasses);
      plMarauderClasses = Cons(&MarauderShindra, plMarauderClasses);
      plMarauderClasses = Cons(&MarauderJinora, plMarauderClasses);
      plMarauderClasses = Cons(&MarauderToxus, plMarauderClasses);
      plMarauderClasses = Cons(&MarauderSeaMan, plMarauderClasses);
      plMarauderClasses = Cons(&MarauderBobo, plMarauderClasses);
      plMarauderClasses = cons(&MarauderKat,plMarauderClasses);
      plMarauderClasses = cons(&MarauderArabella,plMarauderClasses);
      plMarauderClasses = cons(&MarauderCynn,plMarauderClasses);
      plMarauderClasses = cons(&MarauderDannyDiablo,plMarauderClasses);
      plMarauderClasses = cons(&MarauderDyvora,plMarauderClasses);
      plMarauderClasses = cons(&MarauderAlo,plMarauderClasses);
      plMarauderClasses = cons(&MarauderEvila,plMarauderClasses);
      plMarauderClasses = cons(&MarauderEvilChrisChan,plMarauderClasses);
      plMarauderClasses = cons(&MarauderEvilJJ,plMarauderClasses);
      plMarauderClasses = cons(&MarauderGon,plMarauderClasses);
      plMarauderClasses = cons(&MarauderJamesBot,plMarauderClasses);
      plMarauderClasses = cons(&MarauderNog,plMarauderClasses);
      plMarauderClasses = cons(&MarauderRocket,plMarauderClasses);
      plMarauderClasses = cons(&MarauderWorm,plMarauderClasses);
      plMarauderClasses = cons(&MarauderRusco,plMarauderClasses);
      plMarauderClasses = cons(&MarauderSalem,plMarauderClasses);
      plMarauderClasses = cons(&MarauderSammy,plMarauderClasses);
      plMarauderClasses = cons(&MarauderSeava,plMarauderClasses);
      plMarauderClasses = cons(&MarauderSethBot,plMarauderClasses);
      plMarauderClasses = cons(&MarauderShivron,plMarauderClasses);
      plMarauderClasses = cons(&MarauderEvilGar,plMarauderClasses);
      plMarauderClasses = cons(&MarauderEvilSunnyCatt,plMarauderClasses);
      plMarauderClasses = cons(&MarauderMutant,plMarauderClasses);
      plMarauderClasses = cons(&MarauderTamisc,plMarauderClasses);
      plMarauderClasses = cons(&MarauderJensen,plMarauderClasses);
      plMarauderClasses = cons(&MarauderSabrienna,plMarauderClasses);
      return;
   }
   
   CheckForMarauderSpawnOnKill(who=$)
   {
      if NOT Send(who,@HasMovedRecentlyLong)
      {
         Post(self,@SpawnMarauders,#who=who,#bAmbush=TRUE);
         return;
      }

      if Send(who,@GetMarauderAntagonist)
      {
         if Random(1,(piMarauderOnKillSpawnChanceOutOf/3)) = 1
         {
            Post(self,@SpawnMarauders,#who=who);
         }
      }
      else if Random(1,piMarauderOnKillSpawnChanceOutOf) = 1
      {
         Post(self,@SpawnMarauders,#who=who);
      }
      return;
   }
   
   CheckForMaruderSpawnOnZone(who=$)
   {
      % Don't do this anymore. Lame gameplay style.
      return;
      if Send(who,@GetMarauderAntagonist)
      {
         if Random(1,(piMarauderOnZoneSpawnChanceOutOf/3)) = 1
         {
            Post(self,@SpawnMarauders,#who=who);
         }
      }
      if Random(1,piMarauderOnZoneSpawnChanceOutOf) = 1
      {
         Send(self,@SpawnMarauders,#who=who);
      }
      return;
   }

   SpawnMarauders(who=$,bAmbush=FALSE)
   {
      local iRID, oRoom, oBaseRoom, iBaseRID, iRegion, i, n, lNeighborRooms, lEntrances, bSpawned, lChosenEntrance, 
            iMarauderCount, iHealth, lFinalList, iEnlightenments, bChaosStratum;
      
      oRoom = Send(who,@GetOwner);
      
      if oRoom = $
      {
         return FALSE;
      }
      iRID = Send(oRoom,@GetRoomNum);
      iRegion = Send(oRoom,@GetRegion);
      if Send(SYS,@FindRoomByNum,#num=RID_CHAOS_STRATA_START) = Send(who,@GetOwner)
      {
         bChaosStratum = TRUE;
         oBaseRoom = Send(oRoom,@GetBaseRoom);
      }
      else
      {
         bChaosStratum = FALSE;
         oBaseRoom = oRoom;
      }
      
      iBaseRID = Send(oBaseRoom,@GetRoomNum);
      lNeighborRooms = $;
      foreach i in Send(oBaseRoom,@GetYellZone)
      {
         if i <> iBaseRID
         {
            lNeighborRooms = Cons(Send(SYS,@FindRoomByNum,#num=i),lNeighborRooms);
         }
      }
      
      foreach i in lNeighborRooms
      {
         foreach n in Send(i,@GetStandardExits)
         {
            if Nth(n,3) = iBaseRID
            {
               lEntrances = Cons([Nth(n,4),Nth(n,5)],lEntrances);
            }
         }
         foreach n in Send(i,@GetEdgeExits)
         {
            if Nth(n,2) = iBaseRID
            {
               lEntrances = Cons([Nth(n,3),Nth(n,4)],lEntrances);
            }
         }
      }
      
      bSpawned = FALSE;
      
      if lEntrances = $
      {
         return bSpawned;
      }

      iEnlightenments = Send(who,@GetNumReincarnations);
      iHealth = Send(who,@GetBaseMaxHealth);
      iMarauderCount = iHealth/50;
      
      if (iRegion = RID_KOCATAN
         AND pbSpawnIslandMaraudersOnPlayers)
         OR bAmbush
      {
         lChosenEntrance = [Send(who,@GetRow), Send(who,@GetCol)];
      }
      else
      {
         lChosenEntrance = Nth(lEntrances,Random(1,Length(lEntrances)));
      }
      
      if Send(who,@GetMarauderAntagonist)
      {
         iHealth = 500;
         iEnlightenments = 20;
      }
      
      lFinalList = $;
      foreach i in plMarionToSeafarersMarauderClasses
      {
         if iHealth >= Nth(i,2)
            AND iEnlightenments >= Nth(i,3)
         {
            lFinalList = Cons(i,lFinalList);
         }
      }

      switch(iRegion)
      {
         case RID_KOCATAN:
            foreach i in plIslandMarauderClasses
            {
               if iHealth >= Nth(i,2)
                  AND iEnlightenments >= Nth(i,3)
               {
                  lFinalList = Cons(i,lFinalList);
               }
            }
            break;
         case RID_DESERT:
            foreach i in plDesertMarauderClasses
            {
               if iHealth >= Nth(i,2)
                  AND iEnlightenments >= Nth(i,3)
               {
                  lFinalList = Cons(i,lFinalList);
               }
            }
            break;
         case RID_ORC_CAVE1:
            foreach i in plObscureAreasMarauderClasses
            {
               if iHealth >= Nth(i,2)
                  AND iEnlightenments >= Nth(i,3)
               {
                  lFinalList = Cons(i,lFinalList);
               }
            }
            break;
         case RID_BRAX:
            foreach i in plObscureAreasMarauderClasses
            {
               if iHealth >= Nth(i,2)
                  AND iEnlightenments >= Nth(i,3)
               {
                  lFinalList = Cons(i,lFinalList);
               }
            }
            break;
         case RID_DEFAULT:
            foreach i in plMainlandMarauderClasses
            {
               if iHealth >= Nth(i,2)
                  AND iEnlightenments >= Nth(i,3)
               {
                  lFinalList = Cons(i,lFinalList);
               }
            }
            break;
      }
      
      foreach i in plAnywhereMarauderClasses
      {
         if iHealth >= Nth(i,2)
            AND iEnlightenments >= Nth(i,3)
         {
            lFinalList = Cons(i,lFinalList);
         }
      }

      while iMarauderCount > 0
      {
         if Length(lFinalList) >= 1
         {
            Send(oRoom,@NewHold,#what=Create(First(Nth(lFinalList,Random(1,Length(lFinalList))))),
                                #new_row=Nth(lChosenEntrance,1),
                                #new_col=Nth(lChosenEntrance,2));
            bSpawned = TRUE;       
         }
         iMarauderCount--;
      }
       
%      foreach i in lEntrances
%      {
%         Send(oRoom,@NewHold,#what=Create(First(Nth(lFinalList,Random(1,Length(lFinalList))))),
%                             #new_row=Nth(i,1),
%                             #new_col=Nth(i,2));
%         bSpawned = TRUE;
%      }

      return bSpawned;
   }
   
   GetLadderPlayerCount()
   {
      local lUsers, lLadderUsers, oPlayer;
      
      lUsers = Send(SYS,@GetUsers);
      
      lLadderUsers = $;
      foreach oPlayer in lUsers
      {
         if Send(oPlayer,@GetLadderID) = 1
         {
            lLadderUsers = Cons(oPlayer,lLadderUsers);
         }
      }
      
      if lLadderUsers <> $
      {
         return Length(lLadderUsers);
      }
      
      return 0;
   }
   
   GetTopOneHundred()
   {
      local lUsers, lLadderUsers, oPlayer, iHighestWisdom, oHighestPlayer, iCount;
      
      lUsers = Send(SYS,@GetUsers);
      
      lLadderUsers = $;
      foreach oPlayer in lUsers
      {
         if Send(oPlayer,@GetLadderID) = 1
         {
            lLadderUsers = Cons(oPlayer,lLadderUsers);
         }
      }
      
      plTopOneHundredLadderPlayers = $;
      iCount = 105;
      
      while iCount > 0
      {
         oHighestPlayer = $;
         iHighestWisdom = 0;
         foreach oPlayer in lLadderUsers
         {
            if Send(oPlayer,@GetAccumulatedWisdom) > iHighestWisdom
            {
               oHighestPlayer = oPlayer;
               iHighestWisdom = Send(oPlayer,@GetAccumulatedWisdom);
            }
         }
         plTopOneHundredLadderPlayers = Cons([oHighestPlayer,iHighestWisdom],plTopOneHundredLadderPlayers);
         lLadderUsers = DelListElem(lLadderUsers,oHighestPlayer);
         if lLadderUsers = $
         {
            return;
         }
         iCount--;
      }

      return iCount;
   }
   
   DoFinalReport(to_whom=$)
   {
      local lPlayer, iCount;
      
      Send(self,@GetTopOneHundred);
      
      iCount = 105;
      foreach lPlayer in plTopOneHundredLadderPlayers
      {
         Send(to_whom,@MsgSendUser,#message_rsc=final_report_msg,#parm1=iCount,#type2=STRING_RESOURCE,#parm2=Send(First(lPlayer),@GetName),#parm3=Nth(lPlayer,2));
         iCount--;
      }
      
      return;
   }
   
   GiveRewards()
   {
      local lPlayer, iCount, oRewardItem;
      
      Send(self,@GetTopOneHundred);
      
      iCount = 105;
      foreach lPlayer in plTopOneHundredLadderPlayers
      {
         if Nth(lPlayer,2) >= 130000000
         {
            Send(First(lPlayer),@SetFinalLadderRank,#value=1);
         }
         else
         {
            Send(First(lPlayer),@SetFinalLadderRank,#value=iCount);
         }
         Send(First(lPlayer),@SetParticipatedInLadder,#value=1);
         
         if Random(1,2) = 1
         {
            oRewardItem = Create(&VoidstepTabi);
            Send(oRewardItem,@SetItemLevel,#iLevel=212);
         }
         else
         {
            oRewardItem = Create(&Scepter);
            Send(oRewardItem,@SetItemLevel,#iLevel=260);
         }
         Send(First(lPlayer),@NewHold,#what=oRewardItem);
         
         if iCount < 16
         {
            oRewardItem = Create(&MagicBow);
            Send(oRewardItem,@SetItemLevel,#iLevel=260);
            Send(oRewardItem,@SetColor,#iColor=XLAT_TO_BLUE);
            Send(First(lPlayer),@NewHold,#what=oRewardItem);
         }
         
         if Nth(lPlayer,2) >= 130000000
         {
            oRewardItem = Create(&Helm);
            Send(oRewardItem,@SetItemLevel,#iLevel=300);
            Send(First(lPlayer),@NewHold,#what=oRewardItem);
         }
         
         iCount--;
      }

      return;
   }
   
   ConvertAllToNonLadder()
   {
      local lUsers, lLadderUsers, oPlayer, iHighestWisdom, oHighestPlayer, iCount;
      
      lUsers = Send(SYS,@GetUsers);

      foreach oPlayer in lUsers
      {
         if Send(oPlayer,@GetLadderID) = 1
         {
            Send(oPlayer,@RevertToNonLadder);
         }
      }
      return;
   }
   
   GetMarauderList()
   {
      return plMarauderClasses;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
