% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
VectorWall is ActiveWallElement

constants:

   include blakston.khd
   
   % This type of wall does quick effect checks, then creates
   % another of itself at a different position and deletes itself
   % to give the illusion of motion.
   %
   % Can be sent a name, icon, and element.
   
   % This is high to avoid triggering. Vector walls check when they move
   VECTOR_EFFECT_INTERVAL = 300000
   
   VECTOR_MOVE_INTERVAL = 50

resources:

   VectorWall_desc_rsc = \
      "Look out!"
   
   freezing_winds_name = "freezing winds"
   freezing_winds_icon = poisoncl.bgf
   
   freezing_winds_damage0 = "You withstand the freezing winds unscathed."
   freezing_winds_damage5 = "Freezing winds chill you to the bone!"
   freezing_winds_damage10 = "Gales of elemental cold tear at your flesh!"
   freezing_winds_damage15 = "Icy daggers of air blacken your skin!"
   freezing_winds_damage20 = "~B~bYou are being frozen solid!"

   flamewave_damage0 = "You withstand the fire unscathed."
   flamewave_damage5 = "You feel a bit toasted from the flames."
   flamewave_damage10 = "Something smells like burnt hair.  Wait, it's you!"
   flamewave_damage15 = "You are on fire!  Put it out!"
   flamewave_damage20 = "Ouch, that really burned!"

   flamewave_illusion_damage0 = "These flames burn without heat!"
   flamewave_illusion_damage5 = "The heat is creeping upon you!"
   flamewave_illusion_damage10 = "Your flesh toasts and curls black before your eyes!"
   flamewave_illusion_damage15 = "The flames! The flames!"
   flamewave_illusion_damage20 = "You can feel yourself dying in the roaring flames!"

   freezing_winds_overall_sound = wind-howl-01.wav
   freezing_winds_hit_sound = ice-cracking-01.wav

   freezing_winds_dissipates = "The cold winds swirl ominously, but fail to coalesce."
   flamewave_dissipates = "A bit of the flamewave fades into coldness."

classvars:

   vrName = freezing_winds_name
   vrIcon = freezing_winds_icon
   vrDesc = VectorWall_desc_rsc

   viObject_flags = OF_NOEXAMINE

properties:

   viLightBonus = 0

   vrDissipateMessage = freezing_winds_dissipates
   vrUnaffectedMessage = freezing_winds_damage0

   piDrawEffectFlag = OF_FLICKERING
   piDrawFx = DRAWFX_TRANSLUCENT_50

   viIllusion = FALSE
   piMaxDamage = 0
   
   piRowChange = 1
   piColChange = 1
   piFineRowChange = 0
   piFineColChange = 0

   % Fine units
   piExplosionRange = 96

messages:

   Constructor(caster = $, duration = 200, MaxDamage = 0, illusionary = FALSE,row_change=0,col_change=0,fine_row_change=16,fine_col_change=16)
   {
      piMaxDamage = MaxDamage;
      viIllusion = illusionary;
   
      piRowChange = row_change;
      piColChange = col_change;
      piFineRowChange = fine_row_change;
      piFineColChange = fine_col_change;

      propagate;
   }

   Expire()
   {
      local oRoomData, iRow, iCol, iFineRow, iFineCol, iNewRow, iNewCol, iNewFineRow, iNewFineCol;
      
      ptExpire = $;
      
      if poOwner <> $
      {
         iRow = Send(self,@GetRow);
         iCol = Send(self,@GetCol);
         iFineRow = Send(self,@GetFineRow);
         iFineCol = Send(self,@GetFineCol);
      
         iNewRow = iRow + piRowChange;
         iNewCol = iCol + piColChange;
         iNewFineRow = iFineRow + piFineRowChange;
         iNewFineCol = iFineCol + piFineColChange;

         if iNewRow > 0
            AND iNewRow < Send(poOwner,@GetRoomRows)
            AND iNewCol > 0
            AND iNewCol < Send(poOwner,@GetRoomCols)
         {

            oRoomData = Send(poOwner,@GetRoomData);
            if CanMoveInRoomBSP(oRoomData,iRow,iCol,iFineRow,iFineCol,
                                iNewRow,iNewCol,iNewFineRow,iNewFineCol,
                                self,FALSE)
            {
               if NOT Send(SYS,@UtilGoNearSquare,#what=self,#where=poOwner,
                        #new_row=iNewRow,
                        #new_col=iNewCol,
                        #fine_row=iNewFineRow,
                        #fine_col=iNewFineCol)
               {
                  Send(self,@Explode);
                  return;
               }

               ptExpire = CreateTimer(self,@Expire,Send(self,@GetDuration));
               return;
%               Send(poOwner,@NewHold,#what=Create(&VectorWall,
%                                                   #caster=poCaster,
%                                                   #MaxDamage=piMaxDamage,
%                                                   #Illusionary=viIllusion,
%                                                   #row_change=piRowChange,
%                                                   #col_change=piColChange,
%                                                   #fine_row_change=piFineRowChange,
%                                                   #fine_col_change=piFineColChange),
%                                     #new_row=iNewRow,
%                                     #new_col=iNewCol,
%                                     #fine_row=iNewFineRow,
%                                     #fine_col=iNewFineCol);
            }
            else
            {
               Send(self,@Explode,
                     #new_row=iNewRow,
                     #new_col=iNewCol,
                     #fine_row=iNewFineRow,
                     #fine_col=iNewFineCol);
               return;
            }
         }
      }

      Send(self,@Delete);

      return;
   }

   Explode(new_row=0, new_col=0, fine_row=0, fine_col=0)
   {
      local i, each_obj;
      
      if poOwner = $
         OR NOT IsClass(poOwner,&Room)
      {
         Send(self,@Delete);
         return;
      }
      
      foreach i in Send(poOwner,@GetHolderActive)
      {
         each_obj = Send(poOwner,@HolderExtractObject,#data=i);
         if IsClass(each_obj,&Battler)
            AND NOT (IsClass(each_obj,&Player)
               AND Send(each_obj,@IsInCannotInteractMode))
         {
            if Send(each_obj,@SquaredFineDistanceToLocation,#row=new_row,
                                                            #col=new_col,
                                                            #fine_row=fine_row,
                                                            #fine_col=fine_col) <= piExplosionRange*piExplosionRange
            {
               Send(self,@DoEffect,#what=each_obj);
            }
         }
      }

      Send(poOwner,@SomethingExploded,#what=self);
      %Send(poOwner,@SomethingWaveRoom,#what=self,#wave_rsc=freezing_winds_hit_sound);

      Send(self,@Delete);
      return;
   }

   GetPeriodicDuration()
   {
      % 90 - 110% of base interval
      return (VECTOR_EFFECT_INTERVAL * Random(90,110))/100;
   }

   GetDuration(duration = 0, bMS = FALSE)
   {
      return VECTOR_MOVE_INTERVAL;
   }

   DoEffect(what = $)
   {
      local iDamage, iDuration, rMessage;

      iDamage = 0;
      
      if viIllusion
      {
         iDamage = Send(what,@AssessDamage,#what=poCaster,#damage=Random(0,piMaxDamage),
                        #aspell=ATCK_SPELL_ALL+ATCK_SPELL_ILLUSION,#report=FALSE,#report_resistance=FALSE);
      }
      else
      {
         iDamage = Send(what,@AssessDamage,#what=poCaster,#damage=Random(0,piMaxDamage),
                        #aspell=ATCK_SPELL_ALL+ATCK_SPELL_COLD,#report=FALSE,#report_resistance=FALSE);
      }
      Send(poOwner,@SomethingAttacked,#what=poCaster,#victim=what,
            #use_weapon=self,#stroke_obj=self);

      if iDamage = $
      {
         Send(poCaster,@KilledSomething,#what=what,#use_weapon=self);
      }

      if IsClass(poCaster,&Player)
      {
         if iDamage = $
         {
            Send(poCaster,@SetPlayerFlag,#flag=PFLAG_DID_DAMAGE,#value=TRUE);
            
            return;
         }
         else
         {
            if piMaxDamage > 0
            {
               Send(poCaster,@SetPlayerFlag,#flag=PFLAG_DID_DAMAGE,#value=TRUE);
            }
         }
      }

      if IsClass(what,&Player)
      {
         if iDamage = 0
            OR iDamage = $
         {
            if viIllusion
            {
               rMessage = firewall_illusion_damage0;
            }
            else
            {
               rMessage = freezing_winds_damage0;
            }
         }
         else
         {
            % Damage is greater than 0
            Send(what,@WaveSendUser,#wave_rsc=freezing_winds_hit_sound);

            if iDamage < 500
            {
               if viIllusion
               {
                  rMessage = firewall_illusion_damage5;
               }
               else
               {
                  rMessage = freezing_winds_damage5;
               }
            }
            else
            {
               if iDamage < 1000
               {
                  if viIllusion
                  {
                     rMessage = firewall_illusion_damage10;
                  }
                  else
                  {
                     rMessage = freezing_winds_damage10;
                  }
               }
               else
               {
                  if iDamage < 1500
                  {
                     if viIllusion
                     {
                        rMessage = firewall_illusion_damage15;
                     }
                     else
                     {
                        rMessage = freezing_winds_damage15;
                     }
                  }
                  else
                  {
                     if viIllusion
                     {
                        rMessage = firewall_illusion_damage20;
                     }
                     else
                     {
                        rMessage = freezing_winds_damage20;
                     }
                  }
               }
            }
         }

         Send(what,@MsgSendUser,#message_rsc=rMessage);
      }
      
      propagate;
   }

   SendAnimation()
   {
      AddPacket(1, ANIMATE_NONE, 2, Random(1,5));
      
      return;
   }

   NewOwner(what = $)
   {
      % Add a bit of light if this isn't an illusion.  Firewalls should add some light.
      if NOT viIllusion
      {
         % new room
         if what <> $
         {
            Send(what,@AddBaseLight,#amount=viLightBonus);
         }
   
         % old room
         if poOwner <> $
         {
            Send(poOwner,@AddBaseLight,#amount=-viLightBonus);
         }
      }
      
      propagate;
   }

   Delete()
   {
      % Take away the light we added if we weren't an illusion.
      if NOT viIllusion
      {
         if poOwner <> $
         {
            Send(poOwner,@AddBaseLight,#amount=-viLightBonus);
         }
      }
      
      propagate;
   }

   SendLightingInformation()
   {
      AddPacket(2,(LIGHT_FLAG_ON | LIGHT_FLAG_DYNAMIC));
      % Intensity
      if viIllusion
      {
         % Send less light if we're illusionary.
         AddPacket(1,0);
      }
      else
      {
         % 5 out of 255 intensity of light
         AddPacket(1,5);
      }

      if Send(self,@GetOwner) <> $
         AND Send(Send(self,@GetOwner),@LightingColorOverride)
      {
         AddPacket(2,Send(Send(self,@GetOwner),@GetLightingColorOverride));
         return;
      }

      % Blue color
      AddPacket(2,LIGHT_BLUE);

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
