% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Forge is StorageBox

% 

constants:

   CHEST_ITEMS_MAX = 1
   
   TIER_ZERO_POWER = 0
   TIER_ONE_POWER = 1
   TIER_TWO_POWER = 2
   TIER_THREE_POWER = 3
   TIER_FOUR_POWER = 4
   TIER_FIVE_POWER = 5
   TIER_SIX_POWER = 6
   TIER_SEVEN_POWER = 7
   TIER_EIGHT_POWER = 8
   TIER_NINE_POWER = 9
   TIER_TEN_POWER = 10

   include blakston.khd

resources:

   Forge_name_rsc = "magical forge"
   Forge_icon_rsc = anvil.bgf

   Forge_desc_rsc = \
      "What magics the Adventurer's League have learned from "
      "its members may be imbued into items here by a process of "
      "forging and inlaying of runes."
      "\n\nPlace the object inside to begin the forging process. "
      "\n\nWhile you are present in the area, your item "
      "cannot be taken from the forge by anyone else."
   
   forge_powered_up_msg = \
      "The power of the forge grows by %i points in the realm of %s."

   forge_active_item = \
      "Your %s becomes surrounded in free-flowing magical energies! To strike while the iron is hot, choose a forging category by speaking one out loud. This forge "
      "has the capability to smelt in the realms of: "
   forge_category_prompt = \
      "%s - tier %i"

classvars:

   vrName = Forge_name_rsc
   vrIcon = Forge_icon_rsc
   vrDesc = Forge_desc_rsc

   viBulk_hold_max = $
   viWeight_hold_max = $

properties:

   pbActive = FALSE
   
   poHeldItem = $
   poForger = $

messages:

   CanBeForged(what=$)
   {
      if what <> $
      {
         if IsClass(what,&DefenseModifier)
            OR IsClass(what,&Weapon)
         {
            return TRUE;
         }
      }

      return FALSE;
   }

   CanPowerUpForge(what=$)
   {
      if what <> $
      {
         if IsClass(what,&ElementalChunk)
            OR IsClass(what,&MajorHeartStone)
            OR IsClass(what,&HeartStone)
         {
            return TRUE;
         }
      }

      return FALSE;
   }

   ReqNewHold(what = $, who = $)
   {
      if NOT Send(self,@CanDepositItems,#lItems=[what])
      {
         return FALSE;
      }

      if NOT Send(self,@CanPowerUpForge,#lItems=[what])
         AND NOT NOT Send(self,@CanBeForged,#lItems=[what])
      {
         return FALSE;
      }

      Post(self,@RegisterForger,#what=what,#who=who);
      propagate;
   }

   RegisterForger(what=$,who=$)
   {
      if who <> $
      {
         poForger = who;
      }
      return;
   }
   
   UnregisterForger()
   {
      if poHeldItem <> $
         AND poForger <> $
      {
         Send(poForger,@NewHold,#what=poHeldItem);
         poHeldItem = $;
      }
      poForger = $;
      return;
   }

   CanDepositItems(lItems = $)
   {
      local plItemsStored;

      if lItems = $
      {
         return FALSE;
      }

      plItemsStored = Send(self,@GetHolderPassive);
      if plItemsStored <> $
         AND (Length(plItemsStored) + Length(lItems)) > CHEST_ITEMS_MAX
      {
         return FALSE;
      }
      
      if (IsClass(First(lItems),&NumberItem)
         AND NOT IsClass(First(lItems),&HeartStone))
      {
         return FALSE;
      }

      return TRUE;
   }

   NewHold(what=$)
   {
      if Send(self,@CanPowerUpForge,#what=what)
      {
         Send(self,@PowerUpForge,#what=what);
         Post(what,@Delete);
         propagate;
      }

      if Send(self,@CanBeForged,#what=what)
      {
         poHeldItem = what;
         pbActive = TRUE;
         Send(self,@InformRoomForgingReady);
         Post(poOwner,@SomethingChanged,#what=self);
      }
      propagate;
   }

   PowerUpForge(what=$)
   {
      if what <> $
      {
         if IsClass(what,&ElementalChunk)
         {
            Send(SETTINGS_OBJECT,@AddLeagueForgePower,#iAmount=Send(what,@GetForgeValue),#iType=Send(what,@GetForgeType));
            Send(self,@InformRoomOfPowerUp,#iAmount=Send(what,@GetForgeValue),#iType=Send(what,@GetForgeType));
            return;
         }
         if IsClass(what,&MajorHeartStone)
         {
            Send(SETTINGS_OBJECT,@AddLeagueForgePower,#iAmount=Send(what,@GetForgeValue),#iType=Send(what,@GetForgeType));
            Send(self,@InformRoomOfPowerUp,#iAmount=Send(what,@GetForgeValue),#iType=Send(what,@GetForgeType));
            return;
         }
         if IsClass(what,&HeartStone)
         {
            Send(SETTINGS_OBJECT,@AddLeagueForgePower,#iAmount=Send(what,@GetForgeValue)*Send(what,@GetNumber),#iType=Send(what,@GetForgeType));
            Send(self,@InformRoomOfPowerUp,#iAmount=Send(what,@GetForgeValue),#iType=Send(what,@GetForgeType));
            return;
         }
      }
      return;
   }
   
   InformRoomOfPowerUp(iAmount=0,iType=FORGE_FIRE)
   {
      local i, rString;

      foreach i in Send(poOwner,@GetHolderActive)
      {
         if IsClass(First(i),&Player)
         {
            Send(First(i),@MsgSendUser,#message_rsc=forge_powered_up_msg,#parm1=iAmount,#parm2=Send(SYS,@GetForgeCategory,#iType=iType));
         }
      }
      return;
   }
   
   InformRoomForgingReady()
   {
      local i, index, lForgePowers;
      
      lForgePowers = Send(SETTINGS_OBJECT,@GetLeagueForgePowers);

      foreach i in Send(poOwner,@GetHolderActive)
      {
         if IsClass(First(i),&Player)
         {
            Send(First(i),@MsgSendUser,#message_rsc=forge_active_item,#parm1=Send(poHeldItem,@Getname));
            index = FORGE_FIRE;
            while index <= Length(lForgePowers)
            {
               if Nth(lForgePowers,index) > 0
               {
                  Send(First(i),@MsgSendUser,#message_rsc=forge_category_prompt,
                                             #parm1=Send(SYS,@GetForgeCategory,#iType=index),
                                             #parm2=Send(self,@CalculateTier,#iPower=Nth(lForgePowers,index)));
               }
               index++;
            }
         }
      }
      return;
   }

   LeaveHold(what=$)
   {
      local i;
      
      poForger = $;
      poHeldItem = $;
      pbActive = FALSE;
      Post(poOwner,@SomethingChanged,#what=self);

      propagate;
   }

   % Give forge a glow when active.
   SendLightingInformation()
   {
      if pbActive
      {
         AddPacket(2,LIGHT_FLAG_ON);
         % Intensity (out of 255)
         AddPacket(1,1);
         % Color
         AddPacket(2,LIGHT_BRED);
         return;
      }

      propagate;
   }

   SendAnimation()
   {
      AddPacket(1,ANIMATE_NONE,2,1);

      return;
   }
   
   GetForgingItem()
   {
      return poHeldItem;
   }
   
   Delete()
   {
      if poHeldItem <> $
      {
         Send(poHeldItem,@Delete);
      }
      poHeldItem = $;
      propagate;
   }

   GetActive()
   {
      return pbActive;
   }

   SomeoneSaid(what=$,string=$,type=$)
   {
      return;
   }

   ReqTaker(what=$,taker=$)
   {
      if poForger <> $
         AND poForger <> taker
         AND Send(poForger,@GetOwner) = Send(self,@GetOwner)
      {
         return FALSE;
      }

      propagate;
   }

   CalculateTier(iPower=0)
   {
      if iPower > 1000000
      {
         return 10;
      }
      if iPower > 7500000
      {
         return 9;
      }
      if iPower > 4800000
      {
         return 8;
      }
      if iPower > 240000
      {
         return 7;
      }
      if iPower > 120000
      {
         return 6;
      }
      if iPower > 60000
      {
         return 5;
      }
      if iPower > 30000
      {
         return 4;
      }
      if iPower > 15000
      {
         return 3;
      }
      if iPower > 5000
      {
         return 2;
      }
      if iPower > 0
      {
         return 1;
      }
      return 0;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
