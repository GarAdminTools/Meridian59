% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
WoodenBox is StorageBox

constants:

   CHEST_ITEMS_MAX = 75

   include blakston.khd

resources:

   include woodbox.lkod

   WoodenBox_name_rsc = "wooden box"
   WoodenBox_icon_rsc = box1.bgf

   WoodenBox_desc_rsc = \
      "This sturdy wooden box can store a good number of items."

classvars:

   vrName = WoodenBox_name_rsc
   vrIcon = WoodenBox_icon_rsc
   vrDesc = WoodenBox_desc_rsc

   viBulk_hold_max = $
   viWeight_hold_max = $

properties:

messages:

   ReqNewHold(what = $, who = $)
   {
      if NOT Send(self,@CanDepositItems,#lItems=[what])
      {
         return FALSE;
      }

      propagate;
   }

   CanDepositItems(lItems = $)
   {
      local plItemsStored;

      if lItems = $
      {
         return FALSE;
      }

      plItemsStored = Send(self,@GetHolderPassive);
      if plItemsStored <> $
         AND (Length(plItemsStored) + Length(lItems)) > CHEST_ITEMS_MAX
         AND NOT (Length(lItems) = 1
            AND IsClass(First(lItems),&NumberItem))
      {
         return FALSE;
      }

      return TRUE;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
