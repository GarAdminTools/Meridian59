% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
LeagueDonations is StorageBox

constants:

   CHEST_ITEMS_MAX = 500

   include blakston.khd

resources:

   LeagueDonations_name_rsc = "Adventurer's League Funding"
   LeagueDonations_icon_rsc = artrop04.bgf

   LeagueDonations_desc_rsc = \
      "Put shillings into this device to donate to the expansion of the Adventurer's League. "
      "We thank you for your contributions!"

   donated_money_to_league = \
      "%s just donated %i shillings to the League's expansion projects!"

classvars:

   vrName = LeagueDonations_name_rsc
   vrIcon = LeagueDonations_icon_rsc
   vrDesc = LeagueDonations_desc_rsc

   viBulk_hold_max = $
   viWeight_hold_max = $

properties:

messages:

   Constructor()
   {
      propagate;
   }

   ReqNewHold(what = $, who = $)
   {
      if NOT Send(self,@CanDepositItems,#lItems=[what])
      {
         return FALSE;
      }
      
      Post(self,@DonateMoneyToLeague,#what=what,#who=who);

      propagate;
   }

   CanDepositItems(lItems = $)
   {
      local plItemsStored;

      if lItems = $
      {
         return FALSE;
      }
      
      if NOT IsClass(First(lItems),&Money)
      {
         return FALSE;
      }
      
      if Send(First(lItems),@GetNumber) > 5000000
      {
         return FALSE;
      }

      plItemsStored = Send(self,@GetHolderPassive);
      if plItemsStored <> $
         AND (Length(plItemsStored) + Length(lItems)) > CHEST_ITEMS_MAX
         AND NOT (Length(lItems) = 1
            AND IsClass(First(lItems),&NumberItem))
      {
         return FALSE;
      }

      return TRUE;
   }

   DonateMoneyToLeague(what=$,who=$)
   {
      local i, each_obj, iCount, oLeagueMaint;
      
      iCount = Send(what,@GetNumber);

      foreach i in Send(poOwner,@GetHolderActive)
      {
         each_obj = Send(poOwner,@HolderExtractObject,#data=i);
         if IsClass(each_obj,&Player)
            AND each_obj <> who
         {
            Send(each_obj,@MsgSendUser,#message_rsc=donated_money_to_league,#parm1=Send(who,@GetName),#parm2=iCount);
         }
      }

      Send(what,@SubtractNumber,#number=iCount);

      oLeagueMaint = Send(SYS,@GetLeagueMaintenance);
      Send(oLeagueMaint,@DonatedMoneyToLeague,#who=who,#iAmount=iCount);

      return;
   }

   SendAnimation()
   {
      AddPacket(1,ANIMATE_NONE,2,1);

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
