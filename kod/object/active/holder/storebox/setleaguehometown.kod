% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
SetLeagueHometown is StorageBox

constants:

   CHEST_ITEMS_MAX = 500

   include blakston.khd

resources:

   SetLeagueHometown_name_rsc = "Adventurer's League Membership"
   SetLeagueHometown_icon_rsc = artrop01.bgf

   SetLeagueHometown_desc_rsc = \
      "Care to make the Adventurer's League your home return building? "
      "Become a permanent member today! Any amount of shillings will do "
      "for a donation!"

   hometown_donated_money_to_league = \
      "%s just donated %i shillings to set the League as their home return building!"

classvars:

   vrName = SetLeagueHometown_name_rsc
   vrIcon = SetLeagueHometown_icon_rsc
   vrDesc = SetLeagueHometown_desc_rsc

   viBulk_hold_max = $
   viWeight_hold_max = $

properties:

messages:

   Constructor()
   {
      propagate;
   }

   ReqNewHold(what = $, who = $)
   {
      if NOT Send(self,@CanDepositItems,#lItems=[what])
      {
         return FALSE;
      }
      
      Post(self,@DonateMoneyToLeague,#what=what,#who=who);

      propagate;
   }

   CanDepositItems(lItems = $)
   {
      local plItemsStored;

      if lItems = $
      {
         return FALSE;
      }
      
      if NOT IsClass(First(lItems),&Money)
      {
         return FALSE;
      }

      plItemsStored = Send(self,@GetHolderPassive);
      if plItemsStored <> $
         AND (Length(plItemsStored) + Length(lItems)) > CHEST_ITEMS_MAX
         AND NOT (Length(lItems) = 1
            AND IsClass(First(lItems),&NumberItem))
      {
         return FALSE;
      }

      return TRUE;
   }

   DonateMoneyToLeague(what=$,who=$)
   {
      local i, each_obj, iCount, oLeagueMaint;
      
      iCount = Send(what,@GetNumber);

      foreach i in Send(poOwner,@GetHolderActive)
      {
         each_obj = Send(poOwner,@HolderExtractObject,#data=i);
         if IsClass(each_obj,&Player)
         {
            Send(each_obj,@MsgSendUser,#message_rsc=hometown_donated_money_to_league,#parm1=Send(who,@GetName),#parm2=iCount);
         }
      }

      Send(what,@SubtractNumber,#number=iCount);

      oLeagueMaint = Send(SYS,@GetLeagueMaintenance);
      Send(oLeagueMaint,@DonatedMoneyToLeague,#who=who,#iAmount=iCount);

      Send(who,@SetHomeroom,#RID=RID_ADVENTURERS_GUILD);
      return;
   }

   SendAnimation()
   {
      AddPacket(1,ANIMATE_NONE,2,1);

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
