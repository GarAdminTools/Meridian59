% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
MarauderWorm is Marauder

% Worm uses heals and leeches to attack his foes. 
% He should group heal his bandit friends. 

constants:

   include blakston.khd

   HUMAN_TRANSLATION2_HAIR_MASK = 0xff
   HUMAN_TRANSLATION2_HAIR_MUL = 1

resources:

   MarauderWorm_name_rsc = "Worm"
   MarauderWorm_desc_rsc = \
      "This bandit's foul body odor assaults your nostrils.   "
      "He's sickly, grimey, and utterly repulsive. Most succumb to his demands to get away as soon as possible. "
	  "He is a murderer! Defend yourself! "
   MarauderWorm_dead_name_rsc = "dead male body"

   MarauderWorm_url = "http://wiki.openmeridian.org/index.php/Main_Page"
   
   Worm_hair_bb_icon = ptbb.bgf

classvars:

   vrName = MarauderWorm_name_rsc
   vrDesc = MarauderWorm_desc_rsc
   vrDead_name = MarauderWorm_dead_name_rsc

   % Worm's a human
   viMinSkinColor = 1
   viMaxSkinColor = 1

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viColorTranslate1 = XLAT_TO_RED     % Shirt
   viColorTranslate2 = XLAT_TO_RED     % Pants

   vcShieldClass = &PrincessShield

   viFaction = FACTION_PRINCESS
   
properties:

   piBaseLevel = 150
   piBaseDifficulty = 10
   viLevel = 150
   viDifficulty = 10

   piMana = 120
   piMax_Mana = 120
   
   % What are the monster's chances to cast spells each check?
   % Number is out of one hundred
   piMeleeSpellChance = 70
   piRangedSpellChance = 30
   piAllySpellChance = 10
   piSelfSpellChance = 10
   piOtherSpellChance = 10


   piMonsterCastDelayMinimum = 1500


messages:

   SetGender()
   {
      return;
   }

   Constructed()
   {
      local i;

      % Qor PK
      plMeleeSpellBook = [  [SID_SUMMON_POISON_FOG,10,5,50],
                            [SID_VAMPIRIC_DRAIN,6,100,99]];
      plRangedSpellBook = [ [SID_HEX_LEECHES,20,20,99],
                            [SID_PURGE,14,40,90],
                            [SID_BRAMBLEBLAST,12,100,40]];
	  plAllySpellBook = [[SID_GROUP_HEAL,25,100,70]];
	  
      
      plResistances = [[ATCK_WEAP_ALL,25]];
                        
      psUrl = MarauderWorm_url;
      propagate;
   }

   GetBaseEmpowerments()
   {
      return [[ATCK_WEAP_PIERCE,10]];
   }

   SetEquipment()
   {
      local oEquipmentItem, iBonusModifier, oSpell;

      oEquipmentItem = Create(&Dagger,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&Gauntlet,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&LeatherArmor,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);
	  
	  oEquipmentItem = Create(&Tanktop,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);
	  
	  oEquipmentItem = Create(&Shades,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      return;
   }

   SetHair()
   % Sets the hairdo, color.
   {
      local iTranslation;

      prToupee = human_hair_db_icon;
      iTranslation = PT_GRAY_TO_DGREEN;

      piBody_translations2 = piBody_translations2 & ~HUMAN_TRANSLATION2_HAIR_MASK;
      piBody_translations2 = piBody_translations2
                                 | (iTranslation * HUMAN_TRANSLATION2_HAIR_MUL);

      return;
   }

   SetFace()
   {
      prEyes = human_eyes_cx_icon;
      prNose = human_nose_ax_icon;
      prMouth = human_mouth_bx_icon;

      return;
   }

   SetShield()
   {
      local oEquipmentItem;

      oEquipmentItem = Create(vcShieldClass,#color1=viShieldColor1,#color2=viShieldColor2,
                              #shape=viShieldShape);
      if viInsignia <> $
      {
         Send(oEquipmentItem,@ChangeGuildInsignia,#insignia=viInsignia);
      }

      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      return;
   }
   
    
    
   GetBaseSize()
   {
      % Half demon base size
      return 150;
	 
   }
   
  
end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
