% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
MarauderJinora is Marauder

% Jinora is a half-demon Qor/Witchery PK.

constants:

   include blakston.khd

   HUMAN_TRANSLATION2_HAIR_MASK = 0xff
   HUMAN_TRANSLATION2_HAIR_MUL = 1

resources:

   MarauderJinora_name_rsc = "Jinora"
   MarauderJinora_desc_rsc = \
      "The Radiant Outriders enlisted a pair of elite assassins "
      "known as the Subdual Artists. They were raised by an infamous line "
      "of assassins from birth. Killing is the only thing they have ever known. "
      "Make peace with the gods! You are about to die! "
   MarauderJinora_dead_name_rsc = "dead female body"

   MarauderJinora_url = "http://wiki.openmeridian.org/index.php/Main_Page"
   
   Jinora_hair_bb_icon = ptbb.bgf

classvars:

   vrName = MarauderJinora_name_rsc
   vrDesc = MarauderJinora_desc_rsc
   vrDead_name = MarauderJinora_dead_name_rsc

   % Jinora's a half-demon.
   viMinSkinColor = 135
   viMaxSkinColor = 135

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE
   
   viDefault_behavior = AI_MOVE_REGROUP | AI_FIGHT_SWITCHALOT | AI_FIGHT_LASHOUT | AI_FIGHT_WIZARD_KILLER | AI_FIGHT_AGGRESSIVE | AI_MOVE_OPTIMAL_RANGE

   viColorTranslate1 = XLAT_TO_BLACK     % Shirt
   viColorTranslate2 = XLAT_TO_BLACK    % Pants

   vcShieldClass = &GuildShield
   viShieldColor1 = 55
   viShieldColor2 = 55
   viShieldShape = SHIELDSHAPE_HORIZ
   viInsignia = INSIG_SHALILLE
   
   viAttack_spell = ATCK_SPELL_HOLY

properties:

   piBaseLevel = 200
   piBaseDifficulty = 15
   viLevel = 200
   viDifficulty = 15

   piMana = 120
   piMax_Mana = 120
   
   viKarma = 100
   
   piGender = GENDER_FEMALE
   
   % What are the monster's chances to cast spells each check?
   % Number is out of one hundred
   piMeleeSpellChance = 60
   piRangedSpellChance = 100
   piAllySpellChance = 10
   piSelfSpellChance = 10
   piOtherSpellChance = 10

   piMonsterCastDelayMinimum = 1500


   piDemonSponsor = SSS_WITCHERY_NEQUZON

messages:


      SetGender()
   {
		viGender = GENDER_FEMALE;
         vrIcon = human_female_icon_rsc;
         prHead = human_head_female_icon;
         prEyes = human_eyes_kx_icon;
         prMouth = human_mouth_kx_icon;
         prNose = human_nose_kx_icon;
         prToupee = human_hair_ca_icon;
         prLegs = human_legs_b_rsc;
         prRight_arm = human_rightarm_b_rsc;
         prLeft_arm = human_leftarm_b_rsc;
         vrDead_Icon = human_dead_female_icona_rsc;
         vrDead_Legs = human_dead_female_iconb_rsc;
         vrSound_death = human_female_sound_death;
		 
      return;
   }

   Constructed()
   {
      local i;

      % Qor PK
      plMeleeSpellBook = [ [SID_PURGE,6,100,90]];
      plRangedSpellBook = [ [SID_DAZZLE,13,50,90],
                            [SID_LIGHTNING_BOLT,14,90,90],
                            [SID_LIGHTNING_STRIKE,6,100,90]];
      plAllySpellBook = [[SID_GROUP_HEAL,25,100,70]];

      % Half-demon base resists
      plResistances = [ [-ATCK_SPELL_FIRE, 20 ],
                        [-ATCK_SPELL_COLD, -25 ],
                        [-ATCK_SPELL_ACID, 10],
                        [ATCK_WEAP_NERUDITE,10]];
                        
      psUrl = MarauderJinora_url;
      propagate;
   }

   GetBaseEmpowerments()
   {
      return [[ATCK_WEAP_PIERCE,80]];
   }

   SetEquipment()
   {
      local oEquipmentItem, iBonusModifier, oSpell;

      oEquipmentItem = Create(&Dagger,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&LightDiscipleRobe,#level=viLevel,#school=SS_SHALILLE);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);
	  
      oEquipmentItem = Create(&Tanktop,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&Gauntlet,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&IvyCirclet,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      return;
   }

   SetHair()
   % Sets the hairdo, color.
   {
      local iTranslation;

      prToupee = human_hair_dc_icon;
      iTranslation = PT_GRAY_TO_BLACK;

      piBody_translations2 = piBody_translations2 & ~HUMAN_TRANSLATION2_HAIR_MASK;
      piBody_translations2 = piBody_translations2
                                 | (iTranslation * HUMAN_TRANSLATION2_HAIR_MUL);

      return;
   }


   SetShield()
   {
      local oEquipmentItem;

      oEquipmentItem = Create(vcShieldClass,#color1=viShieldColor1,#color2=viShieldColor2,
                              #shape=viShieldShape);
      if viInsignia <> $
      {
         Send(oEquipmentItem,@ChangeGuildInsignia,#insignia=viInsignia);
      }

      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      return;
   }
   
      IsAlly(target = $, regroup = FALSE)
   {
      if IsClass(target,&MarauderShindra)
      {
         return TRUE;
      }

      propagate;
   }
   
      TargetSwitch(what=$,iHatred=0,forally=FALSE)
   {
      local i;

      if what = $
         OR poOwner = $
      {
         Debug("BAD target we're switching to here!");

         return;
      }

      if IsClass(what,&User)
         AND NOT Send(what,@IsLoggedOn)
      {
         % Trying to switch to someone logged off. Can't do that.
         return;
      }

      piHatred = iHatred;

      if poTarget <> $
         AND Send(poTarget,@GetOwner) = poOwner
         AND IsClass(poTarget,&user)
      {
         Send(poTarget,@SubtractFromMonsterChasers,
              #level=piAmountAddedToMonsterChasers);
      }

      poTarget = what;
      if IsClass(poTarget,&user)
      {
         piAmountAddedToMonsterChasers = piLevel;
         Send(poTarget,@AddToMonsterChasers,#level=piAmountAddedToMonsterChasers);
      }

      piState = (piState & VSTATE_VALIDITY_MASK);

      % 'lock' sound
      if vrSound_aware <> $
      {
         Send(poOwner,@SomethingWaveRoom,#what=self,#wave_rsc=vrSound_aware);
      }

      % Call our tribe!
      if NOT forally
         AND poOwner <> $
      {
         foreach i in Send(poOwner,@GetHolderActive)
         {
            if IsClass(First(i),&MarauderShindra)
               AND Send(self,@IsAlly,#target=First(i))
            {
               Send(First(i),@TargetSwitch,#what=what,#iHatred=100,#forally=TRUE);
               Send(First(i),@EnterStateChase,#target=what,#actnow=TRUE);
            }
         }
      }
      return;
   }

   GetBaseSize()
   {
      % Half demon base size
      return 150;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
