% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
MarauderSabrienna is Marauder

% Sabrienna is a half-demon Qor/Witchery PK.

constants:

   include blakston.khd

   HUMAN_TRANSLATION2_HAIR_MASK = 0xff
   HUMAN_TRANSLATION2_HAIR_MUL = 1

   
       HEX_CHANCE = 5
	   
resources:

   MarauderSabrienna_name_rsc = "Sabrienna"
   MarauderSabrienna_desc_rsc = \
      "Sabrienna suffered much at the hands of her persecutors. She's lost everyones she has "
	  "ever held dear during the persecution of those witches who allied with the wrong demons. "
	  "She surrendered her broken body for an imperishable form of spirit energy, "
	  "driven by vengeance, capable of doing great damage in the material plane. "
	  "She may spend eternity without sensation, but she will have her revenge. " 
      "Make peace with the gods! You are about to die! "
   MarauderSabrienna_dead_name_rsc = "dead female body"

   MarauderSabrienna_url = "http://wiki.openmeridian.org/index.php/Main_Page"
   
   Sabrienna_hair_bb_icon = ptbb.bgf

classvars:

   vrName = MarauderSabrienna_name_rsc
   vrDesc = MarauderSabrienna_desc_rsc
   vrDead_name = MarauderSabrienna_dead_name_rsc

   % Sabrienna's a half-demon.
   viMinSkinColor = 135
   viMaxSkinColor = 135

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE
   
   viDefault_behavior = AI_MOVE_REGROUP | AI_FIGHT_SWITCHALOT | AI_FIGHT_LASHOUT | AI_FIGHT_WIZARD_KILLER | AI_FIGHT_AGGRESSIVE | AI_MOVE_OPTIMAL_RANGE

   viColorTranslate1 = XLAT_TO_ORANGE     % Shirt
   viColorTranslate2 = XLAT_TO_RED     % Pants

   vcShieldClass = &GuildShield
   viShieldColor1 = 20
   viShieldColor2 = 55
   viShieldShape = SHIELDSHAPE_HORIZ
   viInsignia = INSIG_QOR

properties:

   piBaseLevel = 400
   piBaseDifficulty = 12
   viLevel = 400
   viDifficulty = 12

   piMana = 120
   piMax_Mana = 120
   
   viKarma = -100
   
   piGender = GENDER_FEMALE
   
   % What are the monster's chances to cast spells each check?
   % Number is out of one hundred
   piMeleeSpellChance = 65
   piRangedSpellChance = 100

   piMonsterCastDelayMinimum = 500

   piDemonSponsor = SSS_WITCHERY_NEQUZON
   
   piDrawFx = DRAWFX_TRANSLUCENT_50

messages:


      SetGender()
   {
		viGender = GENDER_FEMALE;
         vrIcon = human_female_icon_rsc;
         prHead = human_head_female_icon;
         prEyes = human_eyes_lx_icon;
         prMouth = human_mouth_mx_icon;
         prNose = human_nose_kx_icon;
         prToupee = human_hair_ca_icon;
         prLegs = human_legs_b_rsc;
         prRight_arm = human_rightarm_b_rsc;
         prLeft_arm = human_leftarm_b_rsc;
         vrDead_Icon = human_dead_female_icona_rsc;
         vrDead_Legs = human_dead_female_iconb_rsc;
         vrSound_death = human_female_sound_death;
		 
      return;
   }

   Constructed()
   {
      local i;


      plMeleeSpellBook = [  [SID_JIG,100,5,99],
							[SID_HOLD,20,20,50],
						   [SID_VAMPIRIC_DRAIN,5,100,90] ];
      plRangedSpellBook = [ [SID_LIGHTNING_BOLT,5,40,90],
							[SID_BLIND,13,55,90],
							[SID_SAND_STORM,30,65,90],
							[SID_FROST_BREATH,50,70,99],
							[SID_RING_OF_FLAMES,30,75,99],
							[SID_SLITHERBOLT,2,80,90],
                           [SID_BRAMBLEBLAST,30,100,99]];
      
      plResistances = [ [-ATCK_SPELL_FIRE, -30 ],
                        [-ATCK_SPELL_COLD, 35 ],
                        [-ATCK_SPELL_ACID, 90],
						[-ATCK_SPELL_SHOCK, 50],
                        [ATCK_WEAP_NERUDITE,-10],
						[ATCK_WEAP_ALL,80]];
                        
      psUrl = MarauderSabrienna_url;
      propagate;
   }

   GetBaseEmpowerments()
   {
      return [[ATCK_WEAP_THRUST,80],
			   [-ATCK_SPELL_UNHOLY,70],
			   [-ATCK_SPELL_SHOCK, 100],
			    [-ATCK_SPELL_ACID, 90]];
   }

   SetEquipment()
   {
      local oEquipmentItem, iBonusModifier, oSpell;

      oEquipmentItem = Create(&Longsword,#level=240);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&LightRobe,#level=240);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);
	  
	  oEquipmentItem = Create(&Tanktop,#level=240);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&Gauntlet,#level=240);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&RunedHelm,#level=240);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      return;
   }

   SetHair()
   % Sets the hairdo, color.
   {
      local iTranslation;

      prToupee = human_hair_dc_icon;
      iTranslation = PT_GRAY_TO_BLACK;

      piBody_translations2 = piBody_translations2 & ~HUMAN_TRANSLATION2_HAIR_MASK;
      piBody_translations2 = piBody_translations2
                                 | (iTranslation * HUMAN_TRANSLATION2_HAIR_MUL);

      return;
   }


   SetShield()
   {
      local oEquipmentItem;

      oEquipmentItem = Create(vcShieldClass,#color1=viShieldColor1,#color2=viShieldColor2,
                              #shape=viShieldShape);
      if viInsignia <> $
      {
         Send(oEquipmentItem,@ChangeGuildInsignia,#insignia=viInsignia);
      }

      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      return;
   }
   
    HitSideEffect(what = $, who = $)
   {
      local oSpell;

      oSpell = Send(SYS,@FindSpellByNum,#num=SID_HEX_CORRUPTION);
      if NOT Send(what,@IsEnchanted,#what=oSpell)
         AND Random(1,HEX_CHANCE) = 1
      {
         if who <> $
         {
            Send(oSpell,@CastSpell,#who=who,#lTargets=[what],#iSpellPower=99);
         }
         else
         {
            Send(oSpell,@DoSpell,#what=self,#oTarget=what,#iSpellPower=99);
         }
      }

      propagate;
   }
   
   
   GetBaseSize()
   {
      % Half demon base size
      return 150;
   }
   
      GetOptimalRange()
   "For most monsters, this is the attack range."
   {
      return 4 * FINENESS;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
