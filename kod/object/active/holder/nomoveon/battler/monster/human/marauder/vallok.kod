% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
MarauderVallok is Marauder

% Vallok is a half-demon Qor/Witchery PK.
% He uses Corrosion + Splash of Acid, as well as Blind and Hold.
% He's also invisible.

constants:

   include blakston.khd

   HUMAN_TRANSLATION2_HAIR_MASK = 0xff
   HUMAN_TRANSLATION2_HAIR_MUL = 1

resources:

   MarauderVallok_name_rsc = "Vallok"
   MarauderVallok_desc_rsc = \
      "An aura of evil surrounds this dark wizard. "
      "He is a murderer. Defend yourself!"
   MarauderVallok_dead_name_rsc = "dead male body"

   MarauderVallok_url = "http://wiki.openmeridian.org/index.php/Main_Page"
   
   vallok_hair_bb_icon = ptbb.bgf

classvars:

   vrName = MarauderVallok_name_rsc
   vrDesc = MarauderVallok_desc_rsc
   vrDead_name = MarauderVallok_dead_name_rsc

   % Vallok's a half-demon.
   viMinSkinColor = 135
   viMaxSkinColor = 135

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viColorTranslate1 = XLAT_TO_RED     % Shirt
   viColorTranslate2 = XLAT_TO_RED     % Pants

   vcShieldClass = &GuildShield
   viShieldColor1 = 6
   viShieldColor2 = 6
   viShieldShape = SHIELDSHAPE_HORIZ
   viInsignia = INSIG_QOR

properties:

   piBaseLevel = 140
   piBaseDifficulty = 7
   viLevel = 140
   viDifficulty = 7

   piMana = 120
   piMax_Mana = 120
   
   % What are the monster's chances to cast spells each check?
   % Number is out of one hundred
   piMeleeSpellChance = 100
   piRangedSpellChance = 100

   piMonsterCastDelayMinimum = 1500

   piDrawFx = DRAWFX_INVISIBLE

   piDemonSponsor = SSS_WITCHERY_NEQUZON

   viBonusBaseTP = 1000
   vbBonusXPGain = 1000

messages:

   SetGender()
   {
      return;
   }

   Constructed()
   {
      local i;

      % Qor PK
      plMeleeSpellBook = [ [SID_SPLASH_OF_ACID,6,100,90]];
      plRangedSpellBook = [ [SID_BLIND,13,20,90],
                            [SID_HOLD,14,70,90],
                            [SID_HEX_CORROSION,12,100,40]];

      % Half-demon base resists
      plResistances = [ [-ATCK_SPELL_FIRE, 20 ],
                        [-ATCK_SPELL_COLD, -25 ],
                        [-ATCK_SPELL_ACID, 10],
                        [ATCK_WEAP_NERUDITE,10]];
                        
      psUrl = MarauderVallok_url;
      propagate;
   }

   GetBaseEmpowerments()
   {
      return [[-ATCK_SPELL_ACID,15]];
   }

   SetEquipment()
   {
      local oEquipmentItem, iBonusModifier, oSpell;

      oEquipmentItem = Create(&Mace,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&DiscipleRobe,#level=viLevel,#school=SS_QOR);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&Glove,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&Circlet,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      return;
   }

   SetHair()
   % Sets the hairdo, color.
   {
      local iTranslation;

      prToupee = vallok_hair_bb_icon;
      iTranslation = PT_GRAY_TO_PLATBLOND;

      piBody_translations2 = piBody_translations2 & ~HUMAN_TRANSLATION2_HAIR_MASK;
      piBody_translations2 = piBody_translations2
                                 | (iTranslation * HUMAN_TRANSLATION2_HAIR_MUL);

      return;
   }

   SetFace()
   {
      prEyes = human_eyes_cx_icon;
      prNose = human_nose_ax_icon;
      prMouth = human_mouth_bx_icon;

      return;
   }

   SetShield()
   {
      local oEquipmentItem;

      oEquipmentItem = Create(vcShieldClass,#color1=viShieldColor1,#color2=viShieldColor2,
                              #shape=viShieldShape);
      if viInsignia <> $
      {
         Send(oEquipmentItem,@ChangeGuildInsignia,#insignia=viInsignia);
      }

      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      return;
   }

   GetBaseSize()
   {
      % Half demon base size
      return 150;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
