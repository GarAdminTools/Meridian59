% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
MarauderToxus is Marauder

% Toxus is a human Qor bower PK.
% He is supposed to use a bow. However, that functionality doesn't seem to work. 

constants:

   include blakston.khd

   HUMAN_TRANSLATION2_HAIR_MASK = 0xff
   HUMAN_TRANSLATION2_HAIR_MUL = 1
   
   POISON_CHANCE = 5
   POISON_DURATION = 30000   %%% in milliseconds
   POISON_LOSSRATE = 10000    %%% in health points * 10^-4 / second

resources:

   MarauderToxus_name_rsc = "Toxus"
   MarauderToxus_desc_rsc = \
      "This man traverses the sewers of Jasper, preying on unsuspecting  "
      "adventurers, taking their stores in lieu of his own survival. "
      "He is accustomed to the rogue life of hiding down below. Defend yourself! "
   MarauderToxus_dead_name_rsc = "dead male body"

   MarauderToxus_url = "http://wiki.openmeridian.org/index.php/Main_Page"
   
   Toxus_hair_bb_icon = ptbb.bgf
   
   Toxus_poisoned_add_rsc = \
      "As the poison travels to your head, you begin "
      "to feel a little light-headed.  The room spins around you."
   Toxus_illusion_poisoned = \
      "%s%s reels as poison from your attack courses through %s veins."

classvars:

   vrName = MarauderToxus_name_rsc
   vrDesc = MarauderToxus_desc_rsc
   vrDead_name = MarauderToxus_dead_name_rsc

   % Toxus's disgusting!
   viMinSkinColor = 2
   viMaxSkinColor = 2

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viColorTranslate1 = XLAT_TO_RED     % Shirt
   viColorTranslate2 = XLAT_TO_RED     % Pants
   viAttack_spell = ATCK_SPELL_ACID

   viShieldColor1 = 1
   viShieldColor2 = 1

   viDefault_behavior = AI_MOVE_REGROUP | \
      AI_FIGHT_AGGRESSIVE |  AI_FIGHT_WIZARD_KILLER | AI_FIGHT_HYPERAGGRESSIVE | AI_FIGHT_SINGLEMINDED | AI_MOVE_OPTIMAL_RANGE

properties:

   piBaseLevel = 160
   piBaseDifficulty = 10
   viLevel = 160
   viDifficulty = 10

   piMana = 90
   piMax_Mana = 90
   
   % What are the monster's chances to cast spells each check?
   % Number is out of one hundred
   piMeleeSpellChance = 70
   piRangedSpellChance = 100

   piMonsterCastDelayMinimum = 1500




messages:

   SetGender()
   {
      return;
   }

   Constructed()
   {
      local i;

      % Qor PK
      plMeleeSpellBook = [ [SID_BLIND,13,70,99],
                           [SID_SUMMON_POISON_FOG,30,100,90]];
      plRangedSpellBook = [ [SID_BLIND,13,90,90],
                            [SID_DEMENT,14,95,90],
                            [SID_KILLING_FIELDS,12,100,90]];

      
      plResistances = [];
                        
      psUrl = MarauderToxus_url;
      propagate;
   }



   SetEquipment()
   {
      local oEquipmentItem, iBonusModifier, oSpell;

      oEquipmentItem = Create(&LongBow,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);
	  poArrow = Create(&AcidArrow);
      iBonusModifier = 4;

      oEquipmentItem = Create(&DiscipleRobe,#level=viLevel,#school=SS_QOR);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&Gauntlet,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&DuskRatMask,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      return;
   }

   SetHair()
   % Sets the hairdo, color.
   {
      local iTranslation;

      prToupee = human_hair_ad_icon;
      iTranslation = PT_GRAY_TO_BLACK;

      piBody_translations2 = piBody_translations2 & ~HUMAN_TRANSLATION2_HAIR_MASK;
      piBody_translations2 = piBody_translations2
                                 | (iTranslation * HUMAN_TRANSLATION2_HAIR_MUL);

      return;
   }

   SetFace()
   {
      prEyes = human_eyes_bx_icon;
      prNose = human_nose_cx_icon;
      prMouth = human_mouth_bx_icon;

      return;
   }
   
       HitSideEffect(what=$,who=$)
   {
      local oSpell;

      if Random(1,POISON_CHANCE) = 1
      {
         oSpell = Send(SYS,@FindSpellByNum,#num=SID_POISON);
         Send(oSpell,@MakePoisoned,#who=what,
               #lossrate=POISON_LOSSRATE,#duration=POISON_DURATION);

         if IsClass(what,&Player)
         {
            Send(what,@EffectSendUserDuration,#effect=EFFECT_WAVER,
                  #duration=10000);
            Send(what,@MsgSendUser,#message_rsc=Toxus_poisoned_add_rsc);
         }
         
         if who <> $
         {
            Send(who,@MsgSendUser,#message_rsc=Toxus_illusion_poisoned,
                  #parm1=Send(what,@GetDef),#parm2=Send(what,@GetName),
                  #parm3=Send(what,@GetHisHer));
         }
      }

      propagate;
   }

   GetBaseSize()
   {
      % Human
      return 200;
   }

   GetOptimalRange()
   {
      % Random range between 8-15 row/col units.
      return random(200,460);
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%