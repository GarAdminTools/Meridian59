% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
MarauderAbu is Marauder

% Abutuneebo is a witch doctor.
% He is a Qor Mage who uses poisons and bleeds to his advantage.
% He's also invisible.

constants:

   include blakston.khd

   HUMAN_TRANSLATION2_HAIR_MASK = 0xff
   HUMAN_TRANSLATION2_HAIR_MUL = 1
   
   POISON_CHANCE = 6
   POISON_DURATION = 30000   %%% in milliseconds
   POISON_LOSSRATE = 10000    %%% in health points * 10^-4 / second

resources:

   MarauderAbu_name_rsc = "Abutuneebo"
   MarauderAbu_desc_rsc = \
      "Before you is a man of slightly uneven limb. He scales the  "
      "jungles with a bizarre gait in pursuit of living reagents. The "
	  "air surrounding this witch doctor is foul and devoid of life. His "
	  "mischevious grin only adds to your discomfort. Defend yourself! "
   MarauderAbu_dead_name_rsc = "dead male body"
   
      Abu_poisoned_add_rsc = \
      "As the poison travels to your head, you begin "
      "to feel a little light-headed.  The room spins around you."
   Abu_illusion_poisoned = \
      "%s%s reels as poison from your attack courses through %s veins."

   MarauderAbu_url = "http://wiki.openmeridian.org/index.php/Main_Page"
   
   Abu_hair_bb_icon = ptbb.bgf

classvars:

   vrName = MarauderAbu_name_rsc
   vrDesc = MarauderAbu_desc_rsc
   vrDead_name = MarauderAbu_dead_name_rsc

   % Abu's a human. 
   viMinSkinColor = 4
   viMaxSkinColor = 4

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE
   

   viColorTranslate1 = XLAT_TO_RED     % Shirt
   viColorTranslate2 = XLAT_TO_RED     % Pants

   vcShieldClass = &OrcShield
   viShieldColor1 = 1
   viShieldColor2 = 1


properties:

   piBaseLevel = 200
   piBaseDifficulty = 9
   viLevel = 200
   viDifficulty = 9

   piMana = 160
   piMax_Mana = 160
   
   viKarma = -100
   
   % What are the monster's chances to cast spells each check?
   % Number is out of one hundred
   piMeleeSpellChance = 75
   piRangedSpellChance = 100

   piMonsterCastDelayMinimum = 1500

   piDrawFx = DRAWFX_INVISIBLE

   piDemonSponsor = SSS_WITCHERY_NEQUZON

messages:

   SetGender()
   {
      return;
   }

   Constructed()
   {
      local i;

      % Qor PK
      plMeleeSpellBook = [ [SID_SUMMON_POISON_FOG,6,40,90],
							[SID_BRAMBLEBLAST,6,30,91],
							[SID_VAMPIRIC_DRAIN,15,30,90]];
      plRangedSpellBook = [ [SID_BLIND,13,20,90],
                            [SID_HOLD,14,10,90],
                            [SID_SLITHERBOLT,12,70,90]];

      % Faren Resist Spells up. 
      plResistances = [ [-ATCK_SPELL_FIRE, 20 ],
                        [-ATCK_SPELL_COLD, 25 ]];
                        
      psUrl = MarauderAbu_url;
      propagate;
   }

   GetBaseEmpowerments()
   {
      return [[-ATCK_SPELL_UNHOLY,15]];
   }

   SetEquipment()
   {
      local oEquipmentItem, iBonusModifier, oSpell;

      oEquipmentItem = Create(&Dagger,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&SkullMask,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&Glove,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&Circlet,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);
	  
	  oEquipmentItem = Create(&Tanktop,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);
	  
      oEquipmentItem = Create(&Skirt,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);
	  
	  oEquipmentItem = Create(&OrcShield,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      return;
   }

   SetHair()
   % Sets the hairdo, color.
   {
      local iTranslation;

      prToupee = human_hair_bald_icon;
      iTranslation = PT_GRAY_TO_BLOND;

      piBody_translations2 = piBody_translations2 & ~HUMAN_TRANSLATION2_HAIR_MASK;
      piBody_translations2 = piBody_translations2 | (iTranslation * HUMAN_TRANSLATION2_HAIR_MUL);

      return;
   }

   SetFace()
   {
      prEyes = human_eyes_ax_icon;
      prNose = human_nose_ax_icon;
      prMouth = human_mouth_ax_icon;

      return;
   }


   
      HitSideEffect(what=$,who=$)
   {
      local oSpell;

      if Random(1,POISON_CHANCE) = 1
      {
         oSpell = Send(SYS,@FindSpellByNum,#num=SID_POISON);
         Send(oSpell,@MakePoisoned,#who=what,
               #lossrate=POISON_LOSSRATE,#duration=POISON_DURATION);

         if IsClass(what,&Player)
         {
            Send(what,@EffectSendUserDuration,#effect=EFFECT_WAVER,
                  #duration=10000);
            Send(what,@MsgSendUser,#message_rsc=Abu_poisoned_add_rsc);
         }
         
         if who <> $
         {
            Send(who,@MsgSendUser,#message_rsc=Abu_illusion_poisoned,
                  #parm1=Send(what,@GetDef),#parm2=Send(what,@GetName),
                  #parm3=Send(what,@GetHisHer));
         }
      }

      propagate;
   }

   GetBaseSize()
   {
      % Human base size
      return 200;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
