% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Marauder is Human

constants:

   include blakston.khd

   EQUIPMENT_DROP_PERCENT = 100

   % Values returned by UserGet
   AUTOLOOT_GOT_ITEM = 1
   AUTOLOOT_OUT_OF_RANGE = 2
   AUTOLOOT_GOT_PARTIAL_STACK = 3
   AUTOLOOT_CANNOT_GET = 4

resources:

   % in increasing order of agony
   marauder_sound_male_ouch1 = ouchm1.wav
   marauder_sound_male_ouch2 = ouchm2.wav
   marauder_sound_male_ouch3 = ouchm3.wav
   marauder_sound_male_ouch4 = ouchm4.wav

   marauder_sound_female_ouch1 = ouchf1.wav
   marauder_sound_female_ouch2 = ouchf2.wav
   marauder_sound_female_ouch3 = ouchf3.wav
   marauder_sound_female_ouch4 = ouchf4.wav
   
   marauder_through_walls_rsc = "%s activates %s wallhack! What a cheater!"

classvars:

   vrName = NomadBandit_name_rsc
   vrDesc = NomadBandit_desc_rsc
   vrDead_name = NomadBandit_dead_name_rsc

   viTreasure_type = TID_MARAUDER

   viSpeed = SPEED_FASTER
   viAttack_type = ATCK_WEAP_SLASH
   viAttributes = MOB_ONE_TREASURE
   viDefault_behavior = AI_MOVE_REGROUP | \
      AI_FIGHT_AGGRESSIVE |  AI_FIGHT_WIZARD_KILLER | AI_FIGHT_HYPERAGGRESSIVE | AI_FIGHT_SINGLEMINDED

   viColorTranslate1 = XLAT_TO_ORANGE     % Shirt
   viColorTranslate2 = XLAT_TO_ORANGE   % Pants

   vcShieldClass = &GuildShield
   viShieldColor1 = XLAT_TO_RED
   viShieldColor2 = XLAT_TO_RED
   viShieldShape = SHIELDSHAPE_SLASH
   viInsignia = $

   viVisionDistance = 35

   viCashMin = 1500
   viCashMax = 60000

   viCanSpawnAsChampion = FALSE

   viLeadershipCost = 2200

properties:

   piBaseLevel = 150
   piBaseDifficulty = 6

   %Here so they can be set
   vrIcon = human_male_icon_rsc
   vrDead_Icon = human_dead_male_icona_rsc
   vrDead_Legs = human_dead_male_iconb_rsc
   viLevel = 150         % piBaseLevel
   viDifficulty = 6     % piBaseDifficulty
   piGender = GENDER_MALE
   viKarma = 0
   
   psURL = $
   
   piMoveSpeed = 40
   
   vrSound_aware = $
   
   ptAggroTimer = $
   
   piFailedAggroChecks = 0

messages:

   Constructed()
   {
      if ptAggroTimer = $
      {
         ptAggroTimer = CreateTimer(self,@DoAggroCheck,5000);
      }
      propagate;
   }

   SomethingLeft(what = $, iRow=0, iCol=0)
   "Follow the enemy!"
   {
      if poMaster = $
         AND what <> $
         AND IsClass(what,&Player)
         AND Send(self,@GetTarget) = what
      {
         Post(self,@MarauderFollow,#who=what);
         Post(self,@DeactivateWallhack);
      }

      propagate;
   }
   
   DoAggroCheck(timer=$)
   {
      ptAggroTimer = $;
      
      if poOwner <> $
      {
         ptAggroTimer = CreateTimer(self,@DoAggroCheck,5000);
      }
      
      if poTarget <> $
      {
         return;
      }
      
      if Send(self,@AggroPlayers)
      {
         piFailedAggroChecks = 0;
      }
      else
      {
         piFailedAggroChecks++;
      }
      
      if piFailedAggroChecks > 24
      {
         Post(self,@Delete);
      }
      
      return;
   }

   IsAlly(target = $, regroup = FALSE)
   {
      if IsClass(target,&Monster)
      {
         return TRUE;
      }

      propagate;
   }

   GetObjectFlags()
   {
      local iFlags, oIllusion;

      iFlags = OF_ATTACKABLE | OF_PLAYER | OF_DISPLAY_NAME;

      % Get the flashing/flickering/phasing/bouncing flags.
      iFlags = iFlags | piDrawEffectFlag;

      return iFlags;
   }

   GetPlayerNameColor()
   {
      local iDrawFX, iFlags;

      iFlags = 0;

      % Check for any shadow form effects.
      iDrawfX = Send(self,@GetDrawingEffects);
      if (iDrawFX & DRAWFX_BLACK) = DRAWFX_BLACK
      {
         iFlags = iFlags | NC_SHADOW;
      }

      iFlags = iFlags | NC_KILLER;

      return iFlags;
   }

   GetClientObjectType()
   {
      return OT_KILLER;
   }

   GetURL()
   {
      return psURL;
   }

   GetOuchSound()
   {
      local iHealthFraction;

      % Since piHealth is inflated by a factor of 100, 
      % this returns the correct fraction.
      if piMax_Hit_points > 0
      {
         iHealthFraction = piHit_points / piMax_Hit_points;
      }
      else
      {
         iHealthFraction = 0;
      }

      if piGender = GENDER_MALE
      {
         if iHealthFraction > 75
         {
            return marauder_sound_male_ouch1;
         }

         if iHealthFraction > 50
         {
            return marauder_sound_male_ouch2;
         }

         if iHealthFraction > 25
         {
            return marauder_sound_male_ouch3;
         }

         return marauder_sound_male_ouch4;
      }

      if iHealthFraction > 75
      {
         return marauder_sound_female_ouch1;
      }

      if iHealthFraction > 50
      {
         return marauder_sound_female_ouch2;
      }

      if iHealthFraction > 25
      {
         return marauder_sound_female_ouch3;
      }

      return marauder_sound_female_ouch4;
   }

   MarauderFollow(who=$)
   {
      local oNewRoom;

      if who = $
      {
         return;
      }

      oNewRoom = Send(who,@GetOwner);
      if oNewRoom = $
      {
         return;
      }
       
      if IsClass(oNewRoom,&GuildHall)
         OR Send(oNewRoom,@CheckRoomFlag,#flag=ROOM_NO_COMBAT)
         OR Send(oNewRoom,@CheckRoomFlag,#flag=ROOM_SANCTUARY)
         OR Send(oNewRoom,@CheckRoomFlag,#flag=ROOM_SAFE_DEATH)
         OR poOwner = $
      {
         return;
      }

      Send(oNewRoom,@NewHold,#what=self,
            #new_row=Send(who,@GetRow),#new_col=Send(who,@GetCol),
            #fine_row=Send(who,@GetFineRow),#fine_col=Send(who,@GetFineCol),
            #new_angle=Send(who,@GetAngle));

      return;
   }
   
   AggroPlayers()
   {
      local i, each_obj;
      
      if poOwner <> $
      {
         foreach i in Send(poOwner,@GetHolderActive)
         {
            each_obj = Send(poOwner,@HolderExtractObject,#data=i);
            if IsClass(each_obj,&Player)
            {
               Send(self,@TargetSwitch,#what=each_obj,#iHatred=100);
               Send(self,@EnterStateChase,#target=each_obj,#actnow=TRUE);
               
               return TRUE;
            }
         }
      }
      return FALSE;
   }
   
   NewOwner()
   {
      Post(self,@AggroPlayers);
      propagate;
   }

   CreateTreasure(who = $, corpse = $,ladderID=0)
   {
      local oTreasure, iNumber, iNumberOfReagentsDropped, lReagentClasses, oUsedItem, oItemAtt, 
            lItemClasses, iNumLoot, lShirtClasses, lPantsClasses, lMaskClasses;

      lReagentClasses = $;
      lReagentClasses = Cons(&DarkAngelFeather,lReagentClasses);
      lReagentClasses = Cons(&WebMoss,lReagentClasses);
      lReagentClasses = Cons(&BlueDragonScale,lReagentClasses);
      lReagentClasses = Cons(&DragonflyEye,lReagentClasses);
      lReagentClasses = Cons(&ElderBerry,lReagentClasses);
      lReagentClasses = Cons(&Diamond,lReagentClasses);
      lReagentClasses = Cons(&Emerald,lReagentClasses);
      lReagentClasses = Cons(&EntrootBerry,lReagentClasses);
      lReagentClasses = Cons(&FairyWing,lReagentClasses);
      lReagentClasses = Cons(&Herbs,lReagentClasses);
      lReagentClasses = Cons(&KriipaClaw,lReagentClasses);
      lReagentClasses = Cons(&RainbowFern,lReagentClasses);
      lReagentClasses = Cons(&BlueMushroom,lReagentClasses);
      lReagentClasses = Cons(&CyanMushroom,lReagentClasses);
      lReagentClasses = Cons(&GrayMushroom,lReagentClasses);
      lReagentClasses = Cons(&GreenMushroom,lReagentClasses);
      lReagentClasses = Cons(&PurpleMushroom,lReagentClasses);
      lReagentClasses = Cons(&RedMushroom,lReagentClasses);
      lReagentClasses = Cons(&Mushroom,lReagentClasses);
      lReagentClasses = Cons(&YellowMushroom,lReagentClasses);
      lReagentClasses = Cons(&OrcTooth,lReagentClasses);
      lReagentClasses = Cons(&PolishedSeraphym,lReagentClasses);
      lReagentClasses = Cons(&Yrxlsap,lReagentClasses);
      lReagentClasses = Cons(&Ruby,lReagentClasses);
      lReagentClasses = Cons(&ShamanBlood,lReagentClasses);
      lReagentClasses = Cons(&Sapphire,lReagentClasses);
      lReagentClasses = Cons(&Solagh,lReagentClasses);
      lReagentClasses = Cons(&UncutSeraphym,lReagentClasses);
      
      lItemClasses = $;
      lItemClasses = Cons(&MysticSword,lItemClasses);
      lItemClasses = Cons(&Scimitar,lItemClasses);
      lItemClasses = Cons(&Axe,lItemClasses);
      lItemClasses = Cons(&Hatchet,lItemClasses);
      lItemClasses = Cons(&ShortSword,lItemClasses);
      lItemClasses = Cons(&Dagger,lItemClasses);
      lItemClasses = Cons(&Hammer,lItemClasses);
      lItemClasses = Cons(&Mace,lItemClasses);
      lItemClasses = Cons(&NeruditeSword,lItemClasses);
      lItemClasses = Cons(&BanditScimitar,lItemClasses);
      lItemClasses = Cons(&BattleBow,lItemClasses);
      lItemClasses = Cons(&Longbow,lItemClasses);
      lItemClasses = Cons(&CasterMace,lItemClasses);
      lItemClasses = Cons(&Flail,lItemClasses);
      lItemClasses = Cons(&Maul,lItemClasses);
      lItemClasses = Cons(&DiscipleRobe,lItemClasses);
      lItemClasses = Cons(&LightDiscipleRobe,lItemClasses);
      lItemClasses = Cons(&PlateArmor,lItemClasses);
      lItemClasses = Cons(&NeruditeArmor,lItemClasses);
      lItemClasses = Cons(&ChainArmor,lItemClasses);
      lItemClasses = Cons(&LeatherArmor,lItemClasses);
      lItemClasses = Cons(&ScaleArmor,lItemClasses);
      lItemClasses = Cons(&SimpleHelm,lItemClasses);
      lItemClasses = Cons(&SpikedHelm,lItemClasses);
      lItemClasses = Cons(&Circlet,lItemClasses);
      lItemClasses = Cons(&IvyCirclet,lItemClasses);
      lItemClasses = Cons(&KnightHelm,lItemClasses);
      lItemClasses = Cons(&RunedHelm,lItemClasses);
      
      lShirtClasses = $;
      lShirtClasses = Cons(&Shirt,lShirtClasses);
      lShirtClasses = Cons(&Tanktop,lShirtClasses);
      
      lPantsClasses = $;
      lPantsClasses = Cons(&Skirt,lPantsClasses);
      lPantsClasses = Cons(&PantsA,lPantsClasses);
      lPantsClasses = Cons(&PantsB,lPantsClasses);
      lPantsClasses = Cons(&PantsC,lPantsClasses);
      lPantsClasses = Cons(&PantsD,lPantsClasses);
      lPantsClasses = Cons(&ShortSkirt,lPantsClasses);
      
      lMaskClasses = $;
      lMaskClasses = Cons(&AntMask,lMaskClasses);
      lMaskClasses = Cons(&XeoMask,lMaskClasses);
      lMaskClasses = Cons(&DaemonMask,lMaskClasses);
      lMaskClasses = Cons(&FeyMask,lMaskClasses);
      lMaskClasses = Cons(&KriipaMask,lMaskClasses);
      lMaskClasses = Cons(&MummyMask,lMaskClasses);
      lMaskClasses = Cons(&RatMask,lMaskClasses);
      lMaskClasses = Cons(&ShrunkenHeadMask,lMaskClasses);
      lMaskClasses = Cons(&StoneTrollMask,lMaskClasses);
      lMaskClasses = Cons(&SkullMask,lMaskClasses);
      lMaskClasses = Cons(&TrollMask,lMaskClasses);
      
      iNumberOfReagentsDropped = Random(5,12);
      
      while iNumberOfReagentsDropped > 0
      {
         oTreasure = Create(Nth(lReagentClasses,Random(1,Length(lReagentClasses))),#corpse=corpse,#ladderID=1);
         iNumber = Random(5,35);
         Send(oTreasure,@AddNumber,#number=iNumber);

         if Send(poOwner,@ReqNewHold,#what=oTreasure,#new_row=piRow,#new_col=piCol)
         {
            Send(poOwner,@NewHold,#what=oTreasure,
                  #new_row=piRow,#new_col=piCol,
                  #fine_row=piFine_row,#fine_col=piFine_col);
         }
         else
         {
            Send(oTreasure,@Delete);
         }
         iNumberOfReagentsDropped--;
      }

      iNumLoot = Random(2,7);
      
      while iNumLoot > 0
      {
         oTreasure = Create(Nth(lItemClasses,Random(1,Length(lItemClasses))),#corpse=corpse,#ladderID=1,#level=Bound(viLevel,20,240));
         Post(oTreasure,@RollLootMods,#oMonster=self);

         if Send(poOwner,@ReqNewHold,#what=oTreasure,#new_row=piRow,#new_col=piCol)
         {
            Send(poOwner,@NewHold,#what=oTreasure,
                  #new_row=piRow,#new_col=piCol,
                  #fine_row=piFine_row,#fine_col=piFine_col);
         }
         else
         {
            Send(oTreasure,@Delete);
         }
         iNumLoot--;
      }

      iNumLoot = 1;
      
      while iNumLoot > 0
      {
         oTreasure = Create(Nth(lShirtClasses,Random(1,Length(lShirtClasses))),#corpse=corpse,#ladderID=1,#level=Bound(viLevel,20,240));
         Post(oTreasure,@RollLootMods,#oMonster=self);
         Send(oTreasure,@Randomize);

         if Send(poOwner,@ReqNewHold,#what=oTreasure,#new_row=piRow,#new_col=piCol)
         {
            Send(poOwner,@NewHold,#what=oTreasure,
                  #new_row=piRow,#new_col=piCol,
                  #fine_row=piFine_row,#fine_col=piFine_col);
         }
         else
         {
            Send(oTreasure,@Delete);
         }
         iNumLoot--;
      }

      iNumLoot = 1;
      
      while iNumLoot > 0
      {
         oTreasure = Create(Nth(lPantsClasses,Random(1,Length(lPantsClasses))),#corpse=corpse,#ladderID=1,#level=Bound(viLevel,20,240));
         Post(oTreasure,@RollLootMods,#oMonster=self);
         Send(oTreasure,@Randomize);

         if Send(poOwner,@ReqNewHold,#what=oTreasure,#new_row=piRow,#new_col=piCol)
         {
            Send(poOwner,@NewHold,#what=oTreasure,
                  #new_row=piRow,#new_col=piCol,
                  #fine_row=piFine_row,#fine_col=piFine_col);
         }
         else
         {
            Send(oTreasure,@Delete);
         }
         iNumLoot--;
      }

      iNumLoot = Random(-2,1);
      
      while iNumLoot > 0
      {
         oTreasure = Create(Nth(lMaskClasses,Random(1,Length(lMaskClasses))),#corpse=corpse,#ladderID=1,#level=Bound(viLevel,20,240));
         Post(oTreasure,@RollLootMods,#oMonster=self);

         if Send(poOwner,@ReqNewHold,#what=oTreasure,#new_row=piRow,#new_col=piCol)
         {
            Send(poOwner,@NewHold,#what=oTreasure,
                  #new_row=piRow,#new_col=piCol,
                  #fine_row=piFine_row,#fine_col=piFine_col);
         }
         else
         {
            Send(oTreasure,@Delete);
         }
         iNumLoot--;
      }

      oTreasure = Create(&Money,#corpse=corpse,#ladderID=1);
      iNumber = Random(500,35000);
      Send(oTreasure,@AddNumber,#number=iNumber);
      if Send(poOwner,@ReqNewHold,#what=oTreasure,#new_row=piRow,#new_col=piCol)
      {
         Send(poOwner,@NewHold,#what=oTreasure,
               #new_row=piRow,#new_col=piCol,
               #fine_row=piFine_row,#fine_col=piFine_col);
      }
      else
      {
         Send(oTreasure,@Delete);
      }

      oItemAtt = Send(SYS,@FindItemAttByNum,#num=IA_CORPSEPOINTER);

      foreach oUsedItem in plUsing
      {
         if NOT pbIllusion
            AND NOT IsClass(oUsedItem,&MithrilShield)
            AND NOT IsClass(oUsedItem,&SoldierShield)
         {
            if (oItemAtt <> $) AND Send(oItemAtt,@ReqAddToItem,#oItem=oUsedItem)
            {
               Send(oItemAtt,@AddToItem,#oItem=oUsedItem,#state1=corpse);
            }
            
            Send(oUsedItem,@SetLadderID,#value=1);

            Send(poOwner,@NewHold,#what=oUsedItem,
                  #new_row=piRow,#new_col=piCol,
                  #fine_row=piFine_row,#fine_col=piFine_col);
         }
         else
         {
            Send(oUsedItem,@Delete);
         }
      }

      plUsing = $;

      Send(self,@DoTreasureTypeTreasure,#who=who,#corpse=corpse,#ladderID=ladderID);
      return;
   }

   DoTreasureTypeTreasure(who=$,corpse=$,ladderID=0)
   {
      local i, j, oReagent, iNumberItems, oTreasure, oTreasure_type,
            lTreasureItems, iNumberOfItemsDropped, iNumberOfItemsLooted,
            iRandomCashAmount, oMoney, iGetResult, bCapacityFailure,
            bRangeFailure, iSurvivalLoot, iNumShards, oShard, iRand,
            oBestiaryBook;

      % Apparitions, etc, provide no treasure
      if pbIllusion
         OR vbSummoned
      {
         return;
      }

      oTreasure_type = Send(SYS,@FindTreasureByNum,
                              #num=Send(self,@GetTreasureType));
      if oTreasure_type = $
      {
         Debug("Can't find monster treasure type",Send(self,@GetTreasureType),
               vrname,self);

         return;
      }

      % TreasureType now handles putting together the treasure list.
      % Returns a list of items to drop, or $.
      lTreasureItems = Send(oTreasure_Type,@CreateMobTreasure,#mob=self,
                              #who=who,#level=piLevel,#difficulty=viDifficulty,
                              #corpse=corpse,#attributes=viAttributes,
                              #boostedlevel=piBoostedLevel,
                              #iChaosLevel=piChaosLevel,
                              #ladderID=ladderID);

      % Add in our inventory
      foreach i in plActive
      {
         lTreasureItems = Cons(i,lTreasureItems);
      }

      foreach i in plPassive
      {
         lTreasureItems = Cons(i,lTreasureItems);
      }

      % Add money to the treasure
      iRandomCashAmount = Random(viCashMin,viCashMax);
      if iRandomCashAmount > 0
      {
         % Don't send a corpse unless the player killed the mob. Minion kills
         % already handed to the player in KilledSomething and double-checked
         % in Killed. Corpse is used for the loot timer, which GenerateTreasure
         % handles for monster kills but Constructor in Item does not.
         if NOT IsClass(who,&Player)
         {
            corpse = $;
         }

         oMoney = Create(&Money,#corpse=corpse,#number=(iRandomCashAmount + (piBoostedLevel
                     * Send(Send(SYS,@GetSurvivalRoomMaintenance),@GetSurvivalCash)) + (piChaosLevel
                     * Send(Send(SYS,@GetChaosStrataMaintenance),@GetChaosCash)))
                     * Send(self,@GetChampionRewardMultiplier),#ladderID=ladderID);

         Send(Send(SYS,@GetStatistics),@MoneyCreated,#amount=Send(oMoney,@GetNumber));
         lTreasureItems = Cons(oMoney,lTreasureItems);
      }

      % Add shards to champion treasure
      if Send(self,@GetChampStatus) > 0
      {
         iNumShards = Send(self,@GetChampionRewardMultiplier);
         if poOwner <> $
         {
            foreach i in Send(poOwner,@GetObjectAttributes)
            {
               if IsClass(i,&RoomThreat)
               {
                  iNumShards = Send(i,@AlterShardDrops,#iNumShards=iNumShards);
               }
            }
         }
         if Send(self,@GetLevel) < 50
         {
            --iNumShards;
         }
         if Send(self,@GetLevel) < 100
         {
            --iNumShards;
         }
         if Send(self,@GetLevel) < 150
         {
            --iNumShards;
         }
         while iNumShards > 0
         {
            iRand = Random(1,Send(SETTINGS_OBJECT,@GetEmpowerShardProbability)/100);
            if iRand = 1
            {
               oShard = Create(&ShardEmpowerment,#ladderID=ladderID);
            }
            else
            {
               iRand = Random(1,Send(SETTINGS_OBJECT,@GetMajorShardProbability)/100);
               if iRand = 1
               {
                  oShard = Create(&ShardImbueMajor,#ladderID=ladderID);
               }
               else
               {
                  iRand = Random(1,Send(SETTINGS_OBJECT,@GetDrainShardProbability)/100);
                  if iRand = 1
                  {
                     oShard = Create(&ShardRemove,#ladderID=ladderID);
                  }
                  else
                  {
                     iRand = Random(1,Send(SETTINGS_OBJECT,@GetAugmentShardProbability)/100);
                     if iRand = 1
                     {
                        oShard = Create(&ShardAugment,#ladderID=ladderID);
                     }
                     else
                     {
                        if IsClass(poOwner,&DesertRoom)
                           OR IsClass(poOwner,&MinotaurMazeRoom)
                           OR IsClass(poOwner,&AncientTomb1)
                        {
                           iRand = Random(1,Send(SETTINGS_OBJECT,@GetInstillingShardProbability)/10);
                           if iRand = 1
                           {
                              oShard = Create(&ShardReroll,#ladderID=ladderID);
                           }
                           else
                           {
                              iRand = Random(1,Send(SETTINGS_OBJECT,@GetImprovementShardProbability)/10);
                              if iRand = 1
                              {
                                 oShard = Create(&ShardImprovement,#ladderID=ladderID);
                              }
                              else
                              {
                                 iRand = Random(1,Send(SETTINGS_OBJECT,@GetDisruptionShardProbability)/10);
                                 if iRand = 1
                                 {
                                    oShard = Create(&ShardReImbueMajor,#ladderID=ladderID);
                                 }
                                 else
                                 {
                                    oShard = Create(&ShardReImbueMinor,#ladderID=ladderID);
                                 }
                              }
                           }
                        }
                        else
                        {
                           oShard = Create(&ShardImbueMinor,#ladderID=ladderID);
                        }
                     }
                  }
               }
            }
            
            lTreasureItems = Cons(oShard,lTreasureItems);
            --iNumShards;
         }
         
         if Random(1,100) = 1
         {
            if Random(1,2) = 1
            {
               oBestiaryBook = Create(&BestiarySpellBook,#ladderID=ladderID);
            }
            else
            {
               oBestiaryBook = Create(&BestiarySkillBook,#ladderID=ladderID);
            }
            lTreasureItems = Cons(oBestiaryBook,lTreasureItems);
         }
      }

      % Add shards to chaos stratum treasure
      if piChaosLevel > 0
      {
         iNumShards = Random(-5,piChaosLevel/40);
         
         if poOwner <> $
            AND Send(poOwner,@IsBoss,#what=self)
         {
            iNumShards = 4 + piChaosLevel/40;
         }

         if Send(self,@GetLevel) < 50
         {
            --iNumShards;
         }
         if Send(self,@GetLevel) < 100
         {
            --iNumShards;
         }
         if Send(self,@GetLevel) < 150
         {
            --iNumShards;
         }
         while iNumShards > 0
         {
            iRand = Random(1,Send(SETTINGS_OBJECT,@GetEmpowerShardProbability)/100);
            if iRand = 1
            {
               oShard = Create(&ShardEmpowerment,#ladderID=ladderID);
            }
            else
            {
               iRand = Random(1,Send(SETTINGS_OBJECT,@GetMajorShardProbability)/100);
               if iRand = 1
               {
                  oShard = Create(&ShardImbueMajor,#ladderID=ladderID);
               }
               else
               {
                  iRand = Random(1,Send(SETTINGS_OBJECT,@GetDrainShardProbability)/100);
                  if iRand = 1
                  {
                     oShard = Create(&ShardRemove,#ladderID=ladderID);
                  }
                  else
                  {
                     iRand = Random(1,Send(SETTINGS_OBJECT,@GetAugmentShardProbability)/100);
                     if iRand = 1
                     {
                        oShard = Create(&ShardAugment,#ladderID=ladderID);
                     }
                     else
                     {
                        if IsClass(poOwner,&DesertRoom)
                           OR IsClass(poOwner,&MinotaurMazeRoom)
                           OR IsClass(poOwner,&AncientTomb1)
                        {
                           iRand = Random(1,Send(SETTINGS_OBJECT,@GetInstillingShardProbability)/10);
                           if iRand = 1
                           {
                              oShard = Create(&ShardReroll,#ladderID=ladderID);
                           }
                           else
                           {
                              iRand = Random(1,Send(SETTINGS_OBJECT,@GetImprovementShardProbability)/10);
                              if iRand = 1
                              {
                                 oShard = Create(&ShardImprovement,#ladderID=ladderID);
                              }
                              else
                              {
                                 iRand = Random(1,Send(SETTINGS_OBJECT,@GetDisruptionShardProbability)/10);
                                 if iRand = 1
                                 {
                                    oShard = Create(&ShardReImbueMajor,#ladderID=ladderID);
                                 }
                                 else
                                 {
                                    oShard = Create(&ShardReImbueMinor,#ladderID=ladderID);
                                 }
                              }
                           }
                        }
                        else
                        {
                           oShard = Create(&ShardImbueMinor,#ladderID=ladderID);
                        }
                     }
                  }
               }
            }
            
            lTreasureItems = Cons(oShard,lTreasureItems);
            --iNumShards;
         }
         
         if poOwner <> $
            AND Send(poOwner,@IsBoss,#what=self)
         {
            if piChaosLevel = 140
            {
               lTreasureItems = Cons(Create(&QorCharm,#ladderID=ladderID),lTreasureItems);
            }
            else if Random(1,100) <= Send(Send(SYS,@GetChaosStrataMaintenance),@GetBaseScryingCrystalDropChance) + Send(poOwner,@GetQuality)
            {
               lTreasureItems = Cons(Create(&ScryingCrystal,#iTier=Bound(Send(poOwner,@GetTier)+Random(-1,1),1,5),
                                                            #iChaosLevel=piChaosLevel+1,
                                                            #level=180+piChaosLevel,
                                                            #ladderID=ladderID),lTreasureItems);
      
               if Send(poOwner,@GetQuality) >= 100
                  AND Random(1,100) <= Send(poOwner,@GetQuality) - 100
               {
                  lTreasureItems = Cons(Create(&ScryingCrystal,#iTier=Bound(Send(poOwner,@GetTier)+Random(-1,1),1,5),
                                                               #iChaosLevel=piChaosLevel+1,
                                                               #level=180+piChaosLevel,
                                                               #ladderID=ladderID),lTreasureItems);
               }
            }
         }
      }

      iNumberOfItemsDropped = 0;
      iNumberOfItemsLooted = 0;
      bCapacityFailure = FALSE;
      bRangeFailure = FALSE;
      
      % Now drop all the treasure.
      foreach oTreasure in lTreasureItems
      {
         if Send(poOwner,@ReqNewHold,#what=oTreasure,#new_row=piRow,
                 #new_col=piCol)
         {
            Send(poOwner,@NewHold,#what=oTreasure,
                  #new_row=piRow,#new_col=piCol,
                  #fine_row=piFine_row,#fine_col=piFine_col);

            if NOT IsClass(oTreasure,&Money)
            {
               ++iNumberOfItemsDropped;
            }

            if IsClass(oTreasure,&NumberItem)
               AND Send(SYS,@UtilGetRoom,#what=oTreasure) = Send(who,@GetOwner)
               AND Send(oTreasure,@GetPos) <> $
               AND IsClass(who,&User)
               AND Send(who,@CheckPlayerPreference,#flag=CF_AUTOLOOT)
               AND (IsClass(oTreasure,&Money)
                       OR (NOT bRangeFailure
                           AND NOT bCapacityFailure))
            {
               iGetResult = Send(who,@UserGet,#what=oTreasure,#report=FALSE);
               if NOT IsClass(oTreasure,&Money)
                  AND (iGetResult = AUTOLOOT_GOT_ITEM
                         OR iGetResult = AUTOLOOT_GOT_PARTIAL_STACK)
               {
                  ++iNumberOfItemsLooted;
               }

               if iGetResult = AUTOLOOT_OUT_OF_RANGE
               {
                  bRangeFailure = TRUE;
               }
               else if (iGetResult = AUTOLOOT_GOT_PARTIAL_STACK
                        OR iGetResult = AUTOLOOT_CANNOT_GET)
               {
                  bCapacityFailure = TRUE;
               }
            }
         }
         else
         {
            Send(oTreasure,@Delete);
         }
      }

      % Handle autoloot resources displayed to user.
      if iNumberOfItemsDropped <> 0
         AND IsClass(who,&User)
         AND Send(who,@CheckPlayerPreference,#flag=CF_AUTOLOOT)
      {
         if bRangeFailure
         {
            Send(who,@MsgSendUser,#message_rsc=autoloot_too_far_away);

            return;
         }

         if bCapacityFailure
         {
            if iNumberOfItemsLooted > 0
            {
               Send(who,@MsgSendUser,#message_rsc=autoloot_got_some_but_full_now);
            }
            else if iNumberOfItemsLooted = 0
            {
               if iRandomCashAmount > 0
               {
                  Send(who,@MsgSendUser,#message_rsc=autoloot_got_only_shillings_because_full,
                        #parm1=Send(oMoney,@GetNumber));
               }
               else
               {
                  Send(who,@MsgSendUser,#message_rsc=autoloot_got_nothing_because_full);
               }
            }

            return;
         }

         if iNumberOfItemsLooted > 0
         {
            if iNumberOfItemsLooted = iNumberOfItemsDropped
            {
               if iRandomCashAmount > 0
               {
                  Send(who,@MsgSendUser,#message_rsc=autolooted_all,
                        #parm1=Send(oMoney,@GetNumber));
               }
               else
               {
                  Send(who,@MsgSendUser,#message_rsc=autolooted_all_no_money);
               }
            }
            else if iNumberOfItemsLooted < iNumberOfItemsDropped
            {
               if iRandomCashAmount > 0
               {
                  Send(who,@MsgSendUser,#message_rsc=autolooted_some,
                        #parm1=Send(oMoney,@GetNumber));
               }
               else
               {
                  Send(who,@MsgSendUser,#message_rsc=autolooted_some_no_money);
               }
            }
         }
         else if iNumberOfItemsLooted = 0
         {
            if iRandomCashAmount > 0
            {
               Send(who,@MsgSendUser,#message_rsc=autolooted_only_money,
                     #parm1=Send(oMoney,@GetNumber));
            }
            else if iRandomCashAmount <= 0
            {
               Send(who,@MsgSendUser,#message_rsc=autolooted_none);
            }
         }
      }

      return;
   }

   Delete()
   {
      if ptAggroTimer <> $
      {
         DeleteTimer(ptAggroTimer);
      }
      ptAggroTimer = $;
      propagate;
   }
   
   ActivateWallhack()
   {
      local i, each_obj;
      
      Send(self,@SetBehaviorFlag,#flag=AI_MOVE_WALKTHROUGH_WALLS,#value=TRUE);
      
      if poOwner <> $
      {
         foreach i in Send(poOwner,@GetHolderActive)
         {
            each_obj = Send(poOwner,@HolderExtractObject,#data=i);
            if IsClass(each_obj,&Player)
            {
               Send(each_obj,@MsgSendUser,#message_rsc=marauder_through_walls_rsc,#parm1=Send(self,@GetName),#parm2=Send(self,@GetHisHer));
            }
         }
      }
      return;
   }
   
   DeactivateWallhack()
   {
      Send(self,@SetBehaviorFlag,#flag=AI_MOVE_WALKTHROUGH_WALLS,#value=FALSE);
      return;
   }

   TryAttack(what=$,stroke_obj=$)
   {
      
      propagate;
   }
   
   GetEpicRarityBoost()
   {
      % Out of 100,000
      return 50000;
   }
   
   GetRareRarityBoost()
   {
      return 50000;
   }
   
   CanBeCharmed()
   {
      return FALSE;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
