% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
MarauderLuganeon is Marauder

% Luganeon is a human Knight


constants:

   include blakston.khd

   HUMAN_TRANSLATION2_HAIR_MASK = 0xff
   HUMAN_TRANSLATION2_HAIR_MUL = 1

resources:

   MarauderLuganeon_name_rsc = "Luganeon"
   MarauderLuganeon_desc_rsc = \
      "The highest ranking knight of the Radiant Outriders. "
      "He has orders to eradicate humans of this Meridian! Defend yourself!"
   MarauderLuganeon_dead_name_rsc = "dead male body"

   MarauderLuganeon_url = "http://wiki.openmeridian.org/index.php/Main_Page"
   
   Luganeon_hair_bb_icon = ptbb.bgf

classvars:

   vrName = MarauderLuganeon_name_rsc
   vrDesc = MarauderLuganeon_desc_rsc
   vrDead_name = MarauderLuganeon_dead_name_rsc

   % Luganeon's a human.
   viMinSkinColor = 2
   viMaxSkinColor = 2

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viColorTranslate1 = XLAT_TO_BLUE     % Shirt
   viColorTranslate2 = XLAT_TO_BLACK     % Pants

   vcShieldClass = &Knightshield
   viShieldColor1 = 1
   viShieldColor2 = 1


properties:

   piBaseLevel = 350
   piBaseDifficulty = 10
   viLevel = 350
   viDifficulty = 10

   piMana = 120
   piMax_Mana = 120
   
   % What are the monster's chances to cast spells each check?
   % Number is out of one hundred
   piMeleeSpellChance = 50
   piRangedSpellChance = 100

   piMonsterCastDelayMinimum = 1500



messages:

   SetClothes()
   {
      Send(self,@SetDefaultClothes,#shirt_color=viColorTranslate1);
      Send(self,@SetTroopLegs,#translation=viColorTranslate2);

      return;
   }

   SetGender()
   {
      return;
   }

   Constructed()
   {
      local i;

      % KC Guy
      plMeleeSpellBook = [ [SKID_SHIELD_BASH,6,100,90]];
      plRangedSpellBook = [ [SID_ANTIMAGIC_AURA,13,20,90],
                            [SKID_CHARGE_ATTACK,14,100,90]];

      
      plResistances = [ [-ATCK_SPELL_FIRE, -20 ],
                        [-ATCK_SPELL_COLD, -25 ],
                        [-ATCK_SPELL_ACID, -10],
                        [ATCK_WEAP_NERUDITE,-10]];
                        
      psUrl = MarauderLuganeon_url;
      propagate;
   }

   GetBaseEmpowerments()
   {
      return [[ATCK_WEAP_BLUDGEON,30]];
   }

   SetEquipment()
   {
      local oEquipmentItem, iBonusModifier, oSpell;

      oEquipmentItem = Create(&Hammer,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&PlateArmor,#level=viLevel,#school=SS_QOR);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&Gauntlet,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&KnightHelm,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      return;
   }

   SetHair()
   % Sets the hairdo, color.
   {
      local iTranslation;

      prToupee = human_hair_bald_icon;
      iTranslation = PT_GRAY_TO_PLATBLOND;

      piBody_translations2 = piBody_translations2 & ~HUMAN_TRANSLATION2_HAIR_MASK;
      piBody_translations2 = piBody_translations2
                                 | (iTranslation * HUMAN_TRANSLATION2_HAIR_MUL);

      return;
   }

   SetFace()
   {
      prEyes = human_eyes_bx_icon;
      prNose = human_nose_ax_icon;
      prMouth = human_mouth_cx_icon;

      return;
   }

   SetShield()
   {
      local oEquipmentItem;

      oEquipmentItem = Create(vcShieldClass,#color1=viShieldColor1,#color2=viShieldColor2,
                              #shape=viShieldShape);
      if viInsignia <> $
      {
         Send(oEquipmentItem,@ChangeGuildInsignia,#insignia=viInsignia);
      }

      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      return;
   }

   GetBaseSize()
   {
      % Human size
      return 200;
   }
   
   % Simulated for charge attack
   AllowPlayerAttack()
   {
      return TRUE;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
