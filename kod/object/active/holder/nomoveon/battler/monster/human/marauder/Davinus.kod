% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
MarauderDavinus is Marauder

% Davinus is a Shal Faren Mage


constants:

   include blakston.khd

   HUMAN_TRANSLATION2_HAIR_MASK = 0xff
   HUMAN_TRANSLATION2_HAIR_MUL = 1

resources:

   MarauderDavinus_name_rsc = "Davinus"
   MarauderDavinus_desc_rsc = \
      "A genius of vast intellect and prodigious memory "
      "who came to be known as -the- Radiant Archmage. Davinus proudly "
      "serves the Radiant Outriders to feed his insatiable quest for knowledge. He is no "
      "stranger to carrying out genocide, if that is what is required of him. "
      "He is a murderer. Defend yourself!" 
   MarauderDavinus_dead_name_rsc = "dead male body"

   MarauderDavinus_url = "http://wiki.openmeridian.org/index.php/Main_Page"
   
   Davinus_hair_bb_icon = ptbb.bgf

classvars:

   vrName = MarauderDavinus_name_rsc
   vrDesc = MarauderDavinus_desc_rsc
   vrDead_name = MarauderDavinus_dead_name_rsc

   % Davinus's a human.
   viMinSkinColor = 2
   viMaxSkinColor = 2

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viColorTranslate1 = XLAT_TO_RED     % Shirt
   viColorTranslate2 = XLAT_TO_RED     % Pants

   vcShieldClass = &MithrilShield
   viShieldColor1 = 1
   viShieldColor2 = 1

   viBonusBaseTP = 8000
   vbBonusXPGain = 8000

properties:

   piBaseLevel = 350
   piBaseDifficulty = 7
   viLevel = 350 
   viDifficulty = 7
   viKarma = 100

   piMana = 120
   piMax_Mana = 120
   
   % What are the monster's chances to cast spells each check?
   % Number is out of one hundred
   piMeleeSpellChance = 100
   piRangedSpellChance = 100

   piMonsterCastDelayMinimum = 1500


messages:

   SetGender()
   {
      return;
   }

   Constructed()
   {
      local i;

      % Qor PK
      plMeleeSpellBook = [  [SID_RADIANCE,30,20,99],
                            [SID_EXPLOSIVE_FROST,2,100,90]];
      plRangedSpellBook = [ [SID_PURGE,25,30,90],
                            [SID_DAZZLE,10,60,90],
                            [SID_RING_OF_FLAMES,5,70,50],
                            [SID_LIGHTNING_BOLT,5,95,90],
                            [SID_LIGHTNING_STRIKE,5,100,90]];

      % Half-demon base resists
      plResistances = [ [-ATCK_SPELL_FIRE,20 ],
                        [-ATCK_SPELL_COLD,25 ],
                        [-ATCK_SPELL_ACID,40 ],
                        [-ATCK_SPELL_ACID,40 ],
                        [ATCK_WEAP_ALL,-10] ];
                        
      psUrl = MarauderDavinus_url;
      propagate;
   }

   GetBaseEmpowerments()
   {
      return [[-ATCK_SPELL_SHOCK,50],
				[-ATCK_SPELL_COLD,50]];
   }

   SetEquipment()
   {
      local oEquipmentItem, iBonusModifier, oSpell;

      oEquipmentItem = Create(&Flail,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&DiscipleRobe,#level=viLevel,#school=SS_FAREN);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&Glove,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&IvyCirclet,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      return;
   }

   SetHair()
   % Sets the hairdo, color.
   {
      local iTranslation;

      prToupee = human_hair_bb_icon;
      iTranslation = PT_GRAY_TO_BLOND;

      piBody_translations2 = piBody_translations2 & ~HUMAN_TRANSLATION2_HAIR_MASK;
      piBody_translations2 = piBody_translations2
                                 | (iTranslation * HUMAN_TRANSLATION2_HAIR_MUL);

      return;
   }

   SetFace()
   {
      prEyes = human_eyes_ax_icon;
      prNose = human_nose_ax_icon;
      prMouth = human_mouth_ax_icon;

      return;
   }

   SetShield()
   {
      local oEquipmentItem;

      oEquipmentItem = Create(vcShieldClass,#color1=viShieldColor1,#color2=viShieldColor2,
                              #shape=viShieldShape);
      if viInsignia <> $
      {
         Send(oEquipmentItem,@ChangeGuildInsignia,#insignia=viInsignia);
      }

      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      return;
   }

   GetBaseSize()
   {
      % Human base size
      return 200;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%