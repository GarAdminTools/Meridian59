% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
MarauderEvilChrisChan is Marauder

% EvilChrisChan is a half-demon Qor/Witchery PK.
% He uses Corrosion + Splash of Acid, as well as Blind and Hold.
% He's also invisible.

constants:

   include blakston.khd

   HUMAN_TRANSLATION2_HAIR_MASK = 0xff
   HUMAN_TRANSLATION2_HAIR_MUL = 1

resources:

   MarauderEvilChrisChan_name_rsc = "Evil ChrisChan"
   MarauderEvilChrisChan_desc_rsc = \
      "His dagger is bloodied from all the backs he's stabbed throughout the years.... "
      "He is a murderer. Defend yourself!"
   MarauderEvilChrisChan_dead_name_rsc = "dead male body"

   MarauderEvilChrisChan_url = "http://wiki.openmeridian.org/index.php/Main_Page"
   
   EvilChrisChan_hair_bb_icon = ptbb.bgf

classvars:

   vrName = MarauderEvilChrisChan_name_rsc
   vrDesc = MarauderEvilChrisChan_desc_rsc
   vrDead_name = MarauderEvilChrisChan_dead_name_rsc

   
   viMinSkinColor = 4
   viMaxSkinColor = 4

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viColorTranslate1 = XLAT_TO_RED     % Shirt
   viColorTranslate2 = XLAT_TO_RED     % Pants

   vcShieldClass = &GuildShield
   viShieldColor1 = 56
   viShieldColor2 = 56
   viShieldShape = SHIELDSHAPE_HORIZ
   viInsignia = INSIG_FAREN

   viBonusBaseTP = 2000
   vbBonusXPGain = 2000

properties:

   piBaseLevel = 140
   piBaseDifficulty = 7
   viLevel = 140
   viDifficulty = 7

   piMana = 120
   piMax_Mana = 120
   
   % What are the monster's chances to cast spells each check?
   % Number is out of one hundred
   piMeleeSpellChance = 100
   piRangedSpellChance = 100

   piMonsterCastDelayMinimum = 1500



messages:

   SetGender()
   {
      return;
   }

   Constructed()
   {
      local i;

      % Qor PK
      plMeleeSpellBook = [ [SID_VAMPIRIC_DRAIN,6,100,90]];
      plRangedSpellBook = [ [SID_BLIND,13,20,90],
                            [SID_HOLD,14,60,90],
                            [SID_ICICLE_VOLLEY,12,100,40]];

      % Half-demon base resists
      plResistances = [ [-ATCK_SPELL_FIRE, 20 ],
                        [-ATCK_SPELL_COLD, 25 ],
                        [-ATCK_SPELL_ACID, 20],
                        [ATCK_WEAP_ALL,30]];
                        
      psUrl = MarauderEvilChrisChan_url;
      propagate;
   }

   GetBaseEmpowerments()
   {
      return [[-ATCK_WEAP_PIERCE,50]];
   }

   SetEquipment()
   {
      local oEquipmentItem, iBonusModifier, oSpell;

      oEquipmentItem = Create(&Dagger,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&DiscipleRobe,#level=viLevel,#school=SS_QOR);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&Glove,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      oEquipmentItem = Create(&Circlet,#level=viLevel);
      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      return;
   }

   SetHair()
   % Sets the hairdo, color.
   {
      local iTranslation;

      prToupee = human_hair_bald_icon;
      iTranslation = PT_GRAY_TO_PLATBLOND;

      piBody_translations2 = piBody_translations2 & ~HUMAN_TRANSLATION2_HAIR_MASK;
      piBody_translations2 = piBody_translations2
                                 | (iTranslation * HUMAN_TRANSLATION2_HAIR_MUL);

      return;
   }

   SetFace()
   {
      prEyes = human_eyes_cx_icon;
      prNose = human_nose_cx_icon;
      prMouth = human_mouth_cx_icon;

      return;
   }

   SetShield()
   {
      local oEquipmentItem;

      oEquipmentItem = Create(vcShieldClass,#color1=viShieldColor1,#color2=viShieldColor2,
                              #shape=viShieldShape);
      if viInsignia <> $
      {
         Send(oEquipmentItem,@ChangeGuildInsignia,#insignia=viInsignia);
      }

      Send(self,@AddEquipmentObject,#what=oEquipmentItem);

      return;
   }

   GetBaseSize()
   {
      % Half demon base size
      return 150;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
