% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
HunterGhost is Wanderer

constants:

   include blakston.khd

resources:

   include huntghst.lkod

   HunterGhost_name_rsc = "Tendrath"
   HunterGhost_icon_rsc = nnm01.bgf
   HunterGhost_desc_rsc =  \
     "This druidic mage's demeanor is so calm and peaceful that you can't "
     "help but doubt the stories of his family's fate in the ancient city "
     "of Brax. Born not of hate nor of fury but of righteous duty, "
     "his resolve to destroy the Lich and her Necromancers is eternally unwavering. "
     "\n\nTo join the hunt say, 'I wish to take up arms against evil.'"

   huntghost_hunt_only = \
      "I may only teach that to a wielder of a hunter's sword."
   huntghost_not_powerful = \
      "~kTendrath says, \"You are far too inexperienced to wield "
      "such powerful magics.\""

   huntghost_said_resource_str = "%s%s says, \"%s~n\""

   not_strong_enough = \
      "Mind your own safety, innocent traveler. Only those of considerable strength are worthy of our righteous duty."

   angry_have_amulet = \
      "Do you think to deceive me? I sense your foul undead presence. Begone, fiend!"
   
   already_have_soth = \
      "You are already part of the eternal hunt."

   already_placed_soth = \
      "I have placed a Sword of the Hunt in the heart of the enemy's lair. If you are "
      "truly worthy of joining the hunt, infiltrate the maze-like Dungeon of Castle Brax "
      "and take up the living weapon of the hunters."

   placing_soth = \
      "You wish to join our holy crusade? I have placed a Sword of the Hunt in the heart of the enemy's lair. If you are "
      "truly worthy of joining the hunt, infiltrate the maze-like Dungeon of Castle Brax "
      "and take up the living weapon of the hunters."

classvars:

   vrName = HunterGhost_name_rsc
   vrIcon = HunterGhost_icon_rsc
   vrDesc = HunterGhost_desc_rsc

   viAttributes = \
      MOB_NOFIGHT | MOB_RANDOM | MOB_LISTEN | MOB_NOMOVE \
      | MOB_RECEIVE | MOB_TEACHER | MOB_SELLER
   viOccupation = MOB_ROLE_WIZARD
   viLevel = 120

properties:

   viQuestID = 0
   piDrawfx = DRAWFX_TRANSLUCENT_50

messages:

   CreateDestinationList()
   {
      plDestinations = [ [RID_G8, 38, 38] , [RID_I9, 25, 56],
                         [RID_CASTLE1B, 8, 3],
                         [RID_K5, 45, 23] ];

      return;
   }

   SetForSale()
   {
      plFor_Sale = [
         $,$,
         [
            SID_SUMMON_WEB,
            SID_LIGHTNINGWALL,
            SID_DEFLECT,
            SID_HUNT,
            SID_GUILD_CONVEYANCE,
            SID_DETECT_INVIS
         ],
     $];

      return;
   }

   CanDoTeach(who=$,sid=0)
   {
      if NOT Send(self,@SpecialTeachChecks,#report=TRUE,#who=who,#num=sid)
      {
         return FALSE;
      }

      propagate;
   }

   CanAddSpell(who=$,num=0,report=FALSE)
   "We require that a quest be done before one of the spells can be learned"
   {
      if NOT Send(self,@SpecialTeachChecks,#report=report,#who=who,#num=num)
      {
         return FALSE;
      }

      propagate;
   }

   SpecialTeachChecks(who=$,num=0,report=FALSE)
   "We require that a quest be done before one of the spells can be learned"
   {
      if ((num = SID_SUMMON_WEB) OR (num = SID_LIGHTNINGWALL))
         AND NOT Send(who,@CheckPlayerFlag,#flag=PFLAG_PKILL_ENABLE)
      {
         if report
         {
            Send(who,@MsgSendUser,#message_rsc=huntghost_not_powerful);
         }

         return FALSE;
      }

      return TRUE;
   }

   SomeoneSaid(what=$,type=$,string=$)
   {
      if type = SAY_YELL OR NOT IsClass(what,&User)
      {
          propagate;
      }

      if StringContain(string,"I wish to take up arms against evil")
         AND what <> $
         AND IsClass(what,&User)
      {
         if Send(what,@HasAmuletOfTheThree)
         {
            Send(poOwner,@SomeoneSaid,#what=self,#type=SAY_RESOURCE,
		          #string=angry_have_amulet);
            propagate;
         }
         if Send(what,@HasSwordOfTheHunt)
         {
            Send(poOwner,@SomeoneSaid,#what=self,#type=SAY_RESOURCE,
		          #string=already_have_soth);
            propagate;
         }
         if Send(what,@GetBaseMaxHealth) < 61
         {
            Send(poOwner,@SomeoneSaid,#what=self,#type=SAY_RESOURCE,
		          #string=not_strong_enough);
            propagate;
         }
         if Send(Send(SYS,@FindRoomByNum,#num=RID_LICH_MAZE),@HasSwordOfTheHunt,#ladderID=Send(what,@GetLadderID))
         {
            Send(poOwner,@SomeoneSaid,#what=self,#type=SAY_RESOURCE,
		          #string=already_placed_soth);
            propagate;
         }
            Send(poOwner,@SomeoneSaid,#what=self,#type=SAY_RESOURCE,
		          #string=placing_soth);
         Post(Send(SYS,@GetNecromancerBalance),@ForceGenerateSword,#ladderID=Send(what,@GetLadderID));
      }

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
