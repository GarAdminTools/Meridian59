% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
MarionElder is MarionTown

constants:

   include blakston.khd

resources:

   include mrelder.lkod

   MarionElder_name_rsc = "Ran er'Hoth"
   MarionElder_icon_rsc = mrelder.bgf
   MarionElder_desc_rsc = \
      "An ancient member of the Marion community, Ran er'Hoth has taken on a "
      "somewhat legendary status throughout Meridian, and is sought out for "
      "his opinions and knowledge by pilgrims and travellers.  Whether he is "
      "considered a wise man or the village idiot locally really depends on "
      "who you talk to."
      "\n\nHe can also help you change your stats with training "
      "if you are a newbie, if you are holding an Ancient Trinket, or "
      "if you have a free stat reset from recently reincarnating. "
      "First, reset all your stats to one by saying 'reset stats'. "
      "Then, you may say 'train might', for example, to train a point of Might. "
      "You can also say 'train five might' or 'train ten might.' "
      "The same is true for intellect, stamina, agility, mysticism, and aim. "
      "Once you begin this process, finish it before you leave the area. "
      "Your free stat reset or Ancient Trinket will be consumed when you leave. "

   MrEl_entry_welcome = \
      "Another traveller through Marion. Perhaps %s%s will come tell me of "
      "their adventures."

   Ran_orctooth = \
      "I've pulled a few from their rotting skulls up in the mountains. You "
      "see, I know where their burial grounds are, and you could say that "
      "I’m a bit of a ~Icollector~I."

   MrEl_stat_window_trigger = "change stats"

   mar_elder_cannot_train_msg = \
      "You seem to have full command of your potential in that stat. I cannot train you further."
   mar_elder_teach_msg = \
      "You may have heard this tale of wisdom, but I'll tell it again."
   mar_elder_clear_stats_first_msg = \
      "You must say 'reset stats' first to clear your stats. "
      "That will begin the training process. Once it has started, "
      "do not leave before you finish training."

classvars:

   viDefinite = ARTICLE_NONE
   viIndefinite = ARTICLE_NONE
   vrName = MarionElder_name_rsc
   vrIcon = MarionElder_icon_rsc
   vrDesc = MarionElder_desc_rsc
   viAttributes = \
      MOB_RANDOM | MOB_LISTEN | MOB_NOFIGHT | MOB_NOMOVE \
      | MOB_TEACHER | MOB_RECEIVE | MOB_COND_SELLER
   viOccupation = MOB_ROLE_ELDER

properties:

messages:

   Constructor()
   {
      plWantedItems = [&StatsResetToken];

      propagate;
   }

   SomeoneSaid(what = $,type = $,string = $,parm1 = $,parm2 = $,parm3 = $,
               parm4 = $, parm5 = $,parm6 = $,parm7 = $,parm8 = $)
   {
      local iResult;

      if StringEqual(string,MrEl_stat_window_trigger)
      {
         if Send(what,@FindHolding,#class=&StatsResetToken) <> $
            OR (Send(what,@GetBaseMaxHealth)
               <= Send(SETTINGS_OBJECT,@GetFreeStatsResetCap))
            OR Send(what,@HasReincarnationStatsReset)
         {
            Send(what,@SendStatChange);
         }
      }

      if what <> $
         AND IsClass(what,&User)
         AND (Send(what,@FindHolding,#class=&StatsResetToken) <> $
            OR (Send(what,@GetBaseMaxHealth)
               <= Send(SETTINGS_OBJECT,@GetFreeStatsResetCap))
            OR Send(what,@HasReincarnationStatsReset))
      {
         if StringContain(string,"reset stats")
         {
            Send(what,@ClearStats);
            return;
         }
         
         if NOT Send(what,@ClearedStatsRecently)
         {
            Send(self,@SayToOne,#target=what,#message_rsc=mar_elder_clear_stats_first_msg);
            return;
         }

         if StringContain(string,"train might")
         {
            iResult = Send(what,@AttemptTrainStatPoint,#type=STAT_ID_MIGHT);
         }
         if StringContain(string,"train intellect")
         {
            iResult = Send(what,@AttemptTrainStatPoint,#type=STAT_ID_INTELLECT);
         }
         if StringContain(string,"train stamina")
         {
            iResult = Send(what,@AttemptTrainStatPoint,#type=STAT_ID_STAMINA);
         }
         if StringContain(string,"train agility")
         {
            iResult = Send(what,@AttemptTrainStatPoint,#type=STAT_ID_AGILITY);
         }
         if StringContain(string,"train mysticism")
         {
            iResult = Send(what,@AttemptTrainStatPoint,#type=STAT_ID_MYSTICISM);
         }
         if StringContain(string,"train aim")
         {
            iResult = Send(what,@AttemptTrainStatPoint,#type=STAT_ID_AIM);
         }

         if StringContain(string,"train five might")
         {
            iResult = Send(what,@AttemptTrainStatPoints,#type=STAT_ID_MIGHT);
         }
         if StringContain(string,"train five intellect")
         {
            iResult = Send(what,@AttemptTrainStatPoints,#type=STAT_ID_INTELLECT);
         }
         if StringContain(string,"train five stamina")
         {
            iResult = Send(what,@AttemptTrainStatPoints,#type=STAT_ID_STAMINA);
         }
         if StringContain(string,"train five agility")
         {
            iResult = Send(what,@AttemptTrainStatPoints,#type=STAT_ID_AGILITY);
         }
         if StringContain(string,"train five mysticism")
         {
            iResult = Send(what,@AttemptTrainStatPoints,#type=STAT_ID_MYSTICISM);
         }
         if StringContain(string,"train five aim")
         {
            iResult = Send(what,@AttemptTrainStatPoints,#type=STAT_ID_AIM);
         }

         if StringContain(string,"train ten might")
         {
            iResult = Send(what,@AttemptTrainStatPoints,#type=STAT_ID_MIGHT,#amount=10);
         }
         if StringContain(string,"train ten intellect")
         {
            iResult = Send(what,@AttemptTrainStatPoints,#type=STAT_ID_INTELLECT,#amount=10);
         }
         if StringContain(string,"train ten stamina")
         {
            iResult = Send(what,@AttemptTrainStatPoints,#type=STAT_ID_STAMINA,#amount=10);
         }
         if StringContain(string,"train ten agility")
         {
            iResult = Send(what,@AttemptTrainStatPoints,#type=STAT_ID_AGILITY,#amount=10);
         }
         if StringContain(string,"train ten mysticism")
         {
            iResult = Send(what,@AttemptTrainStatPoints,#type=STAT_ID_MYSTICISM,#amount=10);
         }
         if StringContain(string,"train ten aim")
         {
            iResult = Send(what,@AttemptTrainStatPoints,#type=STAT_ID_AIM,#amount=10);
         }
         
         if iResult
         {
            Send(self,@SayToOne,#target=what,#message_rsc=mar_elder_teach_msg);
         }
         else
         {
            Send(self,@SayToOne,#target=what,#message_rsc=mar_elder_cannot_train_msg);
         }
         return;
      }

      propagate;
   }

   AffectMood(what=$,why=$)
   {
      if why = MOODMOD_ACCEPT_ITEM
      {
         if IsClass(what,&Money)
         {
            Send(self,@SetMood,#new_mood=piMood+1);
         }

         if IsClass(what,&Food)
         {
            Send(self,@SetMood,#new_mood=piMood+1);
         }

         return;
      }

      return;
   }

   SetForSale()
   {
      plFor_sale = [
         $,$,
         [
            SID_TRUCE,
            SID_RIGHTEOUS_INHERITANCE
         ],
      $];

      return;
   }

   InitCondSale()
   {
      local oObj;

      oObj = Create(&OrcTooth,#number=4);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
            #triggers=[Send(oObj,@GetName)],
            #action=[LIBACT_CONDITIONAL,oObj,350,ran_orctooth]);

      return;
   }

   UserEntered(who=$)
   {
      Post(who,@SomeoneSaid,#what=self,#string=MrEl_entry_welcome,
            #type=SAY_RESOURCE,#parm1=Send(who,@GetDef),
            #parm2=Send(who,@GetName));

      propagate;
   }

   CanAcceptOffer()
   {
      return TRUE;
   }

   CheckWhyWanted(obj=$,who=$)
   {
      if IsClass(obj,&StatsResetToken)
      {
         Send(who,@SendStatChange);

         % We don't destroy the item, so return FALSE.
         return FALSE;
      }

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
