% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Ent is Monster

constants:

   include blakston.khd

resources:

   include ent.lkod

   ent_koc_name_rsc = "tezmecya"
   ent_name_rsc = "living tree"
   ent_icon_rsc = res0003a.bgf
   ent_desc_rsc = \
      "A primordial spirit flows through the dark branches of "
      "this ancient being.  A forest scent of aged wood and "
      "fertile earth tingles your nostrils.  "
      "You wonder if the story that it can summon lightning bolts to protect "
      "the forest is really true."

   ent_leaves_full_rsc = res0003b.bgf

   ent_dead_icon_rsc = res0003d.bgf
   ent_dead_leaves_full_rsc = res0003e.bgf
   ent_dead_name_rsc = "dead remains of living tree"

   ent_sound_hit = tr_atkh.wav
   ent_sound_miss = tr_atkm.wav
   ent_sound_death = tr_dth.wav
   ent_sound_aware = tr_awr.wav

classvars:

   vrKocName = ent_koc_name_rsc
   vrName = ent_name_rsc
   vrIcon = ent_icon_rsc
   vrDesc = ent_desc_rsc
   vrDead_icon = ent_dead_icon_rsc
   vrDead_name = ent_dead_name_rsc

   viTreasure_type = TID_ENT
   viAttack_type = ATCK_WEAP_WHIP
   viSpeed = SPEED_NONE
   viAttributes = MOB_NOMOVE
   viDefault_behavior = AI_NOMOVE | AI_FIGHT_KARMA_AGGRESSIVE
   viLevel = 50
   viDifficulty = 4
   viVisionDistance = 14
   viKarma = 40
   viCashmin = 100
   viCashmax = 500

   % 1 in x chance to cast a spell.
   viSpellChance = 9

   vrSound_hit = ent_sound_hit
   vrSound_miss = ent_sound_miss
   vrSound_aware = ent_sound_aware
   vrSound_death = ent_sound_death

   vrLeaves = ent_leaves_full_rsc
   vrDead_leaves =ent_dead_leaves_full_rsc

   viLanguage = LANGUAGE_SYLVAN
   
   viBaseBleedPercentage = 50
   viBaseKnockbackPercentage = 0
   
   viBaseSize = 80

properties:

   piAnimation = ANIM_NONE

   piMax_mana = 12
   piMeleeSpellChance = 0
   piRangedSpellChance = 10

   piReputationGroup = REP_FAROL
   
messages:

   Constructor(iTrunkColor=$,iLeavesColor=$)
   {
      local iTrunk, iLeaves, lTrunk, lLeaves;

      % Pick random colors.
      iTrunk = iTrunkColor;
      if iTrunk = $
      {
         lTrunk = [ XLAT_TO_SKIN2, XLAT_TO_SKIN2, XLAT_TO_SKIN2,
                    XLAT_TO_SKIN4 ];
         iTrunk = Nth(lTrunk,Random(1,Length(lTrunk)));
      }

      iLeaves = iLeavesColor;
      if iLeaves = $
      {
         lLeaves = [ XLAT_TO_GREEN, XLAT_TO_GREEN,
                     XLAT_TO_GREEN, XLAT_TO_RED ];
         iLeaves = Nth(lLeaves,Random(1,Length(lLeaves)));
      }

      piColor_Translation = Send(SYS,@EncodeTwoColorXLAT,#color1=iTrunk,
                                 #color2=iLeaves);

      propagate;
   }

   Constructed()
   {
      plResistances = [ [-ATCK_SPELL_COLD, 50 ],
                        [ATCK_WEAP_BLUDGEON, 50 ],
                        [ATCK_WEAP_SLASH, -10 ],
                        [-ATCK_SPELL_FIRE, -10 ] ];
                        
      plMeleeSpellBook = [ [SID_SHOCKING_FURY, 5, 100, Random(50,70)] ];
      plRangedSpellBook = [ [SID_LIGHTNING_BOLT, 5, 100, Random(1,20)] ];

      plReputationEnemies = [REP_UNDEAD];

      plCreatureTypes = Cons(CREATURE_TYPE_PLANT,plCreatureTypes);
      plCreatureTypes = Cons(CREATURE_TYPE_INTELLIGENT,plCreatureTypes);
      plCreatureTypes = Cons(CREATURE_TYPE_MAGICAL,plCreatureTypes);

      propagate;
   }

   MonsterAttack(what=$)
   {
      piAnimation = ANIM_ATTACK;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;

      return;
   }

   SendMoveAnimation()
   {
      if piColor_Translation <> 0
      {
         AddPacket(1,ANIMATE_TRANSLATION, 1,piColor_Translation);
      }

      AddPacket(1,ANIMATE_CYCLE, 4,200, 2,1, 2,2);

      return;
   }

   SendMoveOverlays()
   {
      AddPacket(1,1);

      AddPacket(4,vrLeaves, 1,HS_HEAD);

      if piColor_Translation <> 0
      {
         AddPacket(1,ANIMATE_TRANSLATION, 1,piColor_Translation);
      }

      AddPacket(1,ANIMATE_CYCLE, 4,200, 2,1, 2,2);

      return;
   }

   SendAnimation()
   {
      if piColor_Translation <> 0
      {
         AddPacket(1,ANIMATE_TRANSLATION, 1,piColor_Translation);
      }

      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE, 4,200, 2,2, 2,4, 2,1);

         return;
      }

      % if no body animation
      AddPacket(1,ANIMATE_NONE,2,1);

      return;
   }

   SendOverlays()
   {
      % Send overlay bitmap info to user.
      AddPacket(1,1);

      AddPacket(4,vrLeaves, 1,HS_HEAD);

      if piColor_Translation <> 0
      {
         AddPacket(1,ANIMATE_TRANSLATION,1,piColor_Translation);
      }

      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE, 4,200, 2,2, 2,4, 2,1);

         return;
      }

      AddPacket(1,ANIMATE_NONE, 2,1);

      return;
   }

   AssessDamage(aSpell=0)
   {
      % Living trees get more dangerous if you burn 'em.

      if (aSpell & ATCK_SPELL_FIRE)
      {
         piMeleeSpellChance = Bound(piMeleeSpellChance+10,10,100);
         piRangedSpellChance = Bound(piRangedSpellChance+10,10,100);
      }

      propagate;
   }

   CreateDeadBody(killer=$)
   {
      return Create(&DeadBody,
                    #victim=self,
                    #killer=killer,
                    #BodyTranslation=piColor_Translation,
                    #LegsTranslation=piColor_Translation,
                    #PlayerBodyOverlay=vrDead_leaves);
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
