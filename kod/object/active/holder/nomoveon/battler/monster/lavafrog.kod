% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
LavaFrog is Monster

constants:

   include blakston.khd

resources:

   lavafrog_koc_name_rsc = "ziv'ziv biskalani"
   lavafrog_name_rsc = "lava frog"
   lavafrog_icon_rsc = lavafrog.bgf
   lavafrog_desc_rsc = \
      "A creature of flame and hunger stands before you. According to the nomads, "
      "this is a greater flame entity attracted here by the Shrine of the Harnessed Flame. "
      "Ancient sorcerers fed it runed armor so that its stomach would empower the runes "
      "with magic spells. Unfortunately, nobody has been around to feed the lava frog "
      "in centuries, so it is in no mood to cooperate."

   lavafrog_dead_icon_rsc = airelementaldead.bgf
   lavafrog_dead_name_rsc = "pile of warm rock"

   lavafrog_sound_hit = gst_atkh.wav
   lavafrog_sound_miss = gst_atkm.wav
   lavafrog_sound_death = gst_dth.wav
   lavafrog_sound_aware = gst_awr.wav

classvars:

   vrKocName = lavafrog_koc_name_rsc
   vrName = lavafrog_name_rsc
   vrIcon = lavafrog_icon_rsc
   
   vrDead_icon = lavafrog_dead_icon_rsc
   vrDead_name = lavafrog_dead_name_rsc

   viTreasure_type = TID_LAVA_FROG
   viSpeed = 40
   viLevel = 200
   viDifficulty = 9
   viVisionDistance = 60
   % Attack range of 192, or 3 row/col.
   viAttackRange = 192
   viAttack_spell = ATCK_SPELL_FIRE
   viKarma = 0
   vbIsUndead = FALSE
   viCashmin = 1000
   viCashmax = 5500
   vrSound_hit = lavafrog_sound_hit
   vrSound_miss = lavafrog_sound_miss
   vrSound_aware = lavafrog_sound_aware
   vrSound_death = lavafrog_sound_death

   viDead_drawfx = DRAWFX_TRANSLUCENT_75
   viDefault_behavior = \
      AI_FIGHT_AGGRESSIVE | AI_FIGHT_WIZARD_KILLER
   
   viCanSpawnAsChampion = FALSE
   
   viBonusBaseTP = 1000

   viBaseSize = 2000

   % between 1 and 10000
   viCriticalHitRate = 200
   vbCanCauseKnockback = TRUE

   % Huge monster
   viEnemyRangeBonus = 64 * 3

   vbBonusXPGain = 2000
   
properties:

   piAnimation = ANIM_NONE
   piGroup = 1
   piMinDamage = 10
   piMaxDamage = 22
   vrDesc = lavafrog_desc_rsc

   piMax_mana = 20
   piMeleeSpellChance = 40
   piRangedSpellChance = 5
   
messages:

   Constructed()
   {
      plResistances = [ [-ATCK_SPELL_COLD, -20],
                        [-ATCK_SPELL_FIRE, 300]
                      ];

      plMeleeSpellBook = [ [SID_BLAST_OF_FIRE, 5, 100, Random(85,99)]];
      plRangedSpellBook = [ [SID_FIREBALL, 5, 100, Random(90,99)] ];

      plCreatureTypes = Cons(CREATURE_TYPE_MAGICAL,plCreatureTypes);
      plCreatureTypes = Cons(CREATURE_TYPE_ANIMAL,plCreatureTypes);
      plCreatureTypes = Cons(CREATURE_TYPE_ELEMENTAL,plCreatureTypes);

      propagate;
   }

   GetBaseEmpowerments()
   {
      return [[-ATCK_SPELL_FIRE,300]];
   }

   MonsterAttack(what = $)
   {
      % add in any attack animation calls
      Send(self,@DoSlash);

      return;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_NONE,2,1);

      return;
   }

   SendMoveAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE, 4,125, 2,1, 2,7);

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE,4,200,2,8,2,10,2,1);

         return;
      }

      AddPacket(1,ANIMATE_NONE,2,1);

      return;
   }

   DoSlash()
   {
      piAnimation = ANIM_ATTACK;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;

      return;
   }

   GetAttackTime()
   {
      return Bound(Send(self,@Fuzzy,#num=MOB_ATTACK_TIMER_WAIT)
             + 3500-70*(3*viDifficulty+(piAttackSpeed/4)),250,$);
   }

   SomethingMoved(what=$)
   {
      local oTrance, oActive, lActive, i;

      if what <> self OR poOwner = $
      {
         propagate;
      }

      % Shake the ground when I take a step.
      % Half chance each person might lose any active trance.
      Send(poOwner,@Rumble,#duration=250,#disruption=50);
      
      propagate;
   }

   CanMorphTo()
   {
      return FALSE;
   }

   GetMaxHitPoints()
   {
      return 20000;
   }

   GetBossStatus()
   {
      return TRUE;
   }

   CreateTreasure(who=$,corpse=$,ladderID=0)
   {
      local oTreasure, iNumber;

      oTreasure = Create(&FireFragment,#corpse=corpse,#ladderID=ladderID);
      iNumber = Random(15,80);
      Send(oTreasure,@AddNumber,#number=iNumber);

      if Send(poOwner,@ReqNewHold,#what=oTreasure,#new_row=piRow,#new_col=piCol)
      {
         Send(poOwner,@NewHold,#what=oTreasure,
               #new_row=piRow,#new_col=piCol,
               #fine_row=piFine_row,#fine_col=piFine_col);
      }
      else
      {
         Send(oTreasure,@Delete);
      }

      propagate;
   }

   SendLightingInformation()
   {
      if piChampionStatus <> 0
      {
         propagate;
      }

      AddPacket(2,(LIGHT_FLAG_ON | LIGHT_FLAG_DYNAMIC));
      % 255 out of 255 intensity of light
      AddPacket(1,255);
      % Red
      AddPacket(2,LIGHT_BRED);

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
