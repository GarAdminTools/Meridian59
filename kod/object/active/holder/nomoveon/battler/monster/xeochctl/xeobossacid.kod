% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
XeoBossAcid is Xeochicatl

constants:

   include blakston.khd

   ANIM_CAST = 2     %% Cast spell animation.

resources:

   XeoBossAcid_name_rsc = "vix'ci'xeochicatl"
   XeoBossAcid_koc_name_rsc = "vix'ci'xeo'chicatl"
   XeoBossAcid_icon_rsc = Xeo.bgf
   XeoBossAcid_desc_rsc = \
      "Before you is a vix'ci'xeochicatl, a being from near the heart of the "
      "elemental plane of acid. This is no minor xeochicatl; the vix are "
      "far too strong to grace the mortal plane without visiting tremendous "
      "destruction. This beast is an elemental incarnation of acid itself."

   XeoBossAcid_sound_aware = xeo4awr.wav

   acid_enter_phase_two_msg = \
      "~BThe vix'ci'xeochicatl summons an acidic aura to enhance its strength!"
   acid_enter_phase_three_msg = \
      "~BThe vix'ci'xeochicatl draws on the core power of the acidic plane!"

classvars:

   vrName = XeoBossAcid_name_rsc
   vrDesc = XeoBossAcid_desc_rsc
   vrKocName = XeoBossAcid_koc_name_rsc

   viAttack_type = ATCK_WEAP_MAGIC
   vrSound_aware = XeoBossAcid_sound_aware

   viTreasure_type = TID_XEO_BOSS_ACID
   
   viBonusBaseTP = 2000

   vbBonusXPGain = 5000

   viLeadershipCost = 1500

properties:

   piAnimation = ANIM_NONE
   piMax_Mana = 300

   vrIcon = Xeochicatl_icon_rsc
   vrDead_icon = Xeochicatl_dead_icon_rsc
   
   piPhase = 1

   piDrawfx = DRAWFX_TRANSLUCENT_25
   piMinDamage = 10
   piMaxDamage = 30

   viLevel = 180
   viDifficulty = 8
   viSpeed = SPEED_VERY_FAST
   
   piBaseSize = 600

messages:

   Constructed()
   {
      plResistances = [ [ATCK_SPELL_SHOCK, -20 ],
                        [ATCK_WEAP_MAGIC, -10 ],
                        [-ATCK_SPELL_ACID, 110 ],
                        [-ATCK_SPELL_QUAKE, 100 ]
                      ];

      piColor_Translation = Send(SYS,@EncodeTwoColorXLAT,#color1=XLAT_TO_GREEN);

      plMeleeSpellBook = [ [ SID_SPLASH_OF_ACID, 3, 100, Random(70,99) ] ];
      plRangedSpellBook = [ [ SID_SLITHERBOLT, 10, 100, Random(70,99) ] ];

      Post(self,@AddChampionAbility,#ability=3,#value=90);

      propagate;
   }

   GoPhaseTwo()
   {
      local i;

      piMinDamage = 20;
      piMaxDamage = 40;
      piDrawfx = DRAWFX_TRANSLUCENT_50;
      piLevel = 190;
      viDifficulty = 9;
      Post(self,@AddChampionAbility,#ability=4,#value=80);

      plResistances = [ [ATCK_SPELL_SHOCK, 20 ],
                        [ATCK_WEAP_MAGIC, 30 ],
                        [-ATCK_SPELL_ACID, 150 ],
                        [-ATCK_SPELL_QUAKE, 100 ]
                      ];

      vrIcon = bigger_Xeochicatl_icon_rsc;
      vrDead_icon = bigger_Xeochicatl_dead_icon_rsc;
      piPhase = 2;
      piBaseSize = 1200;
      Post(poOwner,@SomethingChanged,#what=self);

      foreach i in Send(poOwner,@GetHolderActive)
      {
         if IsClass(First(i),&Player)
         {
            Send(First(i),@MsgSendUser,#message_rsc=acid_enter_phase_two_msg);
         }
      }
      Send(poOwner,@Rumble,#duration=4000);

      return;
   }

   GoPhaseThree()
   {
      local i;

      piMinDamage = 30;
      piMaxDamage = 50;
      piDrawfx = DRAWFX_TRANSLUCENT_75;
      piLevel = 200;
      viDifficulty = 10;
      viSpeed = SPEED_VERY_FAST;
      Post(self,@AddChampionAbility,#ability=6,#value=15);

      plResistances = [ [ATCK_SPELL_SHOCK, 60 ],
                        [ATCK_WEAP_MAGIC, 70 ],
                        [-ATCK_SPELL_ACID, 190 ],
                        [-ATCK_SPELL_QUAKE, 100 ]
                      ];

      vrIcon = biggest_Xeochicatl_icon_rsc;
      vrDead_icon = biggest_Xeochicatl_dead_icon_rsc;
      piPhase = 3;
      piBaseSize = 1800;
      Post(poOwner,@SomethingChanged,#what=self);

      foreach i in Send(poOwner,@GetHolderActive)
      {
         if IsClass(First(i),&Player)
         {
            Send(First(i),@MsgSendUser,#message_rsc=acid_enter_phase_three_msg);
         }
      }
      Send(poOwner,@Rumble,#duration=4000);

      return;
   }

   GetMaxHitPoints()
   {
      return 20000;
   }

   GetBossStatus()
   {
      return TRUE;
   }

   HitPointThresholdDamage()
   {
      local iThreshold;

      iThreshold = piHit_points / (piMax_hit_points * 20);

      if iThreshold >= 4
      {
         propagate;
      }
      else if iThreshold = 3
      {
         propagate;
      }
      else if iThreshold = 2
      {
         Send(self,@GoPhaseTwo);
         propagate;
      }
      else if iThreshold = 1
      {
         propagate;
      }
      else
      {
         Send(self,@GoPhaseThree);
         propagate;
      }

      propagate;
   }

   IsAlly(target = $, regroup = FALSE)
   {
      if IsClass(target,&XeoBossAcid)
         OR IsClass(target,&XeoAcid)
         OR IsClass(target,&EarthElemental)
         OR IsClass(target,&EarthElementalChampion)
      {
         return TRUE;
      }

      propagate;
   }

   SendLightingInformation()
   {
      if piPhase = 1
      {
         AddPacket(2,(LIGHT_FLAG_ON | LIGHT_FLAG_DYNAMIC));
         % 255 out of 255 intensity of light
         AddPacket(1,60);
         AddPacket(2,LIGHT_BGREEN);
      }
      if piPhase = 2
      {
         AddPacket(2,(LIGHT_FLAG_ON | LIGHT_FLAG_DYNAMIC));
         % 255 out of 255 intensity of light
         AddPacket(1,145);
         AddPacket(2,LIGHT_BGREEN);
      }
      if piPhase = 3
      {
         AddPacket(2,(LIGHT_FLAG_ON | LIGHT_FLAG_DYNAMIC));
         % 255 out of 255 intensity of light
         AddPacket(1,255);
         AddPacket(2,LIGHT_BGREEN);
      }

      return;
   }

   Killed()
   {
      local i;
      
      foreach i in plHurtMeRecently
      {
         if IsClass(First(i),&Player)
         {
            Send(First(i),@SetPrismDefeated,#num=NTH_ACID_PRISM,#value=1);
         }
      }
      propagate;
   }

   GetBaseSize()
   {
      return piBaseSize;
   }
   
   GetEnemyRangeBonus()
   {
      if piPhase = 2
      {
         return 64 * 2;
      }
      if piPhase = 3
      {
         return 64 * 4;
      }
      return viEnemyRangeBonus;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
