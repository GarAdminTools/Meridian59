% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
MazeBossRoom is MinotaurMazeRoom

% This room contains the access to the true boss area.

constants:

   include blakston.khd
   
   BOSS_ELEVATOR_SECTOR = 735
   BOSS_GOTO_SECTOR = 736

resources:

   room_maze_boss_room = mazebossroom.roo

classvars:

   viTeleport_row = 22
   viTeleport_col = 5

   %%%%%%%%%%%%%%%%%%%% LEVERS %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

   LEVER_ONE_ROW = 11
   LEVER_ONE_TYPE = ANIMATE_FLOOR_LIFT
   LEVER_ONE_ACTIVATES_SECTOR = 735
   LEVER_ONE_DOWN_HEIGHT = 1000
   LEVER_ONE_DOWN_SPEED = 100
   LEVER_ONE_UP_HEIGHT = 3900
   LEVER_ONE_UP_SPEED = 100
   
   LEVER_TWO_ROW = 14
   LEVER_TWO_TYPE = ANIMATE_FLOOR_LIFT
   LEVER_TWO_ACTIVATES_SECTOR = 735
   LEVER_TWO_DOWN_HEIGHT = 1000
   LEVER_TWO_DOWN_SPEED = 100
   LEVER_TWO_UP_HEIGHT = 3900
   LEVER_TWO_UP_SPEED = 100

properties:

   prRoom = room_maze_boss_room
   piRoom_num = RID_MAZE_BOSS_ROOM

messages:

   CreateMazeRoomObjects()
   {
      Send(self,@NewHold,#what=Create(&Lever),
            #new_row=11,#new_col=13,#fine_row=39,#fine_col=38);
      Send(self,@NewHold,#what=Create(&DynamicLight,#iColor=LIGHT_DPURPLE,#iIntensity=255),
            #new_row=10,#new_col=11,#fine_row=39,#fine_col=22);

      Send(self,@NewHold,#what=Create(&Lever),
            #new_row=14,#new_col=8,#fine_row=63,#fine_col=54);
      return;
   }
   
   CheckMoveBorders(what=$, new_row=$, new_col=$)
   {
      return;
   }

   LeverPulled(lever_object=$)
   {
      if lever_object <> $
      {
         if Send(lever_object,@GetRow) = LEVER_ONE_ROW
         {
            Send(self,@SetSector,#sector=LEVER_ONE_ACTIVATES_SECTOR,
                                 #animation=LEVER_ONE_TYPE,
                                 #height=LEVER_ONE_UP_HEIGHT,
                                 #speed=LEVER_ONE_UP_SPEED);
         }

         if Send(lever_object,@GetRow) = LEVER_TWO_ROW
         {
            Send(self,@SetSector,#sector=LEVER_TWO_ACTIVATES_SECTOR,
                                 #animation=LEVER_TWO_TYPE,
                                 #height=LEVER_TWO_DOWN_HEIGHT,
                                 #speed=LEVER_TWO_DOWN_SPEED);
         }
      }
      Send(self,@PlayLiftSound);
      return;
   }
   
   AttemptExit(who=$,iSection=0)
   {
      if who <> $
         AND IsClass(who,&Player)
      {         
         switch(iSection)
         {
            case BOSS_GOTO_SECTOR:
               Send(Send(SYS,@FindRoomByNum,#num=RID_FROZEN_CISTERN),@Teleport,#what=who);
               return TRUE;
               break;
         }
      }
   
      return FALSE;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
