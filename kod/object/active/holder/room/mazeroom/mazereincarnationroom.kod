% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
MazeReincarnationRoom is MinotaurMazeRoom

% This room contains prestige races that a player can reincarnate into if they have the proper quest flags.

constants:

   include blakston.khd


resources:

   room_reincarnation_room = mazereincarnationroom.roo
   room_maze_reincarnation_room_name = "Antechamber of Eternity"
   
   maze_reincarnation_room_challenge_msg = \
      "~kThe spirit of Vecka tells you, \"Before the High Sorcerers went mad with power, "
      "the common sorcerers of the land knew something of reincarnation. This was where "
      "they came in the last days of health to meditate upon their next life.\""

   genealogy_draconian_name = "One with the Primal"

   genealogy_choose_draconian_trigger = "I wish to reincarnate as draconian."
   genealogy_choose_drow_trigger = "I wish to reincarnate as drow."
   
   maze_not_ready_reincarnate = \
      "You cannot reincarnate until you have achieved enough potential "
      "in this life."

   genealogy_draconian = \
      "Written by Qua Kornedrin, High Sorcerer of Fire"
      "\n\n"
      "ON DRACONIANS"
      "\n\n"
      "While the process of reincarnation is little understood, what "
      "little we have observed shows that it is a crude process that destroys "
      "all of the skills, knowledge, and toughness of spirit and body that "
      "a person has acquired in his or her life. I myself am still researching "
      "ways to reincarnate without loss of so much of the self, but that is "
      "a writing for another time."
      "\n\n"
      "The first race we sorcerers were able to fuse our spirits with "
      "was the dragon, mightiest of the beasts of Biskalane. This gave rise "
      "to the Draconians, a primal half-human with animal intellect and the toughness "
      "of a titan."
      "\n\n"
      "The life of a Draconian is one I myself would not choose, "
      "but they have proven fantastic bodyguards and mercenaries. "
      "When apprentice sorcerers try a life or two as a half-dragon, "
      "they find that their claws greatly improve the effectiveness of "
      "their touch spell attacks."
      "\n\n"
      "So there's that, at least. Toughness and claws."
      "\n\n--¶"
      "DRACONIAN TRAITS"
      "\n\n"
      "Racial Resistances & Empowerments:"
      "\n"
      "10% slash resistance\n"
      "5% pierce resistance\n"
      "5% thrust resistance\n"
      "20% acid resistance\n"
      "10% bludgeon vulnerability\n"
      "15% increased punch damage\n"
      "\nRacial Stat Maximums:"
      "\n50 Might"
      "\n30 Intellect"
      "\n70 Stamina"
      "\n30 Agility"
      "\n50 Mysticism"
      "\n50 Aim"
      "\n\n"
      "Draconians have extremely large appetites, and can eat an enormous amount "
      "of food in one sitting. Their resting max vigor is also extremely high."
      "\n\n--¶"
      "REINCARNATION"
      "\n\n"
      "For each enlightenment you have achieved, you will have one additional "
      "stat point to distribute. When you reincarnate, you have a chance to "
      "choose your stats anew. Should you fail to choose at the time of "
      "reincarnation, you will retain one free stat reset that you can use by "
      "going to an Elder and saying 'change stats'."
      "\n\n"
      "Your guardian angel will never return once you reincarnate."
      "\n\n"
      "Draconians are part dragon. This defines everything about how "
      "they live their lives. If a Draconian can get close with a claw "
      "or touch attack, their opponent will suffer dearly for it."
      "\n\n"
      "Draconians have limited animal intellect which severely curtails "
      "any chance of doing battle with spells. Most Draconians enter "
      "the business of weapons, for ten intellect is all that is needed "
      "for getting down and dirty. However, those Draconians that do manage "
      "to learn some spells use them to augment their natural abilities."
      "\n\n--¶"
      "REINCARNATING AS DRACONIAN"
      "\n\n"
      "To reincarnate as draconian, state thus:"
      "\n\n"
      "I wish to reincarnate as draconian."
      "\n\nThis process will destroy all of your worn and held items, "
      "and you will forget everything you have learned in this life. "
      "You will be as a newborn, save a little wiser for your experiences.\n"

classvars:

   vrName = room_maze_reincarnation_room_name

   viTeleport_row = 5
   viTeleport_col = 5

properties:

   prRoom = room_reincarnation_room
   piRoom_num = RID_MAZE_REINCARNATION_ROOM

messages:
   CreateStandardObjects()
   {
      local oBook;

      oBook = Create(&BookPedestal,#Name=genealogy_draconian_name,
                     #Text=genealogy_draconian);
      Send(self,@NewHold,#what=oBook,
           #new_row=21,#new_col=17,#fine_col=0,#fine_row=0,
           #new_angle=ANGLE_NORTH_EAST);

      propagate;
   }

   CreateMazeRoomObjects()
   {
      local lLights;

      lLights = [Create(&DynamicLight,#iColor=LIGHT_BPURPLE,#iIntensity=70),
                  Create(&DynamicLight,#iColor=LIGHT_BPURPLE,#iIntensity=70),
                  Create(&DynamicLight,#iColor=LIGHT_BPURPLE,#iIntensity=70),
                  Create(&DynamicLight,#iColor=LIGHT_BPURPLE,#iIntensity=70),
                  Create(&DynamicLight,#iColor=LIGHT_BPURPLE,#iIntensity=70),
                  Create(&DynamicLight,#iColor=LIGHT_BPURPLE,#iIntensity=70),
                  Create(&DynamicLight,#iColor=LIGHT_BPURPLE,#iIntensity=70),
                  Create(&DynamicLight,#iColor=LIGHT_BPURPLE,#iIntensity=70)];
      
      Send(self,@NewHold,#what=Nth(lLights,1),#new_row=9,#new_col=9,#fine_row=0,#fine_col=0);
      Send(self,@NewHold,#what=Nth(lLights,2),#new_row=29,#new_col=9,#fine_row=0,#fine_col=0);
      Send(self,@NewHold,#what=Nth(lLights,3),#new_row=29,#new_col=29,#fine_row=0,#fine_col=0);
      Send(self,@NewHold,#what=Nth(lLights,4),#new_row=9,#new_col=29,#fine_row=0,#fine_col=0);
      Send(self,@NewHold,#what=Nth(lLights,5),#new_row=19,#new_col=19,#fine_row=0,#fine_col=0);

      return;
   }

   SomeoneSaid(what=$,string=$)
   {
      local bTryReincarnate, iReincarnateAsRace;

      bTryReincarnate = FALSE;
      iReincarnateAsRace = 0;
      if StringEqual(string,genealogy_choose_draconian_trigger)
      {
         bTryReincarnate = TRUE;
         iReincarnateAsRace = RACE_DRACONIAN;
      }

      if StringEqual(string,genealogy_choose_drow_trigger)
      {
         bTryReincarnate = TRUE;
         iReincarnateAsRace = RACE_DROW;
      }

      if bTryReincarnate
         AND Send(what,@GetRow) >= 13
         AND Send(what,@GetRow) <= 25
         AND Send(what,@GetCol) >= 13
         AND Send(what,@GetCol) <= 25
         AND Send(SETTINGS_OBJECT,@GetReincarnationEnabled)
      {
         if Send(what,@CanReincarnate)
         {
            Post(what,@Reincarnate,#race=iReincarnateAsRace);
         }
         else
         {
            Post(what,@MsgSendUser,#message_rsc=maze_not_ready_reincarnate);
            Post(what,@ReportReincarnationProgress);
         }
      }

      propagate;
   }
   
   CheckMoveBorders(what=$, new_row=$, new_col=$)
   {
      return;
   }

   NewHold(what=$)
   {
      if what <> $
         AND IsClass(what,&User)
         AND Send(what,@GetVeckaAdvice)
      {
         Send(what,@MsgSendUser,#message_rsc=maze_reincarnation_room_challenge_msg);
      }
 
      if IsClass(what,&Player)
      {
         Post(Send(SYS,@GetGameQuestMaintenance),@EvaluatePlayer,#who=what,#vbTriggered=TRUE);
      }

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
