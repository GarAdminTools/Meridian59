% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
DesertAfterlifeDunes4East is DesertAfterlife

constants:

   include blakston.khd

resources:

   room_desertALdunes4e = desertaldunes2.roo
   room_name_desertALdunes4e = "Eternal Wastes"
   
   prism_of_death_closed = \
      "The void that would normally be found here has been driven back by intrepid adventurers. "
      "You wander your way back to Xaerdun's central location."

   prism_of_death_defeated = \
      "~BThe Prism of Death in the Eternal Wastes has been defeated by a band of brave spirits. "
      "For a time, dying in the land of Biskalane will be much less harsh."
      
   prism_of_death_reset = \
      "~BThe Prism of Death in the Eternal Wastes has recovered from invasion by adventurers. "
      "Dying in the land of Biskalane will once again be harsh."

classvars:

   vrName = room_name_desertALdunes4e

   viTeleport_row = 37
   viTeleport_col = 140

properties:

   piBaseLight = LIGHT_MAX
   piOutside_factor = OUTDOORS_5 

   piDirectional_percent = DIRECTIONAL_PERCENT_OUTDOORS

   prRoom = room_desertALdunes4e
   piRoom_num = RID_DESERTALDUNES4E

   piGen_time = 30000
   piGen_percent = 100

   piInit_count_min = 25
   piInit_count_max = 25

   piMonster_count_max = 25
   
   pbPrismOpen = TRUE
   pbPrismOfDeathDefeated = FALSE

   ptPrismOfDeathReset = $
   piHoursLeftReset = 71

messages:

   Constructed()
   {
      plMonsters = [ [&LostSoul, 100] ];
      % 120 by 204
      plGenerators = [ [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)],
                       [ Random(3,117), Random(3,201)]
                     ];

      propagate;
   }

   SomethingMoved(what = $,new_row = $, new_col = $)
   {
      if new_col > (Send(self,@GetRoomCols)-2)
      {
         if pbPrismOpen
         {
            Send(SYS,@UtilGoNearSquare,#what=what,
                 #where=Send(SYS,@FindRoomByNum,#num=RID_PRISM_OF_DEATH),
                 #new_row=93,
                 #new_col=145,
                 #fine_row=0,
                 #fine_col=0,
                 #new_angle=Send(what,@GetAngle));
         }
         else
         {
            Send(what,@MsgSendUser,#message_rsc=prism_of_death_closed);
            Send(SYS,@UtilGoNearSquare,#what=what,
                 #where=Send(SYS,@FindRoomByNum,#num=RID_DESERTALDUNES),
                 #new_row=Send(what,@GetRow),
                 #new_col=3,
                 #fine_row=Send(what,@GetFineRow),
                 #fine_col=0,
                 #new_angle=Send(what,@GetAngle));
         }
         return;
      }

      if new_col < 2
      {
         if IsClass(what,&User)
         {
            Send(what,@MsgSendUser,#message_rsc=brighter_going_west_msg);
         }
         Send(SYS,@UtilGoNearSquare,#what=what,
              #where=Send(SYS,@FindRoomByNum,#num=RID_DESERTALDUNES3E),
              #new_row=Send(what,@GetRow),
              #new_col=Send(self,@GetRoomCols)-3,
              #fine_row=0,
              #fine_col=Send(what,@GetFineCol),
              #new_angle=Send(what,@GetAngle));
         return;
      }

      if new_row < 2
      {
         Send(SYS,@UtilGoNearSquare,#what=what,
              #where=Send(SYS,@FindRoomByNum,#num=RID_DESERTALDUNES4E),
              #new_row=Send(self,@GetRoomRows)-3,
              #new_col=Send(what,@GetCol),
              #fine_row=0,
              #fine_col=Send(what,@GetFineCol),
              #new_angle=Send(what,@GetAngle));
         return;
      }

      if new_row > (Send(self,@GetRoomRows)-2)
      {
         Send(SYS,@UtilGoNearSquare,#what=what,
              #where=Send(SYS,@FindRoomByNum,#num=RID_DESERTALDUNES4E),
              #new_row=3,
              #new_col=Send(what,@GetCol),
              #fine_row=0,
              #fine_col=Send(what,@GetFineCol),
              #new_angle=Send(what,@GetAngle));
         return;
      }

      propagate;
   }

   Delete()
   {
      if ptPrismOfDeathReset <> $
      {
         DeleteTimer(ptPrismOfDeathReset);
         ptPrismOfDeathReset = $;
      }
      propagate;
   }

   PrismOfDeathDefeated()
   {
      local i;

      pbPrismOfDeathDefeated = TRUE;
      pbPrismOpen = FALSE;
      
      piHoursLeftReset = Random(30,50);
      ptPrismOfDeathReset = CreateTimer(self,@PrismOfDeathReset,60000 * 60);
      
      foreach i in Send(SYS,@GetUsersLoggedOn)
      {
         Send(i,@MsgSendUser,#message_rsc=prism_of_death_defeated);
      }
      return;
   }

   PrismOfDeathReset()
   {
      local i;

      ptPrismOfDeathReset = $;
      
      if piHoursLeftReset > 0
      {
         --piHoursLeftReset;
         ptPrismOfDeathReset = CreateTimer(self,@PrismOfDeathReset,60000 * 60); % 1 hour, counts down 72 times
         return;
      }

      foreach i in Send(SYS,@GetUsersLoggedOn)
      {
         Send(i,@MsgSendUser,#message_rsc=prism_of_death_reset);
      }

      pbPrismOpen = TRUE;
      pbPrismOfDeathDefeated = FALSE;
      Send(Send(SYS,@FindRoomByNum,#num=RID_MAZE_EXIT_ROOM),@CloseUnholyWall);
      
      return;
   }

   GetPrismOfDeathDefeated()
   {
      return pbPrismOfDeathDefeated;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
