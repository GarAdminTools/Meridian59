% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
ZigguratAltar is DesertRoom

constants:


   include blakston.khd

resources:

   ziggurat_altar_room = ziggurataltar.roo
   ziggurat_altar_room_name = "Apex of the Ziggurat"
   ziggurat_altar_music = aamusic3.mp3

%   high_sorcerers_challenge_msg = \
%      "~kThe spirit of Vecka tells you, \"This is the nest of the beast. The ice dragon has taken many of our tribe over the years. "
%      "Kill the monstrous thing and take its eggs for your own!\""
 
   high_sorcerers_challenge_msg = \
      "~kQua'Kornedrin, High Sorcerer of Fire, shouts, \"We are quite aware of you, barbarian. Set foot on our level, and we will obliterate you. Or, you can still flee.\""
      
classvars:

   vrName = ziggurat_altar_room_name

   viTeleport_row = 53
   viTeleport_col = 28

   viTerrain_type = TERRAIN_MYSTICAL
   
   vbSpawnMonsters = TRUE

properties:

   prRoom = ziggurat_altar_room
   prMusic = ziggurat_altar_music
   piRoom_num = RID_ZIGGURAT_ALTAR

   piBaseLight = LIGHT_NICE

messages:

   SomethingTryGo(what = $,row = $,col = $,fine_row=$,fine_col=$)
   {
      if (row >= 135)
         AND (row <= 137)
         AND (col >= 26)
         AND (col <= 31)
      {
         Send(SYS,@UtilGoNearSquare,#what=what,
              #where=Send(SYS,@FindRoomByNum,#num=RID_PALACE_DUNGEON2),
              #new_row=40,#new_col=44,#fine_row=60,#fine_col=61,#new_angle=ANGLE_WEST);

         return TRUE;
      }

      propagate;
   }

   GetRegion()
   {
      return RID_DESERT;
   }

   GetPhaseMusic()
   {
      return ziggurat_altar_music;
   }

   RecalcBackgroundSkyGraphic(iSkyBox=0)
   {
      prBackground = background_chaos_night;

      return;
   }
   
   GetPhaseThreats()
   {
      return [&ChaosThreat];
   }

   PopulateArea()
   {
      local i, lMonsters, iCol, each_obj;
      
      foreach i in Send(self,@GetHolderActive)
      {
         each_obj = Send(self,@HolderExtractObject,#data=i);
         if IsClass(each_obj,&HighSorcerer)
         {
            % High sorcerers already present.
            return;
         }
      }

      lMonsters = [&HighSorcererFire,
                   &HighSorcererCold,
                   &HighSorcererLightning,
                   &HighSorcererAcid,
                   &HighSorcererDeath,
                   &HighSorcererLife];
      
      iCol = 23;

      foreach i in lMonsters
      {
         Send(self,@NewHold,#what=Create(i),
               #new_row=15,
               #new_col=iCol,
               #new_angle=ANGLE_SOUTH);
         iCol++;
         iCol++;
      }

      return;
   }
   
   DepopulateArea()
   {
      % Don't delete high sorcerers.      
      return;
   }

   NewHold(what=$)
   {
      if what <> $
         AND IsClass(what,&User)
%         AND Send(what,@GetVeckaAdvice)
      {
         Send(what,@MsgSendUser,#message_rsc=high_sorcerers_challenge_msg);
      }
 
      if IsClass(what,&Player)
      {
         Post(Send(SYS,@GetGameQuestMaintenance),@EvaluatePlayer,#who=what,#vbTriggered=TRUE);
      }

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
