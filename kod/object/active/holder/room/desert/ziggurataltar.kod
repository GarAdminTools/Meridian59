% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
ZigguratAltar is DesertRoom

constants:


   include blakston.khd

resources:

   ziggurat_altar_room = ziggurataltar.roo
   ziggurat_altar_room_name = "Apex of the Ziggurat"
   ziggurat_altar_music = aamusic3.mp3

classvars:

   vrName = ziggurat_altar_room_name

   viTeleport_row = 24
   viTeleport_col = 24

   viTerrain_type = TERRAIN_MYSTICAL

properties:

   prRoom = ziggurat_altar_room
   prMusic = ziggurat_altar_music
   piRoom_num = RID_ZIGGURAT_ALTAR

   piBaseLight = LIGHT_NICE

messages:

   GetRegion()
   {
      return RID_DESERT;
   }

   RecalcBackgroundSkyGraphic(iSkyBox=0)
   {
      prBackground = background_chaos_night;

      return;
   }
   
   GetPhaseThreats()
   {
      switch(Send(SYS,@GetDayPhase))
      {
         case DAY_PHASE_DAWN:
            return [&ShockStorm, &ChaosThreat];
         case DAY_PHASE_DAY:
            if Send(Send(SYS,@FindRoomByNum,#num=RID_DESERTRIVER2),
                     @GetPrismOfFireDefeated)
            {
               return [&HeatStorm, &ChaosThreat];
            }
            return [&HeatThreat, &ChaosThreat];
         case DAY_PHASE_DUSK:
            if Send(Send(SYS,@FindRoomByNum,#num=RID_ACIDPATH),
                     @GetPrismOfAcidDefeated)
            {
               return [&AcidStorm, &RainThreat, &ChaosThreat];
            }
            return [&AcidThreat,&RainThreat, &ChaosThreat];
         case DAY_PHASE_NIGHT:
            if Send(Send(SYS,@FindRoomByNum,#num=RID_DESERTSANDBAR),@GetPrismOfIceDefeated)
            {
               return [&ColdStorm, &SnowThreat, &ChaosThreat];
            }
            return [&ColdThreat,&SnowThreat, &ChaosThreat];
      }
      return [&ShockStorm, &ChaosThreat];
   }

   PopulateArea()
   {
      local i, iNumMonsters, oMonster, lMonsters, lClasses, iMinRow, iMinCol, iMaxRow, iMaxCol, iChosenAngle;

      lMonsters = [&HighSorcererFire,
                   &HighSorcererCold,
                   &HighSorcererLightning,
                   &HighSorcererAcid,
                   &HighSorcererDeath,
                   &HighSorcererLife];

      if viMaxSpawnRow = 0
      {
         iMaxRow = Send(self,@GetRoomRows)-1;
      }
      else
      {
         iMaxRow = viMaxSpawnRow;
      }

      if viMaxSpawnCol = 0
      {
         iMaxCol = Send(self,@GetRoomCols)-1;
      }
      else
      {
         iMaxCol = viMaxSpawnCol;
      }

      iChosenAngle = ANGLE_EAST;
      switch(Random(1,4))
      {
         case 1:
            iChosenAngle = ANGLE_WEST;
            break;
         case 2:
            iChosenAngle = ANGLE_NORTH;
            break;
         case 3:
            iChosenAngle = ANGLE_SOUTH;
            break;
         case 4:
            iChosenAngle = ANGLE_EAST;
            break;
      }

      foreach i in lMonsters
      {
         Send(self,@NewHold,#what=Create(i),
               #new_row=Random(viMinSpawnRow,iMaxRow),
               #new_col=Random(viMinSpawnCol,iMaxCol),
               #new_angle=iChosenAngle);
      }

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
