% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
DesertBridge2 is DesertRoom

% Despite being the Gate, this is actually the back door to the Palace,
% openable by players who have infiltrated through other means.
%
% In other words, one player can get inside and let in reinforcements.

constants:

   include blakston.khd

resources:

   room_desertbridge2 = desertbridge2.roo
   room_name_desertbridge2 = "Gate of the Palace of High Sorcery"

   sorcerer_gate_closed_msg = \
      "This ancient service gate was solidly built to prevent any unauthorized intrusion. "
      "You will not be able to open it from this side."

classvars:

   vrName = room_name_desertbridge2

   viTeleport_row = 10
   viTeleport_col = 6
   
   viGateRow = 4
   viGateCol = 191

   vbSpawnMonsters = FALSE

properties:

   piBaseLight = LIGHT_DARK
   piOutside_factor = OUTDOORS_5 

   piDirectional_percent = DIRECTIONAL_PERCENT_OUTDOORS

   prRoom = room_desertbridge2
   piRoom_num = RID_DESERTBRIDGE2

   pbGateOpen = FALSE

messages:
 
   CreateStandardExits()
   {
      plEdge_Exits = $;

      propagate;
   }

   CreateStandardObjects()
   {
      Send(self,@NewHold,#what=Create(&LightningPrism),
            #new_row=10,#new_col=5,#fine_row=32,#fine_col=32);
      propagate;
   }

   RecalcLightAndWeather()
   {
      local SysDayPhase;

      SysDayPhase = Send(SYS,@GetDayPhase);

      % We're crossing over into a new quarter.
      % Collapsed Causeway does not get threats other than shock storms.
      % It does simulate rain and snow, though.
      if piPreviousPhase <> SysDayPhase
      {
         Send(self,@DoPhaseWeather);
      }

      propagate;
   }

   GetPhaseThreats()
   {
      return [&ShockStorm];
   }

   SomethingTryGo(what = $,row = $,col = $)
   {
      if IsClass(what,&Player)
         AND row=viGateRow
         AND col=viGateCol
      {
         if pbGateOpen
         {
            return TRUE;
         }
         else
         {
            Send(what,@MsgSendUser,#message_rsc=sorcerer_gate_closed_msg);
            return TRUE;
         }
      }

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
