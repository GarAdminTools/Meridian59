% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
WeaponSpellProc is WeaponObjectAttribute 

constants:

   include blakston.khd

   SELF_CAST_TYPE = 1
   DEFENDER_CAST_TYPE = 2
   NO_TARGET_CAST_TYPE = 3

   MAZE_WEAPON_SPELL_PROC_PROFILE = 10

resources:

   include weaponspellproc.lkod

   weapon_proc = \
      "The %s worn by %s gives off a magical flare, casting %s on %s!"
   weapon_proc_you = "Your %s gives off a magical flare, casting %s on you!"
   weapon_proc_them = "Your %s gives off a magical flare, casting %s on %s!"
   weapon_proc_nobody = "Your %s gives off a magical flare, casting %s!"
   weapon_proc_no_target = \
      "The %s worn by %s gives off a magical flare, casting %s!"
   weapspellproc_attribute_desc = \
      "\n\nUpon a successful strike, the runes engraved on this %s will cast "
      "%s %s with %i spellpower %i%% of the time."
   wsp_attribute_self_rsc = "on you"
   wsp_attribute_target_rsc = "on the enemy"

classvars:

properties:

   piSpellNum = $
   piType = SELF_CAST_TYPE
   piSpellPower = 70
   piProcChance = 15

messages:

   Constructor(host_object=$,set_spell=$,set_type=1,set_spellpower=1,set_proc_chance=0,use_profile=MAZE_WEAPON_SPELL_PROC_PROFILE)
   {
      local iRandomRoll, lProfile, i;

      if set_spell <> $
      {
         piSpellNum = set_spell;
         piType = set_type;
         piSpellPower = set_spellpower;
         piProcChance = set_proc_chance;

         propagate;
      }

      % Profile of format [[chance roll >=, chance roll <=, SID, type, min power roll, max power roll, min proc chance roll, max proc chance roll], ...]
      lProfile = $;
      Switch(use_profile)
      {
         case MAZE_WEAPON_SPELL_PROC_PROFILE:
            lProfile = [[0,  10,  SID_HOLD,                2, 1, 30, 1, 5],
                        [11, 20,  SID_BLIND,               2, 1, 30, 1, 5],
                        [21, 30,  SID_PURGE,               2, 1, 30, 1, 5],
                        [31, 40,  SID_DAZZLE,              2, 1, 30, 1, 5],
                        [41, 50,  SID_VAMPIRIC_DRAIN,      2, 1, 15, 1, 5],
                        [51, 60,  SID_SHOCKING_FURY,       2, 1, 15, 1, 5],
                        [61, 70,  SID_EXPLOSIVE_FROST,     2, 1, 15, 1, 5],
                        [71, 80,  SID_ILLUSIONARY_WOUNDS,  2, 1, 15, 1, 5],
                        [81, 90,  SID_BLAST_OF_FIRE,       2, 1, 15, 1, 5],
                        [91, 99,  SID_SPLASH_OF_ACID,      2, 1, 15, 1, 5],
                        [100,100, SID_SLITHERBOLT,         2, 1, 15, 1, 5]
                       ];
            
            break;
      }
      
      if lProfile = $
      {
         Post(self,@Delete);
         propagate;
      }
      
      iRandomRoll = Random(1,100);
      
      foreach i in lProfile
      {
         if host_object <> $
            AND IsClass(host_object,&Weapon)
         {
            if iRandomRoll >= Nth(i,1)
               AND iRandomRoll <= Nth(i,2)
            {
               piSpellNum = Nth(i,3);
               piType = Nth(i,4);
               piSpellPower = Random(Nth(i,5),Nth(i,6));
               piProcChance = Random(Nth(i,7),Nth(i,8));
               propagate;
            }
         }
      }

      propagate;
   }

   GetSpell()
   {
      return Send(SYS,@FindSpellByNum,#num=piSpellNum);
   }

   GetType()
   {
      return piType;
   }

   GetSpellpower()
   {
      return piSpellPower;
   }

   GetProcChance()
   {
      return piProcChance;
   }

   ModifyDamage(damage = $,who = $,what = $)
   {
      local i, each_obj, oRoom, oSpell;

      if who = $
      {
         return damage;
      }

      oRoom = Send(who,@GetOwner);

      if piSpellNum <> $
         AND Random(1,100) <= piProcChance
      {
         oSpell = Send(SYS,@FindSpellByNum,#num=piSpellNum);
         
         if oSpell = $
         {
            return damage;
         }

         if piType = SELF_CAST_TYPE
            AND NOT Send(who,@IsEnchanted,#what=oSpell)
         {
            foreach i in Send(oRoom,@GetHolderActive)
            {
               each_obj = Send(oRoom,@HolderExtractObject,#data=i);
               if IsClass(each_obj,&Player)
                  AND each_obj <> who
               {
                  Post(each_obj,@MsgSendUser,#message_rsc=weapon_proc,
                        #parm1=Send(Send(self,@GetHostObject),@GetName),
                        #parm2=Send(who,@GetName),#parm3=Send(oSpell,@GetName),
                        #parm4=Send(who,@GetHimHer));
               }
               if IsClass(each_obj,&Player)
                  AND each_obj = who
               {
                  Post(each_obj,@MsgSendUser,#message_rsc=weapon_proc_you,
                        #parm1=Send(Send(self,@GetHostObject),@GetName),
                        #parm2=Send(oSpell,@GetName));
               }
            }
            Post(oSpell,@CastSpell,#who=who,#ltargets=[who],
                  #iSpellPower=piSpellPower,#bItemCast=TRUE);
         }

         % IsEnchanted check skipped so that the proc can replace enemy
         % buffs with weaker versions. For example, a Qor player may wish
         % to proc a weaker Eagle Eyes on an enemy that they are hitting.
         if piType = DEFENDER_CAST_TYPE
            AND what <> $
         {
            foreach i in Send(oRoom,@GetHolderActive)
            {
               each_obj = Send(oRoom,@HolderExtractObject,#data=i);
               if IsClass(each_obj,&Player)
                  AND each_obj <> who
               {
                  Post(each_obj,@MsgSendUser,#message_rsc=weapon_proc,
                        #parm1=Send(Send(self,@GetHostObject),@GetName),
                        #parm2=Send(who,@GetName),#parm3=Send(oSpell,@GetName),
                        #parm4=Send(what,@GetName));
               }
               if IsClass(each_obj,&Player)
                  AND each_obj = who
               {
                  Post(each_obj,@MsgSendUser,#message_rsc=weapon_proc_them,
                     #parm1=Send(Send(self,@GetHostObject),@GetName),
                     #parm2=Send(oSpell,@GetName),#parm3=Send(what,@GetName));
               }
            }
            Post(oSpell,@CastSpell,#who=who,#ltargets=[what],
                  #iSpellPower=piSpellPower,#bItemCast=TRUE);
         }

         if piType = NO_TARGET_CAST_TYPE
         {
            foreach i in Send(oRoom,@GetHolderActive)
            {
               each_obj = Send(oRoom,@HolderExtractObject,#data=i);
               if IsClass(each_obj,&Player)
                  AND each_obj <> who
               {
                  Post(each_obj,@MsgSendUser,#message_rsc=weapon_proc,
                        #parm1=Send(Send(self,@GetHostObject),@GetName),
                        #parm2=Send(who,@GetName),#parm3=Send(oSpell,@GetName));
               }
               if IsClass(each_obj,&Player)
                  AND each_obj = who
               {
                  Post(each_obj,@MsgSendUser,#message_rsc=weapon_proc_nobody,
                        #parm1=Send(Send(self,@GetHostObject),@GetName),
                        #parm2=Send(oSpell,@GetName));
               }
            }
            Post(oSpell,@CastSpell,#who=who,
                  #lTargets=Send(oSpell,@GetTargets,#who=who),
                  #iSpellPower=piSpellPower,#bItemCast=TRUE);
         }
      }

      return damage;
   }

   AttributeDesc()
   {
      local target_type;

      target_type = Send(self,@GetType);
      if target_type = 1
      {
         AddPacket(4,weapspellproc_attribute_desc, 4,Send(poHostObject,@GetName),
                   4,Send(Send(self,@GetSpell),@GetName),4,wsp_attribute_self_rsc,
                   4,Send(self,@GetSpellPower),4,Send(self,@GetProcChance));
      }
      else if target_type = 2
      {
         AddPacket(4,weapspellproc_attribute_desc, 4,Send(poHostObject,@GetName),
                   4,Send(Send(self,@GetSpell),@GetName),4,wsp_attribute_target_rsc,
                   4,Send(self,@GetSpellPower),4,Send(self,@GetProcChance));
      }
      else
      {
         AddPacket(4,system_blank_resource);
      }

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
