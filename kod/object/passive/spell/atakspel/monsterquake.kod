% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
MonsterEarthquake is AttackSpell

% This is a spell 'fixed' for use by monsters.
% They skip the trance portion and cause server-crashing bugs.

constants:

   include blakston.khd

resources:

   monster_earthquake_name_rsc = "monstrous earthquake"
   monster_earthquake_icon_rsc = irthquak.bgf
   monster_earthquake_desc_rsc = \
      "Moves the earth violently causing damage to all beings in the room. "
      "The more experienced the caster, the better damage is directed "
      "to the target and away from the caster. "
      "Requires red mushrooms and purple mushrooms to cast."
   monster_earthquake_mastered_desc_rsc = \
      "\n\n"
      "You possess exceptional mastery of this spell, allowing you to "
      "deal damage throughout triple the range of an amateur casting."

   monster_earthquake_first_rsc = \
      "You raise your arms to the sky and invoke the fury of Faren!"
   monster_earthquake_third_rsc = \
      "%s%s raises %s arms to the sky and invokes the fury of Faren!"

   monster_earthquake_killed_someone = \
      "The tremors knock %s%s to the ground - %s doesn't get up!"     
   monster_earthquake_killed_self = \
      "You stumble as the tremors knock you to the ground, and everything "
      "goes black..."

   monster_earthquake_cast_rsc = \
      "The earth convulses beneath your feet, tossing you around like a rag "
      "doll."
   monster_earthquake_hit_rsc = \
      "%s%s lets out a cry of pain and surprise as the earth convulses "
      "beneath %s feet."

   monster_earthquake_sound = fquake.wav

classvars:

   vrName = monster_earthquake_name_rsc
   vrDesc = monster_earthquake_desc_rsc
   vrIcon = monster_earthquake_icon_rsc
   vrMasteredDesc = monster_earthquake_mastered_desc_rsc

   vrKilled_Someone = monster_earthquake_killed_someone
   vrKilled_Self = monster_earthquake_killed_self
   vrCast = monster_earthquake_cast_rsc
   vrHit = monster_earthquake_hit_rsc

   viSpell_num = SID_MONSTER_EARTHQUAKE
   viSchool = SS_FAREN
   viSpell_level = 5
   viMana = 12

   viCast_time = 5000
   viChance_to_increase = 15
   viMeditate_ratio = 75

   viAttack_spell = ATCK_SPELL_ALL + ATCK_SPELL_QUAKE
   viOutlaw = TRUE
   viHarmful = TRUE
   viNoNewbieOffense = TRUE

   % Earthquake still handles range in row/col units, as it affects
   % everyone in the radius regardless of height difference.
   % Damage is 100% within this distance
   viMax_damage_distance = 8
   % Damage is 0 outside this distance
   viZero_damage_distance = 20

   vbIsAreaEffect = TRUE
   vbSelfHarming = TRUE
   
   vbSafetyReport = FALSE

properties:

   piDamageMin = 17
   piDamageMax = 25

   piCasterDmgPct = 75
   piManaFocusBonus = 0

messages:

   ResetReagents()
   {
      plReagents = $;

      return;
   }

   GetNumSpellTargets()
   {
      return 0;
   }

   GetTargets(who=$,lTargets=$)
   "This returns a list of valid targets in the room."
   {
      local oRoom, i, each_obj, lFinalTargets;

      if who = $
      {
         return;
      }

      lFinalTargets = $;

      if IsClass(who,&Room)
      {
         oRoom = who;
      }
      else
      {
         oRoom = Send(who,@GetOwner);
      }

      foreach i in Send(oRoom,@GetHolderActive)
      {
         each_obj = Send(oRoom,@HolderExtractObject,#data=i);

         % We do the guildhall check here now, not added to lFinalTargets
         % if they're not in the same zone as the caster.
         if IsClass(each_obj,&Battler)
            AND NOT (IsClass(oRoom,&GuildHall)
               AND Send(oRoom,@InFoyer,#who=each_obj)
                  <> Send(oRoom,@InFoyer,#who=who))
         {
            % Skip players who are phased/in spectator mode.
            if IsClass(each_obj,&Player)
               AND Send(each_obj,@IsInCannotInteractMode)
            {
               continue;
            }
            
            % Skip evil twins.
            if IsClass(each_obj,&EvilTwin)
            {
               continue;
            }
            
            % Skip monsters.
            if IsClass(each_obj,&Monster)
            {
               continue;
            }

            lFinalTargets = Cons(each_obj,lFinalTargets);
         }
      }

      return lFinalTargets;
   }

   CastSpell(who=$,lTargets=$,iSpellPower=0,bItemCast=FALSE)
   {
      local iDamage, oRoom;

      % First off, get the room this was cast in. If a room cast this, set
      % that room to oRoom.
      if who <> $
      {
         if IsClass(who,&Room)
         {
            oRoom = who;
         }
         else
         {
            oRoom = Send(who,@GetOwner);
         }
      }
      else
      {
         return;
      }

      % Call DoEarthQuake to handle special messages
      Send(self,@DoEarthQuake,#who=who,#lTargets=lTargets,#oRoom=oRoom);

      propagate;
   }

   DoEarthQuake(who=$,lTargets=$,oRoom=$)
   {
      local i, lActive, each_obj;

      lActive = Send(oRoom,@GetHolderActive);

      % Do the rumble effect, with a chance to disrupt casting. :)
      % Post it so that it won't affect this casting.
      Post(oRoom,@Rumble,#duration=3000,#disruption=100);

      % Send a message to all in room of spellcasters actions
      foreach i in lActive
      {
         each_obj = Send(oRoom,@HolderExtractObject,#data=i);
         
         if IsClass(each_obj,&Player)
            AND who <> $
         {
            if each_obj = who
            {
               Send(each_obj,@MsgSendUser,#message_rsc=monster_earthquake_first_rsc);
            }
            else
            {
               if who <> oRoom
               {
                  Send(each_obj,@MsgSendUser,#message_rsc=monster_earthquake_third_rsc,
                     #parm1=Send(who,@GetCapDef),#parm2=Send(who,@GetName),
                     #parm3=Send(who,@GetHisHer));
               }
            }
         }
      }

      % Everybody in the room hears the earthquake sound.
      Send(oRoom,@SomethingWaveRoom,#wave_rsc=monster_earthquake_sound);

      return;
   }

   ComputeDamage(who=$,target=$,iDamage=$)
   "Return the damage to be applied to the given target, taking into account "
   "distance."
   {
      local iPercent, iDistance_squared, iMax_distance_squared,
         iZero_distance_squared;

      if IsClass(who,&Room)
      {
         return 3000;
      }
      
      % Do full damage within viMax_damage_distance, zero damage outside
      % viZero_damage_distance, and an amount that falls off with distance
      % between the two.
      iPercent = 100;

      if who <> $
      {
         iDistance_squared = Send(who,@SquaredDistanceTo,#what=target);

         % SquaredDistanceTo returns $ for target not in same room
         if iDistance_squared = $
         {
            return 0;
         }
      }
      else
      {
         % Full damage for earthquakes without a caster
         iDistance_squared = 0;
      }

      iMax_distance_squared = viMax_damage_distance * viMax_damage_distance;
      iZero_distance_squared = viZero_damage_distance * viZero_damage_distance;
      
      if Send(who,@HasMasteredThisSpell,#num=viSpell_num)
      {
         iMax_distance_squared = iMax_distance_squared * 3;
         iZero_distance_squared = iZero_distance_squared * 3;
      }

      if iDistance_squared > iMax_distance_squared
      {
         if iDistance_squared > iZero_distance_squared
         {
            iPercent = 0;
         }
         else
         {
         iPercent = 100 * (iZero_distance_squared - iDistance_squared) /
                              (iZero_distance_squared - iMax_distance_squared);
         }
      }
      %Debug("percent = ", iPercent, " and iDamage is ", iDamage);
      iDamage = (iDamage*iPercent)/100;
      %Debug("damage after % is ",iDamage);
      return iDamage;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
