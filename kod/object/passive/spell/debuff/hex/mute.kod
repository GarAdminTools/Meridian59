% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Mute is Hex

constants:

   include blakston.khd

   % Mute's power lies in the fact that it suppresses stance changes.

resources:

   mute_cast_rsc = \
      "%s%s suddenly goes silent."
   mute_already_enchanted = \
      "That poor soul is already afflicted by mute."
   mute_start = "You find yourself unable to speak."
   mute_stop = \
     "The strange affliction fades, and you find yourself able to speak again."
   has_been_hexed_mute = \
      "%s%s suddenly goes silent!"

   mute_name_rsc = "mute"
   mute_icon_rsc = ihexmute.bgf
   mute_desc_rsc = \
      "Temporarily prevents the victim from speaking, preventing "
      "the intonation of stance mantras and other important spoken tactics. "
      "This hex requires one unholy fragment to cast."

   mute_hex_secondary_report = \
      "At that extreme effectiveness, your mute hex will prevent all of the victim's communications."
   mute_hex_effect_report = \
      "At that effectiveness, your mute hex will block ~r~B%i\% ~nof the victim's attempts to communicate."   

   mute_sound = qenfeebl.wav

classvars:

   vrName = mute_name_rsc
   vrIcon = mute_icon_rsc
   vrDesc = mute_desc_rsc

   vrHexEffectReport = mute_hex_effect_report

   vrAlreadyEnchanted = mute_already_enchanted
   vrEnchantment_On = mute_start
   vrEnchantment_Off = mute_stop
   vrSuccess = mute_cast_rsc
   vrInformRoom = has_been_hexed_mute

   viSpell_num = SID_HEX_MUTE
   viSchool = SS_WITCHERY
   viSubSchool = SSS_WITCHERY_XAERDUN
   viSpell_level = 1
   viMana = 2
   viSpell_Exertion = 2

   viFlash = FLASH_BAD

   vrSucceed_wav = mute_sound
   viChance_To_Increase = 25
   viMeditate_ratio = 30

properties:

   % This much chance to prevent communication
   piBaseEffect = 1

   piFlatDuration = 120000

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&UnholyFragment,1],plReagents);

      return;
   }

   ReqNormalSay(state=0)
   {
      if Random(1,100) < piBaseEffect + state
      {
         return FALSE;
      }

      return TRUE;
   }

   DoHexEffectReport(what=$,iHexEffect=0)
   {
      if piBaseEffect+iHexEffect >= 100
      {
         Send(what,@MsgSendUser,#message_rsc=mute_hex_secondary_report);
      }
      else
      {
         Send(what,@MsgSendUser,#message_rsc=vrHexEffectReport,#parm1=piBaseEffect+iHexEffect);
      }
      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
