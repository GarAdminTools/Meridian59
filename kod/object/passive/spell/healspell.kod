% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
HealSpell is Spell

constants:

   include blakston.khd

resources:

   heal_harm_rsc = \
      "%s%s's corrupted spell harms you for ~r~B%i~B~n hitpoints!"
   heal_harm_self_rsc = \
      "Your corrupted spell harms you for ~r~B%i~B~n hitpoints!"
   heal_harm_other_rsc = \
      "Your corrupted heal harms %s%s for ~r~B%i~B~n hitpoints!"

   heal_cast_rsc = \
      "%s%s's spell heals some minor injuries, restoring ~g~B%i~B~n hitpoints."
   heal_cast_self = \
      "Shal'ille's holy magic heals some minor injuries, restoring "
      "~g~B%i~B~n hitpoints."
   heal_cast_on_other = "You heal %s%s for ~g~B%i~B~n hitpoints."
   heal_unnecessary_rsc = "%s%s is perfectly healthy."
   
   cannot_attack_with_heal = \
      "If you were you to heal %s%s under their current afflictions, they would be harmed instead, and you can't attack them right now!"

classvars:

   vrHealCastOnYouMsg   = heal_cast_rsc
   vrHealCastSelfMsg    = heal_cast_self
   vrHealCastOnOtherMsg = heal_cast_on_other

   viFlash = FLASH_GOOD

   viHealAttackType = 0
   viHealSpellType = 0

   vbSingleTargetHeal = TRUE

   % This is 1-5 health base
   viHealBaseMinimum = 100
   viHealBaseMaximum = 500

   % How much does each point of spellpower add?
   % In this case, spellpower can add up to 500  (aka 100 * 5)
   viSpellpowerHealMultiplier = 5
   
   % Do we affect caster's karma?
   vbAffectCasterKarma = FALSE
   viKarmaSwingFactor = 0
   
   % Do we remove all bleeds? 
   vbRemoveAllBleeds = FALSE

   % Do we lower max health equal to half total healing?
   vbCauterizeEffect = FALSE

properties:

messages:

   GetNumSpellTargets()
   {
      if vbSingleTargetHeal
      {
         return 1;
      }
      return 0;
   }

   CanPayCosts(who = $, lTargets = $)
   {
      local target, i, iHeal;

      % Can cast spell if the 1 target item is a user
      if vbSingleTargetHeal
      {
         if Length(lTargets) <> 1
         {
            return FALSE;
         }

         target = First(lTargets);
         if NOT IsClass(target,&Battler)
         {
            return FALSE;
         }

         % Let's use a test heal to see if Hexes on the target reverse it.
         iHeal = 100;
         foreach i in Send(target,@GetEnchantmentsByClass,#enchClass=&Hex)
         {
            iHeal = Send(Nth(i,2),@ModifyHeal,#amount=iHeal,#state=Nth(i,3));
         }
         
         if iHeal < 0
         {
            % This is an attack now.
            if NOT Send(who,@AllowPlayerAttack,#victim=target)
            {
               Send(who,@MsgSendUser,#message_rsc=cannot_attack_with_heal,
                     #parm1=Send(target,@GetDef),#parm2=Send(target,@GetName));

               return FALSE;
            }
         }
         else
         {
            if Send(target,@GetHealth) >= Send(target,@GetMaxHealth)
            {
               Send(who,@MsgSendUser,#message_rsc=heal_unnecessary_rsc,
                     #parm1=Send(target,@GetCapDef),#parm2=Send(target,@GetName));

               return FALSE;
            }
         }
      }

      propagate;
   }

   CastSpell(who = $, lTargets = $, iSpellPower=0)
   {
      local iHeal, oTarget, i, oRoom, iResult, lCasterEmpowerments, iEmpowermentPercentage, iNumBleeds;

      if NOT vbSingleTargetHeal
      {
         propagate;
      }
      
      oTarget = First(lTargets);

      iHeal = Random(viHealBaseMinimum,viHealBaseMaximum) + ((iSpellPower + 1)*viSpellpowerHealMultiplier);
      
      lCasterEmpowerments = Send(who,@GetCurrentEmpowerments);
      iEmpowermentPercentage = 0;
      foreach i in lCasterEmpowerments
      {
         if Nth(i,1) > 0
         {
            % Weapon empowerment.
            if viHealAttackType & Nth(i,1)
               OR (viHealAttackType <> 0
                  AND (ATCK_WEAP_ALL & -Nth(i,1)))
            {
               iEmpowermentPercentage = iEmpowermentPercentage + Nth(i,2);
            }
         }
         else
         {
            % Spell empowerment.
            if viHealSpellType & -Nth(i,1)
               OR (viHealSpellType <> 0
                  AND (ATCK_SPELL_ALL & -Nth(i,1)))
            {
               iEmpowermentPercentage = iEmpowermentPercentage + Nth(i,2);
            }
         }
      }
      
      if iEmpowermentPercentage <= 0
      {
         % These are heals, let's ignore negative empowerments.
         iEmpowermentPercentage = 0;
      }
      iHeal = (iHeal * (100+iEmpowermentPercentage))/100;
      
      foreach i in Send(oTarget,@GetEnchantmentsByClass,#enchClass=&Hex)
      {
         iHeal = Send(Nth(i,2),@ModifyHeal,#amount=iHeal,#state=Nth(i,3));
      }

      if iHeal < 0
         AND Send(who,@AllowPlayerAttack,#victim=oTarget)
      {
         iResult = 0;
         iResult = Send(oTarget,@AssessDamage,#what=who,
                                #damage=-iHeal,
                                #atype=viHealAttackType,
                                #aspell=viHealSpellType,
                                #precision=TRUE);

         if iResult = $
         {
            % The target died!
            if IsClass(who,&Battler)
            {
               Send(who,@KilledSomething,#what=oTarget,#use_weapon=self);
            }
         }
         else
         {
            if who <> oTarget
            {
               Send(who,@MsgSendUser,#message_rsc=heal_harm_other_rsc,
                                     #parm1=Send(oTarget,@GetDef),
                                     #parm2=Send(oTarget,@GetName),
                                     #parm3=iResult/100);
               Send(oTarget,@MsgSendUser,#message_rsc=heal_harm_rsc,
                                         #parm1=Send(who,@GetCapDef),
                                         #parm2=Send(who,@GetName),
                                         #parm3=iResult/100);
            }
            else
            {
               Send(who,@MsgSendUser,#message_rsc=heal_harm_self_rsc,
                                     #parm1=iResult/100);
            }
         }

         propagate;
      }
      else
      {
         iHeal = Send(oTarget,@GainHealthNormal,#amount=iHeal,#precision=TRUE);

         if vbRemoveAllBleeds
         {
            Send(oTarget,@RemoveEnchantmentClass,#class=&Bleed);
         }
         
         if vbCauterizeEffect
         {
            Send(oTarget,@AssessDamage,#what=who,
                             #damage=iHeal/2,
                             #atype=0,
                             #aspell=ATCK_SPELL_DRAIN,
                             #precision=TRUE,
                             #bSecondary=TRUE);
         }
      }

      if who <> oTarget
      {
         Send(who,@MsgSendUser,#message_rsc=vrHealCastOnOtherMsg,
               #parm1=Send(oTarget,@GetDef),#parm2=Send(oTarget,@GetName),
               #parm3=iHeal/100);

         Send(oTarget,@MsgSendUser,#message_rsc=vrHealCastOnYouMsg,
               #parm1=Send(who,@GetCapDef),#parm2=Send(who,@GetName),
               #parm3=iHeal/100);

         if vbAffectCasterKarma
         {
            % Give them a little boost if they help someone of higher Karma.
            if Send(who,@GetKarma) < Send(oTarget,@GetKarma)
            {
               Send(who,@AddKarma,#amount = Send(who,@CalculateKarmaChangeFromAct,
                     #karma_doer=Send(who,@GetKarma),#karma_act=Send(oTarget,@GetKarma),
                     #Swing_factor = viKarmaSwingFactor));
            }
         }
         
         oRoom = Send(oTarget,@GetOwner);
         if oRoom <> $
            AND Send(oRoom,@IsInBuilderGroup,#who=oTarget)
         {
            Send(who,@JoinBuilderGroup);
         }
         
         % Notify room we healed someone.
         if oRoom <> $
         {
            Send(oRoom,@PlayerHealedSomeone,#caster=who,#oTarget=oTarget,#iHealAmount=iHeal/100);
         }
      }
      else
      {
         Send(who,@MsgSendUser,#message_rsc=vrHealCastSelfMsg,#parm1=iHeal/100);
         
         oRoom = Send(oTarget,@GetOwner);
         % Notify room we healed someone.
         if oRoom <> $
         {
            Send(oRoom,@PlayerHealedSomeone,#caster=who,#oTarget=oTarget,#iHealAmount=iHeal/100);
         }
      }

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
