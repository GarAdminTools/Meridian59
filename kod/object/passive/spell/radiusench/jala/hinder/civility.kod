% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Civility is HinderSpell

constants:

   include blakston.khd

resources:

   include civility.lkod

   Civility_name_rsc = "civility"
   Civility_icon_rsc = icivilty.bgf
   Civility_desc_rsc = \
      "This song hinders the casting of the spells of the master of the elements, Faren, "
      "and will protect listeners somewhat from said elements. "
      "A ruby and a mushroom are required to begin performing this song."

   civility_resist_rsc = \
      "\n\nIn addition to hindering the magics of Faren, the Civility song currently affecting "
      "you adds %i\% resistance to the elements of nature."

   Civility_song = jala3.mp3

classvars:

   vrName = Civility_name_rsc
   vrIcon = Civility_icon_rsc
   vrDesc = Civility_desc_rsc

   viMana = 5          % Mana is amount used upon inititiation
   viManaDrain = 3     % Drain is amount used every viDrainTime milliseconds
   viDrainTime = 5000    % Drain some mana every viDrainTime milliseconds
   viSpell_num = SID_CIVILITY

   viSchool = SS_JALA
   viSpell_level = 2

   viHinderedSchool = SS_FAREN

   viResistOne = -ATCK_SPELL_FIRE
   viResistTwo = -ATCK_SPELL_SHOCK
   viResistThree = -ATCK_SPELL_COLD

   viDivisorOne = 9
   viDivisorTwo = 9
   viDivisorThree = 9

properties:

   prMusic = civility_song

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Ruby,1],plReagents);
      plReagents = Cons([&Mushroom,1],plReagents);

      return;
   }

   SendStatsDesc(who=$)
   {
      if who <> $
         AND IsClass(who,&User)
         AND Send(who,@GetMostPowerfulRadiusEnchantmentState,#byClass=&Civility) <> $
      {
         Send(self,@EffectDesc,#who=who);

         return;
      }

      propagate;
   }

   EffectDesc(who=$)
   {
      local lState, iDivisorOne, iDivisorTwo, iDivisorThree, iResistOne, iResistTwo, iResistThree;
      
      lState = Send(who,@GetMostPowerfulRadiusEnchantmentState,#byClass=&Civility);

      if lState <> $
      {
         iDivisorOne = viDivisorOne;
         if iDivisorOne = 0
         {
            iDivisorOne = 1;
         }
         iDivisorTwo = viDivisorTwo;
         if iDivisorTwo = 0
         {
            iDivisorTwo = 1;
         }
         iDivisorThree = viDivisorThree;
         if iDivisorThree = 0
         {
            iDivisorThree = 1;
         }
         iResistOne = Nth(lState,2)/iDivisorOne;
         AddPacket(4,civility_resist_rsc, 4,iResistOne);
      }

      propagate;
   }

end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
