% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Tumble is BanditrySkill

constants:

   include blakston.khd
   include protocol.khd   

resources:

   Tumble_name_rsc = "tumble"
   Tumble_icon_rsc = dodgeico.bgf
   Tumble_desc_rsc = \
      "The bandit nomads of Biskalane had quite the harsh environment in which "
      "to forge their fighters. Using their generational arts, "
      "agile bandits can sometimes entirely tumble out of the way of an attack spell. "
      "It is difficult to perform successfully, but even one avoided fireball or explosive frost "
      "might mean the difference between life and death."

classvars:

   vrName = Tumble_name_rsc
   vrIcon = Tumble_icon_rsc
   vrDesc = Tumble_desc_rsc

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viSkill_num = SKID_TUMBLE
   viSchool = SKS_BANDITRY
   viSkill_Level = 3
   viChance_to_Increase = 35
   viMeditate_ratio = 40

   viSkillExertion = 0
   vbCheck_exertion = FALSE

   vbAutomatic = TRUE
   viShow_enchantment_icon = 0x02
   
   viPrimaryStat = STAT_ID_AGILITY
   viSecondaryStat = 0

properties:

messages:

   GetRequisiteStat(who=$)
   {
      return Send(who,@GetAgility);
   }
   
   GetTumblePercentage(who=$, what=$)
   {
      local iAgility, iAbility;
      
      if IsClass(who,&Monster)
      {
         return Send(who,@GetTumbling);
      }

      if IsClass(who,&Player)
      {
         % This skill can be used by both players and monsters.
         iAgility = Send(who,@GetAgility);
         iAbility = Send(who,@GetSkillAbility,#skill_num=viSkill_num);
         
         if iAbility = 0
         {
            % Player does not have the skill!
            return 0;
         }
         if Send(who,@IsResting)
            OR Send(who,@CheckPlayerFlag,#flag=PFLAG_NO_MOVE)
         {
            % Can't tumble while resting or paralyzed.
            return 0;
         }
         
         if Send(who,@FindHolding,#class=&BanditryCrest) <> $
         {
            return iAbility + 1 + iAgility;
         }
         
         % up to 100 from ability and up to another 100 from Agility
         % up to 10% chance to avoid spell damage
         return (iAbility + 1 + iAgility)/2;
      }
      
      return 0;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
