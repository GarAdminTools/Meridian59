% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Vrikshasana is Breath

constants:

   include blakston.khd
   include protocol.khd   

resources:

   Vrikshasana_name_rsc = "vrikshasana"
   Vrikshasana_icon_rsc = idetinvi.bgf
   Vrikshasana_desc_rsc = \
      "An intermediate breath technique that seeks absolute stillness. "
      "In the vrikshasana pose, an intermediate monk lifts their arms and takes on the aspect of a tree. "
      "Total inner silence allows the monk to hear the invisible and sense the hidden."
      "\n\nHigher level asanas also contain the benefits of "
      "their lower level counterparts. When resting, "
      "a monk will gain the boosts of all the asanas "
      "they know. However, being hit disrupts their pose, "
      "so asanas require that the monk hasn't been hit in the last few seconds."
   
   Vrikshasana_begin = "You breathe rapidly and lightly."
   Vrikshasana_end = "You stop breathing rapidly and lightly."
   
   Vrikshasana_start_resting = "You lift your arms and become like the tree, taking on a vrikshasana pose."
   Vrikshasana_stop_resting = "You return to your feet."

   Vrikshasana_intro = \
      "Shadowcraft Lv. 3: Detect hidden and invisible enemies while resting."

classvars:

   vrName = Vrikshasana_name_rsc
   vrIcon = Vrikshasana_icon_rsc
   vrDesc = Vrikshasana_desc_rsc

   breath_achieved_msg = Vrikshasana_begin
   breath_end_msg = Vrikshasana_end
   breath_start_resting = Vrikshasana_start_resting
   breath_end_resting = Vrikshasana_stop_resting

   vrSkill_intro = Vrikshasana_intro

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viSkill_num = SKID_VRIKSHASANA
   viSchool = SKS_SHADOWCRAFT
   viSkill_Level = 3
   viChance_to_Increase = 35
   viMeditate_ratio = 40

   viSkillExertion = 0
   vbCheck_exertion = FALSE

   vbAutomatic = TRUE
   
   viPrimaryStat = STAT_ID_STAMINA
   viSecondaryStat = 0

properties:

messages:

   SenseHiddenAndInvisible(who=$)
   {
      local i, oRoom, iDrawingFlags;
      
      if NOT Send(who,@IsResting)
         OR Send(who,@WasHitRecently)
      {
         return;
      }
      
      oRoom = Send(who,@GetOwner);
      if oRoom <> $
      {
         foreach i in Send(oRoom,@GetHolderActive)
         {
            if IsClass(First(i),&Battler)
            {
               iDrawingFlags = Send(First(i),@GetDrawingEffects);
               
               if (iDrawingFlags & DRAWFX_MASK) = DRAWFX_INVISIBLE
                  OR (First(i) <> who
                      AND IsClass(First(i),&Player)
                      AND NOT IsClass(oRoom,&MinotaurMazeRoom)
                      AND Send(Send(SYS,@FindSkillByNum,#num=SKID_HIDE),@IsHiding,#who=First(i))
                      AND Send(First(i),@BuildMinimapDotFlag) = MM_NONE)
               {
                  Send(self,@ImproveAbility,#who=who);
                  Send(who,@SomethingChanged,#what=First(i));
               }
            }
         }
      }
      return;
   }

   StoppedResting(who=$)
   {
      local i, oRoom;
      
      oRoom = Send(who,@GetOwner);
      if oRoom <> $
      {
         foreach i in Send(oRoom,@GetHolderActive)
         {
            if IsClass(First(i),&Battler)
            {
               Send(who,@SomethingChanged,#what=First(i));
            }
         }
      }
      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
