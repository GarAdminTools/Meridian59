% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
HuntIndomitablesBounty is PersonalBounty

constants:

   include blakston.khd

resources:

   hunt_indomitables_bounty_name = "Face an indomitable pair"
   hunt_indomitables_bounty_experience_reward_desc = \
      "Rumor has it that a legendary %s was seen in the vicinity of %s. "
      "Overflowing with strange vitality, defeating it would bring its slayer %i experience."
   hunt_indomitables_bounty_wisdom_reward_desc = \
      "One %s is bad enough, but a pair of them have recently ravaged %s. "
      "Both are indomitable, so the fight will be difficult, but doing so "
      "will earn you %i accumulated wisdom."
   hunt_indomitables_bounty_item_reward_desc = \
      "%s has placed a bounty on the head of a legendary %s that has been ravaging the countryside "
      "from its haunt at %s. Slaying this monster will earn the bounty of %i %ss."
   
   wisdom_reward_from_indomitables_bounty = \
      "~BHaving defeated both indomitable enemies, your mind floods with %i newfound wisdom!"

classvars:

   vrName = hunt_indomitables_bounty_name
   
   viChallengeNum = HUNT_TWO_INDOMITABLES
   viWisdomMultiplier = 20
   viExperienceMultiplier = 40
   
   viPriceMultiplier = 175

properties:

   vrDesc = hunt_indomitables_bounty_experience_reward_desc

   pcOfferingNPCClass = $

   piTargetRID = 0
   pcTargetClass = $
   
   piRewardAmount = 0
   pcRewardClass = $
   
   piWisdomRewardAmount = 0

   piExperienceRewardAmount = 0
   
   pbSpawnedMob = FALSE
   
   piKilledCount = 0

messages:
   
   GetPossibleTargets()
   {
      return Send(SYS,@GetChampionMonsterTemplates);
   }

   Constructed()
   {
      if piWisdomRewardAmount <> 0
      {
         vrDesc = hunt_indomitables_bounty_wisdom_reward_desc;
      }
      else if pcRewardClass <> $
      {
         vrDesc = hunt_indomitables_bounty_item_reward_desc;
      }
      propagate;
   }

   ShowDesc(who=$)
   {
      if piWisdomRewardAmount <> 0
      {
         AddPacket(4,vrDesc, 4,Send(Send(SYS,@FindMonsterByClass,#cClass=pcTargetClass),@GetName),
                             4,Send(Send(SYS,@FindRoomByNum,#num=piTargetRID),@GetName),
                             4,piWisdomRewardAmount);
      }
      return;
   }

   TargetKilled(what=$,oTarget=$)
   {
      if what = poPlayerOwner
         OR Send(oTarget,@GetHighestHurtMeRecentlyBattler) = poPlayerOwner
      {
         piKilledCount++;
      }
      
      if piKilledCount >= 2
      {
         Send(self,@CompleteBounty);
      }
      return;
   }
   
   VisitedAnArea(who=$,where=$)
   {
      local oMob;
      if NOT pbSpawnedMob
         AND Send(where,@GetRoomNum) = piTargetRID
      {
         oMob = Create(pcTargetClass,#bExemptFromChampionAlteration=TRUE,#oBounty=self);
         Post(where,@NewHold,#what=oMob,#new_row=Send(where,@GetTeleportRow),#new_col=Send(where,@GetTeleportCol));
         Post(oMob,@GainChampionStatus,#iType=CHAMPION_INDOMITABLE);
         oMob = Create(pcTargetClass,#bExemptFromChampionAlteration=TRUE,#oBounty=self);
         Post(where,@NewHold,#what=oMob,#new_row=Send(where,@GetTeleportRow),#new_col=Send(where,@GetTeleportCol));
         Post(oMob,@GainChampionStatus,#iType=CHAMPION_INDOMITABLE);
         pbSpawnedMob = TRUE;
      }
      return;
   }

   CompleteBounty()
   {
      if piWisdomRewardAmount <> 0
         AND poPlayerOwner <> $
      {
         Send(poPlayerOwner,@MsgSendUser,#message_rsc=wisdom_reward_from_indomitables_bounty,#parm1=piWisdomRewardAmount);
      }
      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
