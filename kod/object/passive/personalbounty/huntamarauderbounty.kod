% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
HuntAMarauderBounty is PersonalBounty

constants:

   include blakston.khd

resources:

   hunt_a_marauder_bounty_name = "Hunt a Marauder"
   hunt_a_marauder_bounty_experience_reward_desc = \
      "test"
   hunt_a_marauder_bounty_wisdom_reward_desc = \
      "The Marauder %s has been giving the authorities excessive trouble recently around %s. "
      "Hunt them down and put a stop to their waylaying and murdering. "
      "In doing so, you will capture %i of their accumulated wisdom."
   hunt_a_marauder_bounty_item_reward_desc = \
      "test"
   
   wisdom_reward_from_marauder_bounty = \
      "~BHaving defeated your foe, your mind floods with %i newfound wisdom!"

classvars:

   vrName = hunt_a_marauder_bounty_name
   
   viChallengeNum = HUNT_MARAUDERS
   viWisdomMultiplier = 10
   viExperienceMultiplier = 50
   
   viPriceMultiplier = 250

properties:

   vrDesc = hunt_a_marauder_bounty_wisdom_reward_desc

   pcOfferingNPCClass = $

   piTargetRID = 0
   pcTargetClass = $
   
   piRewardAmount = 0
   pcRewardClass = $
   
   piWisdomRewardAmount = 0

   piExperienceRewardAmount = 0
   
   pbSpawnedMob = FALSE

messages:
   
   GetPossibleTargets()
   {
      return Send(SYS,@GetMarauderMonsterTemplates);
   }

   Constructed()
   {
      if piWisdomRewardAmount <> 0
      {
         vrDesc = hunt_a_marauder_bounty_wisdom_reward_desc;
      }
      else if pcRewardClass <> $
      {
         vrDesc = hunt_a_marauder_bounty_item_reward_desc;
      }
      propagate;
   }

   ShowDesc(who=$)
   {
      if piWisdomRewardAmount <> 0
      {
         AddPacket(4,vrDesc, 4,Send(Send(SYS,@FindMonsterByClass,#cClass=pcTargetClass),@GetName),
                             4,Send(Send(SYS,@FindRoomByNum,#num=piTargetRID),@GetName),
                             4,piWisdomRewardAmount);
      }
      return;
   }

   TargetKilled(what=$,oTarget=$)
   {
      if what = poPlayerOwner
         OR Send(oTarget,@GetHighestHurtMeRecentlyBattler) = poPlayerOwner
      {
         Send(self,@CompleteBounty);
      }
      else
      {
         Send(self,@IncompleteBounty);
      }
      return;
   }
   
   VisitedAnArea(who=$,where=$)
   {
      local oMob;
      if NOT pbSpawnedMob
         AND Send(where,@GetRoomNum) = piTargetRID
      {
         oMob = Create(pcTargetClass,#bExemptFromChampionAlteration=TRUE,#oBounty=self);
         Post(where,@NewHold,#what=oMob,#new_row=Send(where,@GetTeleportRow),#new_col=Send(where,@GetTeleportCol));
         pbSpawnedMob = TRUE;
      }
      return;
   }

   CompleteBounty()
   {
      if piWisdomRewardAmount <> 0
         AND poPlayerOwner <> $
      {
         Send(poPlayerOwner,@MsgSendUser,#message_rsc=wisdom_reward_from_marauder_bounty,#parm1=piWisdomRewardAmount);
      }
      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
