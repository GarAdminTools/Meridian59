% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Gauntlet is AttackModifier

constants:

   include blakston.khd

resources:

   include gauntlet.lkod

  
   gauntlet_name_rsc = "gauntlets"
   gauntlet_icon_rsc = gauntlet.bgf
   gauntlet_desc_rsc = \
      "These polished steel gauntlets offer protection for "
      "hands and forearms allowing you to strike with "
      "greater confidence and force."

   gauntlet_condition_exc = " shine with the glint of perfection."
   gauntlet_condition_exc_mended = \
      " are in excellent condition, but have been patched before."
   gauntlet_condition_good = " are tarnished and scarred but still usable."
   gauntlet_condition_med = " have been harshly dented in several places."
   gauntlet_condition_poor = \
      " are in danger of falling apart.  Large plates of metal "
      "have been torn away during battle."
   gauntlet_condition_broken = " are ruined and offer no benefit."
   gauntlet_broken = "Your %s are a tangled mess of metal."

   standardshirt_rightarm_gauntlet_male = bre.bgf
   standardshirt_rightarm_gauntlet_female =brf.bgf
   standardshirt_leftarm_gauntlet_male = ble.bgf
   standardshirt_leftarm_gauntlet_female = blf.bgf

   barearm_rightarm_gauntlet_male = bri.bgf
   barearm_rightarm_gauntlet_female =brj.bgf
   barearm_leftarm_gauntlet_male = bli.bgf
   barearm_leftarm_gauntlet_female = blj.bgf

classvars:

   vrName = gauntlet_name_rsc
   vrIcon = gauntlet_icon_rsc
   vrDesc = gauntlet_desc_rsc
   vrPoss_article = object_article_cap_these_rsc

   vrItem_broken = gauntlet_broken

   viUse_type = ITEM_USE_GAUNTLET
   viItem_type = ITEMTYPE_ARMOR
   viUse_amount = 1

   viHits_init_min = 300
   viHits_init_max = 450

   viValue_average = 400
   viBulk = 50
   viWeight = 50

   viGround_group = 1
   viInventory_group = 2
   viBroken_group = 3

   vbShow_condition = TRUE
   vrCondition_exc = gauntlet_condition_exc 
   vrCondition_exc_mended = gauntlet_condition_exc_mended 
   vrCondition_good = gauntlet_condition_good 
   vrCondition_med = gauntlet_condition_med 
   vrCondition_poor = gauntlet_condition_poor 
   vrCondition_broken = gauntlet_condition_broken

   viDefense_base = 15
   viAttack_base = 20
   viDamage_base = 1

properties:

   piDefense_bonus = 15
   piAttack_bonus = 20
   piDamage_bonus = 1

messages:

   ReqUseSomething(what = $)
   {
      if IsClass(what,&Gauntlet)
         OR IsClass(what,&Glove)
      {
         if Send(poOwner,@TryUnuseItem,#what=self)
         {
            propagate;
         }
         else
         {
            return FALSE;
         }
      }

      propagate;
   }

   NewUsed(what = $)
   {
      local i, oFound, iPriority;

      oFound=$;
      foreach i in Send(what,@GetPlayerUsing)
      {
         iPriority = Send(i,@ItemChangesArms);
         if iPriority <> 0
         {
            if oFound = $ 
            {
               oFound = i;
            }
            else
            {
               if iPriority > Send(oFound,@ItemChangesArms)
               {
                  oFound = i;
               }
            }
         }
      }

      if oFound <> $
      {
         Send(oFound,@DoPlayerArt);
      }
      else
      {
         Send(what,@ResetPlayerArms);
      }

      % We're also a defense modifier
      Send(what,@AddDefenseModifier,#what=self);

      propagate;
   }

   NewUnused(what = $)
   {
      local i, oFound, iPriority;

      oFound=$;
      foreach i in Send(what,@GetPlayerUsing)
      {
         iPriority = Send(i,@ItemChangesArms);
         if iPriority <> 0
         {
            if oFound <> $ 
            {
               if iPriority > Send(oFound,@ItemChangesArms)
               {
                  oFound = i;
               }
            }
            else
            {
               oFound = i;
            }
         }
      }

      if oFound <> $
      {
         Send(oFound,@DoPlayerArt); 
      }
      else
      {
         Send(what,@ResetPlayerArms);
      }

      % We're also a defense modifier
      Send(what,@RemoveDefenseModifier,#what=self);

      propagate;
   }

   DefaultValues()
   {
      piDefense_bonus = viDefense_base;
      piAttack_bonus = viAttack_base;
      piDamage_bonus = viDamage_base;

      propagate;
   }

   OverrideArms()
   {
      return TRUE;
   }

   GetOverrideRightArm(who=$)
   {
      local i, bFound;

      bFound = FALSE;
      foreach i in Send(who,@GetPlayerUsing)
      {
         if IsClass(i,&TankTop)
         {
            bFound = TRUE;

            break;
         }
      }

      if bFound
      {
         if Send(who,@GetGender) = GENDER_MALE
         {
            return barearm_rightarm_gauntlet_male;
         }
      
         return barearm_rightarm_gauntlet_female;
      }

      if Send(who,@GetGender) = GENDER_MALE
      {
         return standardshirt_rightarm_gauntlet_male;
      }

      return standardshirt_rightarm_gauntlet_female;
   }

   GetOverrideLeftArm(who=$)
   {
      local i, bFound;

      bFound = FALSE;
      foreach i in Send(who,@GetPlayerUsing)
      {
         if IsClass(i,&TankTop)
         {
            bFound = TRUE;

            break;
         }
      }

      if bFound
      {
         if Send(who,@GetGender) = GENDER_MALE
         {
            return barearm_leftarm_gauntlet_male;
         }
      
         return barearm_leftarm_gauntlet_female;
      }

      if Send(who,@GetGender) = GENDER_MALE
      {
         return standardshirt_leftarm_gauntlet_male;
      }

      return standardshirt_leftarm_gauntlet_female;
   }

   % No alterations when we hit the target....
   WeaponHitTarget()
   {
      return;
   }

   ModifyHitRoll(who = $,what = $,hit_roll = 0)
   {
      local i, oWeapon, oOffenseMod;

      foreach i in plEnchantmentMods
      {
         if Nth(i,1) = &OffenseMod
         {
            oOffenseMod = Send(SYS,@GetMagicMod,#class=&OffenseMod);
            hit_roll = Send(oOffenseMod,@ModifyHitRoll,#who=who,#what=what,
                        #hit_roll=hit_roll,#iState=Nth(i,2));
         }
      }
      
      if who <> $
         AND NOT IsClass(who,&User)
      {
         return hit_roll + piAttack_bonus;
      }
      
      if who = $
      {
         return hit_roll + piAttack_bonus;
      }

      oWeapon = Send(who,@GetWeapon);
      if (oWeapon = $)
         or (not IsClass(oWeapon,&RangedWeapon))
      {
         return hit_roll + piAttack_bonus;
      }

      return hit_roll;
   }

   ModifyDamage(who = $,what = $,damage = 0)
   {
      local oWeapon, i, oWeaponDamageMod;

      foreach i in plEnchantmentMods
      {
         if Nth(i,1) = &WeaponDamageMod
         {
            oWeaponDamageMod = Send(SYS,@GetMagicMod,#class=&WeaponDamageMod);
            damage = Send(oWeaponDamageMod,@ModifyDamage,#who=who,#what=what,
                        #damage=damage,#iState=Nth(i,2));
         }
      }
 
      if who = $
      {
         return damage + piDamage_bonus;
      }
 
      % Add damage to non-ranged weapons (and punch)
      oWeapon = Send(who,@GetWeapon);
      if oWeapon = $
         OR NOT IsClass(oWeapon,&RangedWeapon)
      {
         return damage + piDamage_bonus;
      }

      return damage;
   }

   ModifyDefensePower(defense_power=$)
   {
      local i, oDefenseMod;

      foreach i in plEnchantmentMods
      {
         if Nth(i,1) = &DefenseMod
         {
            oDefenseMod = Send(SYS,@GetMagicMod,#class=&DefenseMod);
            defense_power = Send(oDefenseMod,@ModifyDefensePower,
                        #defense_power=defense_power,#iState=Nth(i,2));
         }
      }

      return defense_power + piDefense_bonus;
   }

   ModifyDefenseDamage(who=$,what=$,damage=$,atype=0,aspell=0)
   {
      local i, sum, oArmorMod;
      sum = 0;

      if Send(SETTINGS_OBJECT,@GetDirectArmorEnabled)
      {
         foreach i in plEnchantmentMods
         {
            if Nth(i,1) = &ArmorMod
            {
               oArmorMod = Send(SYS,@GetMagicMod,#class=&ArmorMod);
               damage = Send(oArmorMod,@ModifyDefenseDamage,#who=who,#what=what,
                           #damage=damage,#atype=atype,#aspell=aspell,
                           #iState=Nth(i,2));
            }
         }
      }

      return damage;
   }

   GetDamageReduction()
   {
      local i, sum;
      sum = 0;

      if Send(SETTINGS_OBJECT,@GetDirectArmorEnabled)
      {
         foreach i in plEnchantmentMods
         {
            if Nth(i,1) = &ArmorMod
            {
               sum = sum + Nth(i,2);
            }
         }
      }

      return sum;
   }

   CanMend()
   {
      local i, oItemAtt;

      foreach i in plItem_Attributes
      {
         oItemAtt = Send(SYS,@FindItemAttByNum,
                         #num=Send(self,@GetNumFromCompound,#compound=First(i)));
         if oItemAtt = $
         {
            Debug("Illegal ItemAtt in list!");

            continue;
         }

         if NOT Send(oItemAtt,@ItemCanMend,#oItem=self)
         {
            return FALSE;
         }
      }

      return TRUE;
   }

   GetHitBonus()
   {
      return Send(self,@ModifyHitRoll,#who=poOwner,#hit_roll=0);
   }

   GetDefenseBonus()
   {
      local i, sum;
      sum = 0;

      foreach i in plEnchantmentMods
      {
         if Nth(i,1) = &DefenseMod
         {
            sum = sum + Nth(i,2);
         }
      }
      
      return piDefense_bonus + sum;
   }

   GetMeleeDamageBonus()
   {
      local i, sum;
      sum = 0;

      foreach i in plEnchantmentMods
      {
         if Nth(i,1) = &WeaponDamageMod
         {
            sum = sum + Nth(i,2);
         }
      }

      return piDamage_bonus + sum;
   }

   IsPlural()
   {
      return TRUE;
   }

   GetPrefixes()
   {
      return [[&DefenseMod,100],
              [&OffenseMod,100],
              [&WeaponDamageMod,100],
              [&ManaMod,200],
              [&VigorMod,300],
              [&BludgeonEmpowermentMod,100],
              [&ThrustEmpowermentMod,100],
              [&PierceEmpowermentMod,100],
              [&PunchEmpowermentMod,300],
              [&SlashEmpowermentMod,100]];
   }
   
   GetSuffixes()
   {
      return [[&FireResistanceMod,100],
              [&ColdResistanceMod,100],
              [&ShockResistanceMod,100],
              [&AcidResistanceMod,100],
              [&PierceResistanceMod,100],
              [&SlashResistanceMod,100],
              [&BludgeonResistanceMod,100],
              [&CasterIntMod,50],
              [&CasterMystMod,50]];
   }

   GetResistanceModifiers()
   {
      if Send(SETTINGS_OBJECT,@GetDirectArmorEnabled)
      {
         propagate;
      }

      return [ [ATCK_WEAP_ALL,2]
               ];
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
