% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
ForgetPotionBestiary is ForgetPotion

constants:

   include blakston.khd

resources:

   forgetpotionbestiary_desc_rsc = \
      "The knowledge of how life and biology functions is too heavy "
      "on the mind. Drink this potion to sink into oblivion by "
      "forgetting all bestiary skills and spells."

   forgetpotionbestiary_forget_skill = \
      "The art of '%s' slips from your mind!"

classvars:

   viForget_school = SS_BESTIARY
   vrDesc = forgetpotionbestiary_desc_rsc
   
   viValue_average = 130000

properties:

messages:

   QuaffTimer()
   {
      local i, lSpells, oSpell, iSpellnum, Skills, oSkill, iSkillNum;

      ptQuaff = $;
      lSpells = Send(poApply_target,@GetSpellList);
      piCount = 0;
      
      foreach i in lSpells
      {
         iSpellNum = send(poApply_target,@DecodeSpellNum,#compound=i);
         oSpell = send(SYS,@FindSpellByNum,#num=iSpellNum);

         if Send(oSpell,@GetSchool) = viForget_school
            AND Send(oSpell,@CanForget)
         {
            send(poApply_target,@RemoveSpell,#num=iSpellNum);
            send(poApply_target,@MsgSendUser,#message_rsc=forgetpotion_forget_spell,
                 #parm1=send(oSpell,@GetName));
            piCount = piCount + 1;
         }
      }

      Skills = Send(poApply_target,@GetSkillList);
      foreach i in Skills
      {
         iSkillNum = Send(poApply_target,@DecodeSkillNum,#compound=i);

         oSkill = Send(SYS,@FindSkillByNum,#num=iSkillNum);

         if Send(oSkill,@GetSchool) <> viForget_school
            OR Send(oSkill,@IsUtilitySkill)
         {
            continue;
         }

         % Delete all skills!
         Send(poApply_target,@RemoveSkill,#num=iSkillNum);
         Send(poApply_target,@MsgSendUser,
               #message_rsc=forgetpotionbestiary_forget_skill,
               #parm1=Send(oSkill,@GetName));
         piCount = piCount + 1;
      }
      
      if piCount = 0
      {
         Send(poApply_target,@MsgSendUser,#message_rsc=forgetpotion_nothing_happens);
      }
      else
      {
         send(self,@DoSideEffects);
         send(poApply_target,@ToCliStats,#group=3);
      }
      
      Send(self,@Delete);
      
      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
