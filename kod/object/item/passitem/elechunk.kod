% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
ElementalChunk is PassiveItem

constants:

   include blakston.khd

resources:

   ElementalChunk_icon_rsc = Ore.bgf
   ElementalChunk_name_rsc = "chunk of elemental magma"
   ElementalChunk_desc_rsc = "This is a huge chunk of cooling elemental magma."

   hurl_chunk_down = \
      "~BThe %s hits the ground and explodes!"
   chunk_explode = \
      "~B%s hurls a %s to the ground, causing it to explode!"

classvars:
   
   vrName = ElementalChunk_name_rsc
   vrDesc = ElementalChunk_desc_rsc
   vrIcon = ElementalChunk_icon_rsc

   viValue_average = 12000
   viWeight = 600
   viBulk = 600

   viBuffSpellNum = SID_HEAT_STORM_BUFF
   viElement = ATCK_SPELL_FIRE
   viChunkColor = 77

properties:

   piSpellPower = 50

messages:

   Constructed()
   {
      if viChunkColor <> 0
      {
         piItem_flags = viChunkColor;
      }
      propagate;
   }

   GetElement()
   {
      return viElement;
   }

   Dropped(by=$,where=$)
   {
      if where <> $
         AND IsClass(where,&Room)
      {
         if by <> $
            AND IsClass(by,&Player)
         {
            Send(by,@MsgSendUser,#message_rsc=hurl_chunk_down,#parm1=Send(self,@GetName));
            Post(self,@Explode,#who=by);
         }
      }
      return;
   }

   Explode(who=$)
   {
      local i, oSpell;

      if poOwner <> $
         AND IsClass(poOwner,&Room)
      {
         oSpell = Send(SYS,@FindSpellByNum,#num=viBuffSpellNum);
         
         if oSpell = $
         {
            return;
         }

         foreach i in Send(poOwner,@GetHolderActive)
         {
            if IsClass(First(i),&Player)
            {
               if First(i) <> who
               {
                  Send(First(i),@MsgSendUser,#message_rsc=chunk_explode,#parm1=Send(who,@GetName),#parm2=Send(self,@GetName));
               }
               
               if NOT Send(who,@IsEnchanted,#what=oSpell)
               {
                  Send(oSpell,@CastSpell,#who=self,#lTargets=[First(i)],#iSpellPower=piSpellPower);
               }
            }
         }
         Post(self,@Delete);
      }

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

