% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Scepter is Weapon

constants:

   include blakston.khd

resources:

   scepter_name_rsc = "scepter"
   scepter_icon_rsc = scepter.bgf
   scepter_desc_rsc = \
      "This scepter is topped by a large blue orb "
      "which acts as a catalyst for magical energies. "
      "Mages can often be seen wielding a scepter when casting spells or leading minions, "
      "but in desperate situations, it can also act as a bludgeoning implement."

   scepter_window_overlay_rsc = povscepter.bgf
   scepter_player_overlay = scepterov.bgf

classvars:

   vrName = scepter_name_rsc
   vrIcon = scepter_icon_rsc
   vrDesc = scepter_desc_rsc

   % Scepters are high quality bludgeon weapons
   viWeaponType = WEAPON_TYPE_BLUDGEON
   viWeaponQuality = WEAPON_QUALITY_HIGH

   viProficiency_Needed = SKID_PROFICIENCY_MACE

   viValue_average = 1300
   viWeight = 60
   viBulk = 60

   vrWeapon_window_overlay = scepter_window_overlay_rsc
   vrWeapon_overlay = scepter_player_overlay

   viGround_group = 1
   viInventory_group = 3
   viBroken_group = 2

   viHits_init_min = 50
   viHits_init_max = 150
   
   % Chance to apply knockbacks, stuns, and bleeds
   viCriticalHitRate = 100
   vbCanStun = TRUE
   
   viLeadershipBonus = 20

properties:

   piAttack_type = ATCK_WEAP_MAGIC + ATCK_WEAP_BLUDGEON

   piDamageBonus = -4
   piHitBonus = -125

messages:

   GetPrefixes()
   {
      return [[&MinionAttackSpeedMod,500],
              [&InsectSlayerMod,100],
              [&ManaMod,300],
              [&VigorMod,200],
              [&WeaponThrustRendMod,100],
              [&WeaponBludgeonRendMod,100],
              [&WeaponSlashRendMod,100],
              [&WeaponPierceRendMod,100],
              [&ThrustEmpowermentMod,100],
              [&SlashEmpowermentMod,100],
              [&BludgeonEmpowermentMod,100],
              [&PierceEmpowermentMod,100],
              [&CharmEmpowermentMod,200],
              [&FireEmpowermentMod,100],
              [&ColdEmpowermentMod,100],
              [&AcidEmpowermentMod,100],
              [&ShockEmpowermentMod,100],
              [&MagicEmpowermentMod,1]];
   }

   GetSuffixes()
   {
      return [[&FireResistanceMod,100],
              [&ColdResistanceMod,100],
              [&ShockResistanceMod,100],
              [&AcidResistanceMod,100],
              [&PierceResistanceMod,25],
              [&SlashResistanceMod,25],
              [&BludgeonResistanceMod,25],
              [&CasterIntMod,50],
              [&CasterMystMod,50]];
   }
   
   GetBaseSpellModifier()
   {
      return 15;
   }
   
   CheckReplaceWithRelicOfQor()
   {
      if Send(self,@GetMaxHits) = 1
      {
         if poOwner <> $
         {
            Send(poOwner,@NewHold,#what=Create(&RelicOfQor));
            Send(self,@Delete);
         }
      }
      return;
   }
   
   GetSlayerModifiers()
   {
      return [[CREATURE_TYPE_MAGICAL, 10]];
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
